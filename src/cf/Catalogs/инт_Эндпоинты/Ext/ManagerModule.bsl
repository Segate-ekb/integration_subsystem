// BSLLS:DuplicateStringLiteral-off
// BSLLS:Typo-off
////////////////////////////////////////////////////////////////////////////////
// инт_Эндпоинты
//  
////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
// Процедура - Метод создает на форме группу для управления ссылкой и авторизацией удаленного ресурса.
// Метод должен быть вызван приСозданииНаСервере
//
// Параметры:
//  Форма			 -  ФормаКлиентскогоПриложения	 -  Форма на которую добавляются элементы.
//  СсылкаНаЭндпоинт - 	СправочникСсылка.инт_Эндпоинты - Ссылка на эндпоинт, хранимая в заполняемом объекте
//  Родитель		 - Элементы	 -  Элемент формы родитель.
//  Перед			 - ЭлементФормы, Строка	 - Элемент формы(или его имя) Перед которым разместить группу. 
//
// УСТАРЕЛО: Используйте собственную форму справочника инт_Эндпоинты.ФормаОбъекта
//
Процедура ДобавитьИнформациюОбЭндпоинтеНаФорму(Форма, СсылкаНаЭндпоинт, Родитель = Неопределено, Перед = "") Экспорт
	Если Родитель = Неопределено Тогда
		Родитель = Форма;
	КонецЕсли;
	
	Заголовок = инт_ЭндпоинтыКлиентСервер.СформироватьЗаголовок(СсылкаНаЭндпоинт.АдресРесурса);
	
	ГруппаФормы = инт_ДинамическоеФормированиеИнтерфейса.СздГруппаОбычная(Форма,
																			инт_ЭндпоинтыКлиентСервер.ИмяГруппыЭндпоинт(),
																			Родитель,
																			Заголовок,
																			1,
																			0,
																			0,
																			Новый Структура("Поведение", ПоведениеОбычнойГруппы.Всплывающая),
																			Перед);
	
	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															инт_ЭндпоинтыКлиентСервер.ИмяАдресРесурса(),
															Метаданные.Справочники.инт_Эндпоинты.Реквизиты.АдресРесурса.Тип);

	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															инт_ЭндпоинтыКлиентСервер.ИмяТипАвторизации(),
															Метаданные.Справочники.инт_Эндпоинты.Реквизиты.ТипАвторизации.Тип);
	
	СтруктураСобытийЭндпоинт = Новый Структура("ПриИзменении", "АдресРесурсаПриИзменении");
	инт_ДинамическоеФормированиеИнтерфейса.СздПоле(Форма,
													инт_ЭндпоинтыКлиентСервер.ИмяАдресРесурса(),
													ГруппаФормы,
													"Адрес ресурса",
													1,
													инт_ЭндпоинтыКлиентСервер.ИмяАдресРесурса(),
													,
													СтруктураСобытийЭндпоинт);
	
	СтруктураСобытийТипАвторизации = Новый Структура("ПриИзменении", "ТипАвторизацииПриИзменении");
	инт_ДинамическоеФормированиеИнтерфейса.СздПоле(Форма,
													инт_ЭндпоинтыКлиентСервер.ИмяТипАвторизации(),
													ГруппаФормы,
													"Тип авторизации",
													1,
													инт_ЭндпоинтыКлиентСервер.ИмяТипАвторизации(),
													,
													СтруктураСобытийТипАвторизации);
	
	ГруппаСтраниц = инт_ДинамическоеФормированиеИнтерфейса.СздГруппаСтраниц(Форма,
																			инт_ЭндпоинтыКлиентСервер.ИмяГруппыСтраниц(),
																			ГруппаФормы,
																			,
																			,
																			,
																			Новый Структура("ОтображениеСтраниц", ОтображениеСтраницФормы.Нет));
	СоздатьСтраницуБазовойАвторизации(Форма, ГруппаСтраниц);
	СоздатьСтраницуDigest(Форма, ГруппаСтраниц);
	СоздатьСтраницуBearer(Форма, ГруппаСтраниц);
	СоздатьСтраницуApiKey(Форма, ГруппаСтраниц);
	СоздатьСтраницуTls(Форма, ГруппаСтраниц);
	СоздатьСтраницуБезАвторизации(Форма, ГруппаСтраниц);
	
	ЗаполнитьЗначенияРеквизитовПоСсылке(Форма, СсылкаНаЭндпоинт);
КонецПроцедуры

// Функция - Создать обновить эндпоинт
//
// Параметры:
//  Форма			 - ФормаКлиентскогоПриложения	 -  Форма из которой происходит вызов
//  СсылкаНаЭндпоинт - 	СправочникСсылка.инт_Эндпоинты - Ссылка на эндпоинт, если она есть.
// 
// Возвращаемое значение:
//  СправочникСсылка.инт_Эндпоинты - Созданный(обновленный) элемент.
//
Функция СоздатьОбновитьЭндпоинт(Форма, СсылкаНаЭндпоинт) Экспорт
	Если ЗначениеЗаполнено(СсылкаНаЭндпоинт) Тогда
		ЭндпоинтОбъект = СсылкаНаЭндпоинт.ПолучитьОбъект();
	Иначе
		ЭндпоинтОбъект = Справочники.инт_Эндпоинты.СоздатьЭлемент();
	КонецЕсли;
	
	ЭндпоинтОбъект.Наименование = Форма[инт_ЭндпоинтыКлиентСервер.ИмяАдресРесурса()];
	ЭндпоинтОбъект.АдресРесурса = Форма[инт_ЭндпоинтыКлиентСервер.ИмяАдресРесурса()];
	ЭндпоинтОбъект.ТипАвторизации = Форма[инт_ЭндпоинтыКлиентСервер.ИмяТипАвторизации()];
	
	ЭндпоинтОбъект.Записать();

	// Basic авторизация
	ОбновитьВБезопасномХранилище(Форма, ЭндпоинтОбъект.Ссылка, "Basic_Пользователь");
	ОбновитьВБезопасномХранилище(Форма, ЭндпоинтОбъект.Ссылка, "Basic_Пароль", "Basic_ПарольИзменен");
	
	// Digest авторизация
	ОбновитьВБезопасномХранилищеЕслиЕсть(Форма, ЭндпоинтОбъект.Ссылка, "Digest_Пользователь");
	ОбновитьВБезопасномХранилищеЕслиЕсть(Форма, ЭндпоинтОбъект.Ссылка, "Digest_Пароль", "Digest_ПарольИзменен");
	
	// Bearer авторизация
	ОбновитьВБезопасномХранилище(Форма, ЭндпоинтОбъект.Ссылка, "Bearer_Токен", "Bearer_ТокенИзменен");
	
	// ApiKey авторизация
	ОбновитьВБезопасномХранилищеЕслиЕсть(Форма, ЭндпоинтОбъект.Ссылка, "ApiKey_ИмяЗаголовка");
	ОбновитьВБезопасномХранилищеЕслиЕсть(Форма, ЭндпоинтОбъект.Ссылка, "ApiKey_Значение", "ApiKey_ЗначениеИзменено");
	
	// Tls авторизация
	ОбновитьВБезопасномХранилищеЕслиЕсть(Форма, ЭндпоинтОбъект.Ссылка, "Tls_Сертификат", "Tls_СертификатИзменен");
	ОбновитьВБезопасномХранилищеЕслиЕсть(Форма, ЭндпоинтОбъект.Ссылка, "Tls_ПарольСертификата", "Tls_ПарольСертификатаИзменен");
	
	Возврат ЭндпоинтОбъект.Ссылка;
КонецФункции

// Процедура - Удалить эндпоинт
//
// Параметры:
//  Эндпоинт - 	СправочникСсылка.инт_Эндпоинты - Ссылка на эндпоинт
//
Процедура УдалитьЭндпоинт(Эндпоинт) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Эндпоинт) Тогда
		Возврат;
	КонецЕсли;
	
	ЭндпоинтОбъект = Эндпоинт.ПолучитьОбъект();
	
	ЭндпоинтОбъект.УстановитьПометкуУдаления(Истина);
	
КонецПроцедуры

// Функция - Структура параметров соединения
//
// Параметры:
//  Эндпоинт - 	СправочникСсылка.инт_Эндпоинты - Ссылка на эндпоинт
// 
// Возвращаемое значение:
//  Структура - Структура параметров соединения
//		* АдресРесурса - Строка - Строка адреса удаленного ресурса
//		* Сессия - Структура - см. инт_Коннектор.НоваяСессия()
//		* ПроверятьSSL - Булево - Проверять SSL сертификат сервера
//		* Таймаут - Число - Таймаут соединения в секундах
//
Функция СтруктураПараметровСоединения(Эндпоинт) Экспорт
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("АдресРесурса", Эндпоинт.АдресРесурса);
	СтруктураПараметров.Вставить("Сессия", ПолучитьСессию(Эндпоинт));
	СтруктураПараметров.Вставить("ПроверятьSSL", Эндпоинт.ПроверятьSSL);
	СтруктураПараметров.Вставить("Таймаут", ?(Эндпоинт.ТаймаутСоединения > 0, Эндпоинт.ТаймаутСоединения, 30));
	
	Возврат СтруктураПараметров;
КонецФункции

// Возвращает структуру аутентификации для HTTP-коннектора.
//
// Параметры:
//  Эндпоинт - СправочникСсылка.инт_Эндпоинты - Ссылка на эндпоинт
//
// Возвращаемое значение:
//  Структура, Неопределено - Структура аутентификации или Неопределено для анонимного
//
Функция ПолучитьАутентификацию(Эндпоинт) Экспорт
	Возврат ЗаполнитьАутентификацию(Эндпоинт);
КонецФункции

// Функция - Проверить подключение к эндпоинту
//
// Параметры:
//  Эндпоинт - СправочникСсылка.инт_Эндпоинты - Ссылка на эндпоинт
// 
// Возвращаемое значение:
//  Структура - Результат проверки:
//		* Успех - Булево - Признак успешности подключения
//		* КодСостояния - Число - HTTP код ответа (0 если не удалось подключиться)
//		* Сообщение - Строка - Текст ошибки или информация об успехе
//
Функция ПроверитьПодключение(Эндпоинт) Экспорт
	Результат = Новый Структура("Успех, КодСостояния, Сообщение", Ложь, 0, "");
	
	Попытка
		ПараметрыСоединения = СтруктураПараметровСоединения(Эндпоинт);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Таймаут", ПараметрыСоединения.Таймаут);
		ДополнительныеПараметры.Вставить("ПроверятьSSL", ПараметрыСоединения.ПроверятьSSL);
		
		// Сначала пробуем HEAD, если не поддерживается - GET
		Попытка
			Ответ = инт_КоннекторHTTP.Head(ПараметрыСоединения.АдресРесурса, 
											ДополнительныеПараметры, 
											ПараметрыСоединения.Сессия);
		Исключение
			// HEAD может быть не разрешен, пробуем GET
			Ответ = инт_КоннекторHTTP.Get(ПараметрыСоединения.АдресРесурса, 
											ДополнительныеПараметры, 
											ПараметрыСоединения.Сессия);
		КонецПопытки;
		
		Результат.КодСостояния = Ответ.КодСостояния;
		
		Если Ответ.КодСостояния >= 200 И Ответ.КодСостояния < 400 Тогда
			Результат.Успех = Истина;
			Результат.Сообщение = СтрШаблон("Подключение успешно (HTTP %1)", Ответ.КодСостояния);
		Иначе
			Результат.Сообщение = СтрШаблон("Сервер вернул ошибку HTTP %1", Ответ.КодСостояния);
		КонецЕсли;
		
	Исключение
		Результат.Сообщение = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура СоздатьСтраницуБазовойАвторизации(Форма, ГруппаСтраниц)
	Страница = инт_ДинамическоеФормированиеИнтерфейса.СздСтраница(Форма,
																	Строка(Перечисления.инт_ТипыАвторизации.Basic),
																	ГруппаСтраниц, ,
																	1);
	
	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															"Basic_Пользователь",
															инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаСтрока());

	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															"Basic_Пароль",
															инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаСтрока());

	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															"Basic_ПарольИзменен",
															инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаБулево());
	
	инт_ДинамическоеФормированиеИнтерфейса.СздПоле(Форма,
													"Basic_Пользователь",
													Страница,
													"Пользователь",
													1,
													"Basic_Пользователь");
	СтруктураСобытийПароль = Новый Структура("ПриИзменении", "Basic_ПарольПриИзменении");
	инт_ДинамическоеФормированиеИнтерфейса.СздПоле(Форма,
													"Basic_Пароль",
													Страница,
													"Пароль",
													1,
													"Basic_Пароль",
													Новый Структура("РежимПароля", Истина),
													СтруктураСобытийПароль);
	
КонецПроцедуры

Процедура СоздатьСтраницуDigest(Форма, ГруппаСтраниц)
	Страница = инт_ДинамическоеФормированиеИнтерфейса.СздСтраница(Форма,
																	Строка(Перечисления.инт_ТипыАвторизации.Digest),
																	ГруппаСтраниц, ,
																	1);
	
	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															"Digest_Пользователь",
															инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаСтрока());

	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															"Digest_Пароль",
															инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаСтрока());

	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															"Digest_ПарольИзменен",
															инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаБулево());
	
	инт_ДинамическоеФормированиеИнтерфейса.СздПоле(Форма,
													"Digest_Пользователь",
													Страница,
													"Пользователь",
													1,
													"Digest_Пользователь");
	СтруктураСобытийПароль = Новый Структура("ПриИзменении", "Digest_ПарольПриИзменении");
	инт_ДинамическоеФормированиеИнтерфейса.СздПоле(Форма,
													"Digest_Пароль",
													Страница,
													"Пароль",
													1,
													"Digest_Пароль",
													Новый Структура("РежимПароля", Истина),
													СтруктураСобытийПароль);
	
КонецПроцедуры

Процедура СоздатьСтраницуApiKey(Форма, ГруппаСтраниц)
	Страница = инт_ДинамическоеФормированиеИнтерфейса.СздСтраница(Форма,
																	Строка(Перечисления.инт_ТипыАвторизации.ApiKey),
																	ГруппаСтраниц, ,
																	1);
	
	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															"ApiKey_ИмяЗаголовка",
															инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаСтрока());

	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															"ApiKey_Значение",
															инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаСтрока());

	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															"ApiKey_ЗначениеИзменено",
															инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаБулево());
	
	инт_ДинамическоеФормированиеИнтерфейса.СздПоле(Форма,
													"ApiKey_ИмяЗаголовка",
													Страница,
													"Имя заголовка",
													1,
													"ApiKey_ИмяЗаголовка");
	СтруктураСобытийЗначение = Новый Структура("ПриИзменении", "ApiKey_ЗначениеПриИзменении");
	инт_ДинамическоеФормированиеИнтерфейса.СздПоле(Форма,
													"ApiKey_Значение",
													Страница,
													"Значение",
													1,
													"ApiKey_Значение",
													Новый Структура("РежимПароля", Истина),
													СтруктураСобытийЗначение);
	
КонецПроцедуры

Процедура СоздатьСтраницуTls(Форма, ГруппаСтраниц)
	Страница = инт_ДинамическоеФормированиеИнтерфейса.СздСтраница(Форма,
																	Строка(Перечисления.инт_ТипыАвторизации.Tls),
																	ГруппаСтраниц, ,
																	1);
	
	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															"Tls_Пользователь",
															инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаСтрока());

	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															"Tls_Сертификат",
															инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаСтрока());

	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															"Tls_СертификатИзменен",
															инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаБулево());
	
	инт_ДинамическоеФормированиеИнтерфейса.СздПоле(Форма,
													"Tls_Пользователь",
													Страница,
													"Пользователь",
													1,
													"Tls_Пользователь");
	СтруктураСобытийПароль = Новый Структура("ПриИзменении", "Tls_СертификатПриИзменении");
	инт_ДинамическоеФормированиеИнтерфейса.СздПоле(Форма,
													"Tls_Сертификат",
													Страница,
													"Сертификат",
													1,
													"Tls_Сертификат",
													Новый Структура("РежимПароля", Истина),
													СтруктураСобытийПароль);
	
КонецПроцедуры

Процедура СоздатьСтраницуBearer(Форма, ГруппаСтраниц)
	Страница = инт_ДинамическоеФормированиеИнтерфейса.СздСтраница(Форма,
																	Строка(Перечисления.инт_ТипыАвторизации.Bearer),
																	ГруппаСтраниц,
																	,
																	1);
	
	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
															"Bearer_Токен",
															инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаСтрока());

	инт_ДинамическоеФормированиеИнтерфейса.СоздатьРеквизит(Форма,
																"Bearer_ТокенИзменен",
																инт_ДинамическоеФормированиеИнтерфейса.ОписаниеТипаБулево());
	СтруктураСобытийBearer_Токен = Новый Структура("ПриИзменении", // BSLLS:LatinAndCyrillicSymbolInWord-off
													"Bearer_ТокенПриИзменении");
	инт_ДинамическоеФормированиеИнтерфейса.СздПоле(Форма,
													"Bearer_Токен",
													Страница,
													"Токен",
													1,
													"Bearer_Токен",
													Новый Структура("РежимПароля", Истина),
													СтруктураСобытийBearer_Токен);
	
КонецПроцедуры

Процедура СоздатьСтраницуБезАвторизации(Форма, ГруппаСтраниц)
	Страница = инт_ДинамическоеФормированиеИнтерфейса.СздСтраница(Форма,
																	Строка(Перечисления.инт_ТипыАвторизации.Анонимный),
																	ГруппаСтраниц,
																	,
																	1);

	Надпись = инт_ДинамическоеФормированиеИнтерфейса.СздДекорацияНадпись(Форма,
																			"НадписьАвторизацияНеТребуется",
																			Страница, "Авторизация не требуется");
	Надпись.ЦветТекста = ЦветаСтиля.ЦветАкцента;
	
КонецПроцедуры

Процедура ЗаполнитьЗначенияРеквизитовПоСсылке(Форма, СсылкаНаЭндпоинт)
	Форма[инт_ЭндпоинтыКлиентСервер.ИмяАдресРесурса()] = СсылкаНаЭндпоинт.АдресРесурса;
	Форма[инт_ЭндпоинтыКлиентСервер.ИмяТипАвторизации()] = ?(ЗначениеЗаполнено(СсылкаНаЭндпоинт.ТипАвторизации),
																					 СсылкаНаЭндпоинт.ТипАвторизации,
																					 Перечисления.инт_ТипыАвторизации.Анонимный);

	// Basic авторизация
	Basic_ПользовательИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(СсылкаНаЭндпоинт, "Basic_Пользователь");
	Basic_ПарольИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(СсылкаНаЭндпоинт, "Basic_Пароль");
	
	Форма["Basic_Пользователь"] = Basic_ПользовательИзХранилища;
	Форма["Basic_Пароль"] = ?(ЗначениеЗаполнено(Basic_ПарольИзХранилища), Форма.УникальныйИдентификатор, "");
	
	// Digest авторизация
	Digest_ПользовательИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(СсылкаНаЭндпоинт, "Digest_Пользователь");
	Digest_ПарольИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(СсылкаНаЭндпоинт, "Digest_Пароль");
	
	Форма["Digest_Пользователь"] = Digest_ПользовательИзХранилища;
	Форма["Digest_Пароль"] = ?(ЗначениеЗаполнено(Digest_ПарольИзХранилища), Форма.УникальныйИдентификатор, "");
	
	// Bearer авторизация
	Bearer_ТокенИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(СсылкаНаЭндпоинт, "Bearer_Токен");
	
	Форма["Bearer_Токен"] = ?(ЗначениеЗаполнено(Bearer_ТокенИзХранилища), Форма.УникальныйИдентификатор, "");
	
	// ApiKey авторизация
	ApiKey_ИмяЗаголовкаИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(СсылкаНаЭндпоинт, "ApiKey_ИмяЗаголовка");
	ApiKey_ЗначениеИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(СсылкаНаЭндпоинт, "ApiKey_Значение");
	
	Форма["ApiKey_ИмяЗаголовка"] = ApiKey_ИмяЗаголовкаИзХранилища;
	Форма["ApiKey_Значение"] = ?(ЗначениеЗаполнено(ApiKey_ЗначениеИзХранилища), Форма.УникальныйИдентификатор, "");

	ИмяГруппыСтраниц = инт_ЭндпоинтыКлиентСервер.ИмяГруппыСтраниц();
	ИмяТипАвторизации = инт_ЭндпоинтыКлиентСервер.ИмяТипАвторизации();
	Форма.Элементы[ИмяГруппыСтраниц].ТекущаяСтраница = Форма.Элементы[Строка(Форма[ИмяТипАвторизации])];
КонецПроцедуры

Функция ПолучитьСессию(Эндпоинт)
	Сессия = инт_КоннекторHTTP.СоздатьСессию();
	Сессия.Вставить("Аутентификация", ЗаполнитьАутентификацию(Эндпоинт));
	
	Возврат Сессия;
	
КонецФункции

Функция ЗаполнитьАутентификацию(Эндпоинт)
	Аутентификация = Новый Структура;
	
	Если Эндпоинт.ТипАвторизации = Перечисления.инт_ТипыАвторизации.Basic Тогда
		Аутентификация = ЗаполнитьАутентификациюBasic(Эндпоинт);
	ИначеЕсли Эндпоинт.ТипАвторизации = Перечисления.инт_ТипыАвторизации.Digest Тогда
		Аутентификация = ЗаполнитьАутентификациюDigest(Эндпоинт);
	ИначеЕсли Эндпоинт.ТипАвторизации = Перечисления.инт_ТипыАвторизации.Bearer Тогда
		Аутентификация = ЗаполнитьАутентификациюBearer(Эндпоинт);
	ИначеЕсли Эндпоинт.ТипАвторизации = Перечисления.инт_ТипыАвторизации.ApiKey Тогда
		Аутентификация = ЗаполнитьАутентификациюApiKey(Эндпоинт);
	ИначеЕсли Эндпоинт.ТипАвторизации = Перечисления.инт_ТипыАвторизации.Tls Тогда
		Аутентификация = ЗаполнитьАутентификациюTls(Эндпоинт);
	ИначеЕсли Эндпоинт.ТипАвторизации = Перечисления.инт_ТипыАвторизации.Анонимный Тогда
		Аутентификация = Неопределено;
	Иначе
		СообщениеОбОшибке = СтрШаблон("Для типа авторизации <%1> не задан алгоритм заполнения аутентификации",
										Эндпоинт.ТипАвторизации);
										
		ВызватьИсключение СообщениеОбОшибке;
	КонецЕсли;
	
	Возврат Аутентификация;
КонецФункции

Функция ЗаполнитьАутентификациюBasic(Эндпоинт)
	Аутентификация = Новый Структура;
	Аутентификация.Вставить("Тип", "Basic");
	
	Basic_ПользовательИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(Эндпоинт, "Basic_Пользователь");
	Basic_ПарольИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(Эндпоинт, "Basic_Пароль");

	Аутентификация.Вставить("Пользователь", Basic_ПользовательИзХранилища);
	Аутентификация.Вставить("Пароль", Basic_ПарольИзХранилища);
	
	Возврат Аутентификация;
КонецФункции

Функция ЗаполнитьАутентификациюDigest(Эндпоинт)
	Аутентификация = Новый Структура;
	Аутентификация.Вставить("Тип", "Digest");
	
	Digest_ПользовательИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(Эндпоинт, "Digest_Пользователь");
	Digest_ПарольИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(Эндпоинт, "Digest_Пароль");

	Аутентификация.Вставить("Пользователь", Digest_ПользовательИзХранилища);
	Аутентификация.Вставить("Пароль", Digest_ПарольИзХранилища);
	
	Возврат Аутентификация;
КонецФункции

Функция ЗаполнитьАутентификациюBearer(Эндпоинт)
	Аутентификация = Новый Структура;
	Аутентификация.Вставить("Тип", "Bearer");
	
	Bearer_ТокенИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(Эндпоинт, "Bearer_Токен");
	
	Аутентификация.Вставить("Токен", Bearer_ТокенИзХранилища);
	
	Возврат Аутентификация;
КонецФункции

Функция ЗаполнитьАутентификациюApiKey(Эндпоинт)
	Аутентификация = Новый Структура;
	Аутентификация.Вставить("Тип", "ApiKey");
	
	ApiKey_ИмяЗаголовкаИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(Эндпоинт, "ApiKey_ИмяЗаголовка");
	ApiKey_ЗначениеИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(Эндпоинт, "ApiKey_Значение");

	Аутентификация.Вставить("ИмяЗаголовка", ApiKey_ИмяЗаголовкаИзХранилища);
	Аутентификация.Вставить("Значение", ApiKey_ЗначениеИзХранилища);
	
	Возврат Аутентификация;
КонецФункции

Функция ЗаполнитьАутентификациюTls(Эндпоинт)
	Аутентификация = Новый Структура;
	Аутентификация.Вставить("Тип", "Tls");
	
	Tls_СертификатИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(Эндпоинт, "Tls_Сертификат");
	Tls_ПарольСертификатаИзХранилища = ПрочитатьДанныеИзБезопасногоХранилища(Эндпоинт, "Tls_ПарольСертификата");

	Аутентификация.Вставить("Сертификат", Tls_СертификатИзХранилища);
	Аутентификация.Вставить("ПарольСертификата", Tls_ПарольСертификатаИзХранилища);
	
	Возврат Аутентификация;
КонецФункции

Функция ПрочитатьДанныеИзБезопасногоХранилища(Владелец, Ключ)
	УстановитьПривилегированныйРежим(Истина); // BSLLS:SetPrivilegedMode-off
	Возврат ИТКВ_БСП.ПрочитатьДанныеИзБезопасногоХранилища(Владелец, Ключ);
КонецФункции

Процедура ОбновитьВБезопасномХранилище(Форма, Ссылка, Идентификатор, ИмяРеквизитаОтслеживанияИзменений = "")
 Если ИмяРеквизитаОтслеживанияИзменений = "" ИЛИ Форма[ИмяРеквизитаОтслеживанияИзменений] Тогда
  УстановитьПривилегированныйРежим(Истина); // BSLLS:SetPrivilegedMode-off
  ИТКВ_БСП.ЗаписатьДанныеВБезопасноеХранилище(Ссылка, Форма[Идентификатор], Идентификатор);
  
  УстановитьПривилегированныйРежим(Ложь);
  Если Не ИмяРеквизитаОтслеживанияИзменений = "" Тогда
  	Форма[Идентификатор] = ?(ЗначениеЗаполнено(Форма[Идентификатор]), Форма.УникальныйИдентификатор, "");
	Форма[ИмяРеквизитаОтслеживанияИзменений] = Ложь;
  КонецЕсли;
 КонецЕсли;
КонецПроцедуры

// Процедура - Обновить значение в безопасном хранилище, если реквизит присутствует на форме
// Используется для динамически создаваемых реквизитов, которых может не быть на форме
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//  Ссылка - СправочникСсылка.инт_Эндпоинты - Ссылка на эндпоинт
//  Идентификатор - Строка - Идентификатор реквизита
//  ИмяРеквизитаОтслеживанияИзменений - Строка - Имя реквизита отслеживания изменений
//
Процедура ОбновитьВБезопасномХранилищеЕслиЕсть(Форма, Ссылка, Идентификатор, ИмяРеквизитаОтслеживанияИзменений = "")
	Попытка
		// Проверяем наличие реквизита на форме
		ЗначениеРеквизита = Форма[Идентификатор];
		ОбновитьВБезопасномХранилище(Форма, Ссылка, Идентификатор, ИмяРеквизитаОтслеживанияИзменений);
	Исключение
		// Реквизит отсутствует на форме, пропускаем
	КонецПопытки;
КонецПроцедуры

#КонецОбласти

#КонецЕсли

// BSLLS:CommentedCode-off
///////////////////////////////////////////////////////////////////////////////////////////
// Для интеграции справочника на форму, скопируйте дынные команды в модуль формы		 //   
///////////////////////////////////////////////////////////////////////////////////////////
//
//&НаСервере
//Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
//	Справочники.инт_Эндпоинты.ДобавитьИнформациюОбЭндпоинтеНаФорму(ЭтотОбъект,
//																	Объект.СсылкаНаСхему,
//																		Элементы.ГруппаСсылка,
//																			"ОбновитьJsonИзУдаленногоИсточника");
//КонецПроцедуры
// BSLLS:CommentedCode-on
//
//&НаСервере
//Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
//	ТекущийОбъект.СсылкаНаСхему = Справочники.инт_Эндпоинты.СоздатьОбновитьЭндпоинт(ЭтотОбъект, 
//																					ТекущийОбъект.СсылкаНаСхему);
//КонецПроцедуры
//
//&НаКлиенте
//Процедура АдресРесурсаПриИзменении(Элемент)
//	инт_ЭндпоинтыКлиент.АдресРесурсаПриИзменении(ЭтотОбъект["АдресРесурса"], ЭтотОбъект);
//	Модифицированность = Истина;
//КонецПроцедуры   
//
//&НаКлиенте
//Процедура ТипАвторизацииПриИзменении(Элемент)
//	инт_ЭндпоинтыКлиент.ТипАвторизацииПриИзменении(ЭтотОбъект["ТипАвторизации"], ЭтотОбъект);
//	Модифицированность = Истина;
//КонецПроцедуры
//
//&НаКлиенте
//Процедура Basic_ПарольПриИзменении(Элемент)
//	Basic_ПарольИзменен = Истина;
//	Модифицированность = Истина;
//КонецПроцедуры
//
//&НаКлиенте
//Процедура Bearer_ТокенПриИзменении(Элемент)
//	Bearer_ТокенИзменен = Истина;
//	Модифицированность = Истина;
//КонецПроцедуры
//
///////////////////////////////////////////////////////////////////////////////////////////
// BSLLS:CommentedCode-on
