///////////////////////////////////////////////////////////////////////////////
// инт_ПодписчикиKafka (Модуль менеджера)
// Подписчики для отправки в Apache Kafka
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Формирует сообщение для отправки
//
// Параметры:
//  ДанныеСообщения - Соответствие - Данные для сериализации
//  Подписчик - СправочникСсылка.инт_ПодписчикиKafka - Ссылка на подписчика
//  ИдентификаторСообщения - УникальныйИдентификатор - УИД сообщения
//
// Возвращаемое значение:
//  Строка - Сериализованное сообщение
//
Функция СформироватьСообщение(ДанныеСообщения, Подписчик, ИдентификаторСообщения) Экспорт
    
    // TODO: Поддержка Protobuf, Avro
    Возврат инт_КоннекторHTTP.ОбъектВJson(ДанныеСообщения);
    
КонецФункции

// Отправляет сообщение подписчику
//
// Параметры:
//  Сообщение - Структура - Данные сообщения (ИдентификаторСообщения, Подписчик, СформированноеСообщение)
//  ОтложенныеДействия - Соответствие - Для пакетной обработки
//
Процедура ОтправитьСообщение(Сообщение, ОтложенныеДействия) Экспорт
    
    Подписчик = Сообщение.Подписчик;
    
    Если Подписчик.ПакетнаяОбработка Тогда
        // Накапливаем сообщения для пакетной отправки
        МассивСообщений = ОтложенныеДействия.Получить(Подписчик);
        Если МассивСообщений = Неопределено Тогда
            МассивСообщений = Новый Массив;
            ОтложенныеДействия.Вставить(Подписчик, МассивСообщений);
        КонецЕсли;
        
        СтруктураСообщения = инт_РаботаСКафка.ПолучитьСтруктуруСообщенияДляПакетнойОтправки();
        СтруктураСообщения.message = Сообщение.СформированноеСообщение;
        МассивСообщений.Добавить(СтруктураСообщения);
    Иначе
        ИмяТопика = Подписчик.ИмяТопика;
        Брокеры = Подписчик.Эндпоинт.АдресРесурса;
        ТипСжатия = Подписчик.ТипСжатия;
        
        инт_РаботаСКафка.ОтправитьСообщение(Сообщение.СформированноеСообщение, Брокеры, ИмяТопика, , , , ТипСжатия);
    КонецЕсли;
    
КонецПроцедуры

// Отправка накопленного пакета (вызывается в конце обработки потока)
//
// Параметры:
//  Подписчик - СправочникСсылка.инт_ПодписчикиKafka - Подписчик
//  МассивСообщений - Массив - Накопленные сообщения
//
Процедура ОтправитьПакет(Подписчик, МассивСообщений) Экспорт
    
    Брокеры = Подписчик.Эндпоинт.АдресРесурса;
    ТипСжатия = Подписчик.ТипСжатия;
    инт_РаботаСКафка.ОтправитьСообщенияПакетом(МассивСообщений, Брокеры, Подписчик.ИмяТопика, ТипСжатия);
    
КонецПроцедуры

#КонецОбласти
