///////////////////////////////////////////////////////////////////////////////
// инт_ПодписчикиJRPC (Модуль менеджера)
// Подписчики для отправки по JSON-RPC 2.0
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Формирует сообщение для отправки
//
// Параметры:
//  ДанныеСообщения - Соответствие - Данные для сериализации
//  Подписчик - СправочникСсылка.инт_ПодписчикиJRPC - Ссылка на подписчика
//  ИдентификаторСообщения - УникальныйИдентификатор - УИД сообщения
//
// Возвращаемое значение:
//  Строка - Сериализованное сообщение
//
Функция СформироватьСообщение(ДанныеСообщения, Подписчик, ИдентификаторСообщения) Экспорт
    
    СтруктураПотока = РегистрыСведений.инт_ОчередьИсходящихСообщений.ПолучитьДанныеОчередиПоИдентификатору(ИдентификаторСообщения, "ПотокДанных");
    
    Если НЕ СтруктураПотока.Свойство("ПотокДанных") Тогда
        ВызватьИсключение СтрШаблон("Поток данных в сообщении с идентификатором <%1> не найден.", ИдентификаторСообщения);
    КонецЕсли;
    
    ИдентификаторПотока = инт_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктураПотока.ПотокДанных, "Код");
    
    Если ИдентификаторПотока = Неопределено Тогда
        ВызватьИсключение "У потока данных отсутствует Идентификатор Потока (Код).";
    КонецЕсли;
    
    // Формат JSON-RPC 2.0
    СообщениеJRPC = Новый Структура;
    СообщениеJRPC.Вставить("jsonrpc", "2.0");
    СообщениеJRPC.Вставить("method", Строка(ИдентификаторПотока));
    СообщениеJRPC.Вставить("params", ДанныеСообщения);
    СообщениеJRPC.Вставить("id", Строка(ИдентификаторСообщения));
    
    Возврат инт_КоннекторHTTP.ОбъектВJson(СообщениеJRPC);
    
КонецФункции

// Отправляет сообщение подписчику
//
// Параметры:
//  Сообщение - Структура - Данные сообщения (ИдентификаторСообщения, Подписчик, СформированноеСообщение)
//  ОтложенныеДействия - Соответствие - Для пакетной обработки
//
Процедура ОтправитьСообщение(Сообщение, ОтложенныеДействия) Экспорт
    
    ДанныеСообщения = РегистрыСведений.инт_ОчередьИсходящихСообщений.ПолучитьДанныеОчередиПоИдентификатору(Сообщение.ИдентификаторСообщения, "ИсходныеДанные");

    Если ДанныеСообщения = Неопределено Тогда
        ВызватьИсключение СтрШаблон("Данные сообщения с идентификатором <%1> не найдены.", Сообщение.ИдентификаторСообщения);
    КонецЕсли;
    
    Подписчик = Сообщение.Подписчик;
    
    СтруктураПараметровСоединения = Справочники.инт_Эндпоинты.СтруктураПараметровСоединения(Подписчик.Эндпоинт);
    
    Заголовки = Новый Соответствие;
    Заголовки.Вставить("Content-Type", "application/json");
    
    Ответ = инт_КоннекторHTTP.Post(
        СтруктураПараметровСоединения.АдресРесурса, 
        Сообщение.СформированноеСообщение, 
        Заголовки, 
        СтруктураПараметровСоединения.Сессия
    );
    
    // Постобработка по коду ответа
    ВыполнитьПостПроцессинг(Подписчик, Ответ, ДанныеСообщения.ИсходныеДанные);
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет постобработку по коду состояния HTTP-ответа
//
// Параметры:
//  Подписчик - СправочникСсылка.инт_ПодписчикиJRPC - Ссылка на подписчика
//  Ответ - Структура - Ответ HTTP-сервера (КодСостояния, Тело)
//  ИсходныеДанные - ОпределяемыйТип.инт_ИсходныеДанные - Ссылка на исходные данные сообщения
//
Процедура ВыполнитьПостПроцессинг(Подписчик, Ответ, ИсходныеДанные = Неопределено)
    
    КодСостояния = Ответ.КодСостояния;
    ДвоичныеДанныеEPF = ПолучитьОбработчикПоКодуСостояния(КодСостояния, Подписчик);
    
    Попытка
        Исполнить(ДвоичныеДанныеEPF, Ответ, ИсходныеДанные, Истина);
    Исключение
        ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()); // BSLLS:DeprecatedMethods8317-off
        СообщениеОбОшибке = СтрШаблон("При пост-обработке ответа возникла ошибка!
        |
        | Информация об ошибке: %1",
        ПредставлениеОшибки);
        
        ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.Подписчики.ПостОбработкаОтвета",
                                    УровеньЖурналаРегистрации.Ошибка,
                                    ,
                                    ,
                                    СообщениеОбОшибке);
        ВызватьИсключение;
    КонецПопытки;
    
КонецПроцедуры

Функция ПолучитьОбработчикПоКодуСостояния(КодСостояния, Подписчик)
    
    СтрокаПостПроцессинга = Подписчик.ПостПроцессинг.Найти(КодСостояния, "КодСостояния");
    
    Если СтрокаПостПроцессинга <> Неопределено Тогда
        ИдОбъекта = Строка(Подписчик.УникальныйИдентификатор());
        ДвоичныеДанныеИзБД = СтрокаПостПроцессинга.СкомпилированныйОбработчик.Получить();
        ДопПеременные = Новый Структура("Ответ,ИсходныеДанные", "", "");
        
        Возврат инт_ГенераторОбработок.РазрешитьEPFПоКэшу(
            СтрокаПостПроцессинга.Обработчик, ИдОбъекта,
            ДвоичныеДанныеИзБД, ДопПеременные, Подписчик);
    Иначе
        Код = ПолучитьОбработчикПоУмолчанию(КодСостояния);
        Если ПустаяСтрока(Код) Тогда
            Возврат Неопределено;
        КонецЕсли;
        ДопПеременные = Новый Структура("Ответ,ИсходныеДанные", "", "");
        Возврат инт_ГенераторОбработок.СкомпилироватьОбработку(Код, ДопПеременные);
    КонецЕсли;
    
КонецФункции

Функция ПолучитьОбработчикПоУмолчанию(КодСостояния)
    
    Обработчик = "";
    // Здесь через ИначеЕсли можно определить базовое поведение для разных кодов состояний.
    // Если их будет много - вынести в регистр
    Если НЕ (КодСостояния >= 200 И КодСостояния < 300) Тогда // BSLLS:MagicNumber-off ну коды http-то все знают
        Обработчик = "СообщениеОбОшибке = СтрШаблон(""При попытке отправки сообщения код ответа отличается от ожидаемого!
            |	|Код состояния: %1
            |	|ТелоОтвета: %2"", Ответ.КодСостояния, ПолучитьСтрокуИзДвоичныхДанных(Ответ.Тело));
            |ВызватьИсключение Строка(Ответ.Тело);";
    КонецЕсли;
    Возврат Обработчик;
    
КонецФункции

Процедура Исполнить(ДвоичныеДанныеИлиПутьEPF, Ответ, ИсходныеДанные, РазрешитьФиксациюИзменений = Ложь)
    
    Если ДвоичныеДанныеИлиПутьEPF = Неопределено Тогда
        Возврат;
    КонецЕсли;
    
    инт_ГенераторОбработок.ИсполнитьОбработкуПодписчика(ДвоичныеДанныеИлиПутьEPF, Ответ, ИсходныеДанные, РазрешитьФиксациюИзменений);
    
КонецПроцедуры

#КонецОбласти
