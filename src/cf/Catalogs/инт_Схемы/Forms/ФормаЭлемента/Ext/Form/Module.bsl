// BSLLS:LatinAndCyrillicSymbolInWord-off
////////////////////////////////////////////////////////////////////////////////
// <Заголовок модуля: краткое описание и условия применения модуля.>
//  
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Справочники.инт_Эндпоинты.ДобавитьИнформациюОбЭндпоинтеНаФорму(ЭтотОбъект,
																	Объект.СсылкаНаСхему,
																	Элементы.ГруппаСсылка,
																	"ОбновитьJsonИзУдаленногоИсточника");
	ТекстСхемы = Объект.ТекстСхемыJson;
	конс_ПодключаемаяКонсольСервер.ИнициализироватьРедактор(ЭтотОбъект, УникальныйИдентификатор, "РедакторJson");
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	конс_ПодключаемаяКонсольКлиент.ПодключитьРедактор(ЭтаФорма, "РедакторJson");
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Объект.ТекстСхемыJson = конс_ПодключаемаяКонсольКлиент.ПолучитьТекст(ЭтаФорма);
КонецПроцедуры
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.СсылкаНаСхему = Справочники.инт_Эндпоинты.СоздатьОбновитьЭндпоинт(ЭтотОбъект,
																						ТекущийОбъект.СсылкаНаСхему);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РедакторJsonДокументСформирован(Элемент)     
	конс_ПодключаемаяКонсольКлиент.УстановитьТекст(ЭтаФорма, ТекстСхемы);
	конс_ПодключаемаяКонсольКлиент.ОбработчикДокументСформирован(ЭтаФорма);
	конс_ПодключаемаяКонсольКлиент.УстановитьРежимЯзыка(ЭтаФорма, "json");
	Элементы.РедакторJson.Документ.defaultView.setUsingDebugger(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура РедакторJsonПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	конс_ПодключаемаяКонсольКлиент.ОбработчикПриНажатии(ЭтаФорма, ДанныеСобытия);
КонецПроцедуры

&НаКлиенте
Процедура АдресРесурсаПриИзменении(Элемент)
	инт_ЭндпоинтыКлиент.АдресРесурсаПриИзменении(ЭтотОбъект["АдресРесурса"], ЭтотОбъект);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТипАвторизацииПриИзменении(Элемент)
	инт_ЭндпоинтыКлиент.ТипАвторизацииПриИзменении(ЭтотОбъект["ТипАвторизации"], ЭтотОбъект);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура Basic_ПарольПриИзменении(Элемент)
	Basic_ПарольИзменен = Истина;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура Bearer_ТокенПриИзменении(Элемент)
	Bearer_ТокенИзменен = Истина;
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ОбновитьJsonИзУдаленногоИсточника(Команда)
	Если Модифицированность Тогда
		Ответ = Ждать ВопросАсинх("Перед получением схемы необходимо записать объект. Записать?",
																		РежимДиалогаВопрос.ДаНет, 60); // BSLLS:MagicNumber-off
		
		Если Ответ = КодВозвратаДиалога.Да Тогда
			Записать();
		Иначе
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	ОбновитьJsonИзУдаленногоИсточникаНаСервере();
	конс_ПодключаемаяКонсольКлиент.УстановитьТекст(ЭтаФорма, ТекстСхемы, "РедакторJson");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьJsonИзУдаленногоИсточникаНаСервере()
	Объект.ТекстСхемыJson = Справочники.инт_Схемы.ПолучитьТекстJsonДляВнешнейСхемы(Объект);
	ТекстСхемы = Объект.ТекстСхемыJson;
КонецПроцедуры

#КонецОбласти
