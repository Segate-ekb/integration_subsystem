////////////////////////////////////////////////////////////////////////////////
// <Заголовок модуля: краткое описание и условия применения модуля.>
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если Валидация Тогда
		ПроверяемыеРеквизиты.Добавить("СхемаДанных");
		ПроверяемыеРеквизиты.Добавить("ИмяСхемыПакета");
	КонецЕсли;
	
	Если НаправлениеПотока = Перечисления.инт_НаправлениеПотокаДанных.Исходящий Тогда
		ПроверяемыеРеквизиты.Добавить("Пример");
		ПроверяемыеРеквизиты.Добавить("ОтслеживаемыеРеквизиты");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если НаправлениеПотока = Перечисления.инт_НаправлениеПотокаДанных.Исходящий Тогда
		ПроверитьОтслеживаемыеРеквизиты(Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ОтслеживаемыеРеквизиты) И ЗначениеЗаполнено(Пример) Тогда
		ПримерСсылка = ЗначениеИзСтрокиВнутр(Пример);
		ОтслеживаемыеРеквизиты = ЗначениеВСтрокуВнутр(Справочники.инт_ПотокиДанных.ЗаполнитьДеревоРеквизитовПоСсылке(ПримерСсылка, Неопределено));
	КонецЕсли;
	
	ОбеспечитьСкомпилированныйОбработчик();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура - Обеспечить скомпилированный обработчик
// Проверяет, соответствует ли СкомпилированныйОбработчик текущему ТекстуОбработчика.
// Если текст изменился или EPF отсутствует — компилирует заново.
// Пропускает компиляцию, если она уже выполнена формой (флаг в ДополнительныхСвойствах).
//
Процедура ОбеспечитьСкомпилированныйОбработчик()
	
	// Форма уже скомпилировала EPF — повторно не компилируем.
	Если ДополнительныеСвойства.Свойство("ОбработчикСкомпилирован") Тогда
		Возврат;
	КонецЕсли;
	
	// Нет текста обработчика — очищаем скомпилированный EPF.
	Если Не ЗначениеЗаполнено(ТекстОбработчика) Тогда
		СкомпилированныйОбработчик = Новый ХранилищеЗначения(Неопределено);
		Возврат;
	КонецЕсли;
	
	// Для существующих элементов: если текст не изменился и EPF уже есть — пропускаем.
	Если Не Ссылка.Пустая() Тогда
		ТекстВБазе = Ссылка.ТекстОбработчика;
		Если ТекстОбработчика = ТекстВБазе
			И Ссылка.СкомпилированныйОбработчик.Получить() <> Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Компилируем EPF и сохраняем.
	ДвоичныеДанныеEPF = инт_ГенераторОбработок.СкомпилироватьОбработку(ТекстОбработчика);
	СкомпилированныйОбработчик = Новый ХранилищеЗначения(ДвоичныеДанныеEPF);
	
КонецПроцедуры

Процедура ПроверитьОтслеживаемыеРеквизиты(Отказ)
	Дерево = ЗначениеИзСтрокиВнутр(ОтслеживаемыеРеквизиты);
	
	Если Дерево.Строки.Найти(1, "ОтслеживатьИзменения", Истина) = Неопределено Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Ошибка! Не отслеживается изменение ни одного поля!";
		Сообщение.Поле = "ОпределитьОтслеживаемыеРеквизиты"; //имя реквизита
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
