
///////////////////////////////////////////////////////////////////////////////
// инт_ПотокиДанных.ФормаЭлемента
//  
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	конс_ПодключаемаяКонсольКлиент.ПодключитьРедактор(ЭтаФорма);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	конс_ПодключаемаяКонсольСервер.ИнициализироватьРедактор(ЭтотОбъект, УникальныйИдентификатор, "РедакторКода");
    
    ЗаполнитьСписокВыбораИмяСхемПакета();
    ОбновитьВидимостьЭлементовФормы();

	// Десериализуем Пример в реквизит формы ПримерСсылка
    Если ЗначениеЗаполнено(Объект.Пример) Тогда
        ПримерСсылка = ЗначениеИзСтрокиВнутр(Объект.Пример);
    КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
    // Десериализуем Пример в реквизит формы ПримерСсылка при чтении существующего объекта
    Если ЗначениеЗаполнено(Объект.Пример) Тогда
        ПримерСсылка = ЗначениеИзСтрокиВнутр(Объект.Пример);
    Иначе
        ПримерСсылка = Неопределено;
    КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
    // Сериализуем ПримерСсылка обратно в строку
    Если ЗначениеЗаполнено(ПримерСсылка) Тогда
        ТекущийОбъект.Пример = ЗначениеВСтрокуВнутр(ПримерСсылка);
    Иначе
        ТекущийОбъект.Пример = "";
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
   Объект.ТекстОбработчика = конс_ПодключаемаяКонсольКлиент.ПолучитьТекст(ЭтаФорма);
   
   ВалидироватьПередЗаписью = Истина;
   Если ПараметрыЗаписи.Свойство("ВалидироватьПередЗаписью") Тогда
	   ВалидироватьПередЗаписью = ПараметрыЗаписи.ВалидироватьПередЗаписью;
   КонецЕсли;

   Если ВалидироватьПередЗаписью И Не ПроверкаПрошлаУспешно() Тогда
		Отказ = Истина;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОтветаНаВопросОЗаписи", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, "При проверке потока обнаружены ошибки!
		|Все равно записать?",РежимДиалогаВопрос.ДаНет, 60);
   КонецЕсли;

КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура СхемаДанныхПриИзменении(Элемент)
   	ЗаполнитьСписокВыбораИмяСхемПакета();
	УстановитьСхемуРезультата();
КонецПроцедуры

&НаКлиенте
Процедура ПримерПриИзменении(Элемент)
	// Сериализуем ПримерСсылка в Объект.Пример для обновления отслеживаемых реквизитов
	Если ЗначениеЗаполнено(ПримерСсылка) Тогда
		Объект.Пример = ПримерСсылкаВСтроку(ПримерСсылка);
	Иначе
		Объект.Пример = "";
	КонецЕсли;
	ПерезаполнитьОтслеживаемыеРеквизиты();
	ВизуальныеНастройкиЭлементовФормСвязанныхСОтслеживаемымиРеквизитами();
    Объект.ТекстОбработчика = конс_ПодключаемаяКонсольКлиент.ПолучитьТекст(ЭтаФорма);
    РедакторКодаДокументСформирован(Элементы.РедакторКода);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПримерСсылкаВСтроку(Ссылка)
	Возврат ЗначениеВСтрокуВнутр(Ссылка);
КонецФункции

&НаКлиенте
Процедура РедакторКодаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	конс_ПодключаемаяКонсольКлиент.ОбработчикПриНажатии(ЭтаФорма, ДанныеСобытия);
		
КонецПроцедуры

&НаКлиенте
Процедура РедакторКодаДокументСформирован(Элемент)
	конс_ПодключаемаяКонсольКлиент.ОбработчикДокументСформирован(ЭтаФорма);
	ДобавитьПеременные();
	УстановитьСхемуРезультата();
	конс_ПодключаемаяКонсольКлиент.ЗагрузитьПользовательскийКонтекст(ЭтаФорма);
	конс_ПодключаемаяКонсольКлиент.ОчиститьТекст(ЭтаФорма);
	конс_ПодключаемаяКонсольКлиент.УстановитьТекст(ЭтаФорма, Объект.ТекстОбработчика);
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ПроверитьПоПримеру(Команда)
	ОчиститьСообщения();
	Объект.ТекстОбработчика = конс_ПодключаемаяКонсольКлиент.ПолучитьТекст(ЭтаФорма);
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Если ПроверкаПрошлаУспешно() Тогда
		ВремяВыполнения = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("Проверка прошла успешно!
		|Время выполнения: %1 мс.", ВремяВыполнения);
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьОтслеживаемыеРеквизиты(Команда)
	ДопПараметры = Новый Структура("ОтслеживаемыеРеквизиты, СсылкаНаОбъект", Объект.ОтслеживаемыеРеквизиты, ПримерСсылка);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияФормыРедактированияОтслеживаемыхРеквизитов", ЭтотОбъект);
	ОткрытьФорму("Справочник.инт_ПотокиДанных.Форма.РедактированиеОтслеживаемыхРеквизитов",ДопПараметры,ЭтотОбъект,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


&НаКлиенте
Функция СобратьОписаниеОжидаемогоРезультата() Экспорт;
    
   // TODO: !!!! Собирать описание ожидаемого результата, по OpenApi Схеме!!!!
   
   Результат = Новый структура;
   
   Результат.Вставить("ref", "classes.Соответствие");
   Результат.Вставить("description", "Соответствие содержащее сериализованную и подготовленную информацию, которая сможет пройти валидацию по схеме.");
   
   СтруктураПараметра = Новый Структура("ref, name, description", "catalogs.инт_ПотокиДанных", "Dataflow", "Поток данных");
   СтруктураПараметров = Новый Структура("Dataflow", СтруктураПараметра);
   Результат.Вставить("properties", СтруктураПараметров);
   
   Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПерезаполнитьОтслеживаемыеРеквизиты()
	
	Если ЗначениеЗаполнено(Объект.ОтслеживаемыеРеквизиты) Тогда
		ИсходныеДанные = ЗначениеИзСтрокиВнутр(Объект.ОтслеживаемыеРеквизиты);
	Иначе
		ИсходныеДанные = Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПримерСсылка) Тогда
		Объект.ОтслеживаемыеРеквизиты = ЗначениеВСтрокуВнутр(Справочники.инт_ПотокиДанных.ЗаполнитьДеревоРеквизитовПоСсылке(ПримерСсылка, ИсходныеДанные));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТекстПодсказки()
	Возврат "Возврат результата производится через переменную ""Результат""
    |Переменная ""ИсходныеДанные"" Содержит ссылку на обрабатываемый объект. Выберите объект для примера, и вы получите контекстную подсказку основанную на типе примера.";
КонецФункции

&НаСервере
Процедура ПроверитьПоПримеруНаСервере()
	Если Объект.НаправлениеПотока = Перечисления.инт_НаправлениеПотокаДанных.Исходящий Тогда
		Справочники.инт_ПотокиДанных.СформироватьСообщениеПоПотоку(ПримерСсылка, Объект);
	Иначе
		Соответствие = ДесериализоватьИВалидироватьТестовыеДанные();
		Если Соответствие = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Справочники.инт_ПотокиДанных.ОбработатьВходящееСообщениеПоПотоку(Соответствие, Объект, Ложь);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ДесериализоватьИВалидироватьТестовыеДанные()
	Попытка
		Соответствие = инт_КоннекторHTTP.JsonВОбъект(ТестовыеДанные);
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось разобрать Json.";
		Сообщение.Поле = "ТестовыеДанные";
		Сообщение.Сообщить();
		Возврат Неопределено;
	КонецПопытки;
	
	Справочники.инт_ПотокиДанных.ВалидироватьСообщениеПоПотоку(Соответствие, Объект);
	Возврат Соответствие;
КонецФункции
       
&НаСервере
Процедура ЗаполнитьСписокВыбораИмяСхемПакета()
    Элементы.ИмяСхемыПакета.СписокВыбора.ЗагрузитьЗначения(Справочники.инт_Схемы.МассивИменПакетовПоСхеме(Объект.СхемаДанных));
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьЭлементовФормы()
    
	ЭтоВходящийПоток = Объект.НаправлениеПотока = Перечисления.инт_НаправлениеПотокаДанных.Входящий;
	ЭтоИсходящийПоток = Объект.НаправлениеПотока = Перечисления.инт_НаправлениеПотокаДанных.Исходящий;
	
	Элементы.АсинхроннаяОбработка.Видимость = ЭтоВходящийПоток;
    Элементы.ГруппаПодписчики.Видимость = ЭтоИсходящийПоток;
    Элементы.ГруппаПример.Видимость = ЭтоИсходящийПоток;
	Элементы.ТестовыеДанные.Видимость = ЭтоВходящийПоток;
	Элементы.ГруппаПодпискиВходящие.Видимость = ЭтоВходящийПоток;
	
	ВизуальныеНастройкиЭлементовФормСвязанныхСОтслеживаемымиРеквизитами();
КонецПроцедуры

&НаСервере
Процедура ВизуальныеНастройкиЭлементовФормСвязанныхСОтслеживаемымиРеквизитами()
	Элементы.ОпределитьОтслеживаемыеРеквизиты.Видимость = ЗначениеЗаполнено(Объект.ОтслеживаемыеРеквизиты);
	Если Не ЗначениеЗаполнено(Объект.ОтслеживаемыеРеквизиты) Тогда
		Возврат;
	КонецЕсли;
	
	Дерево = ЗначениеИзСтрокиВнутр(Объект.ОтслеживаемыеРеквизиты);
	
	Если НЕ Дерево.Строки.Найти(2, "ОтслеживатьИзменения", Истина) = Неопределено Тогда
		Элементы.ОпределитьОтслеживаемыеРеквизиты.Заголовок = "Список отслеживаемых реквизитов";
		Элементы.ОпределитьОтслеживаемыеРеквизиты.Картинка = БиблиотекаКартинок.ВыбратьЗначение;
		Элементы.ПредупреждениеОбОтслеживаемыхРеквизитах.Видимость = Истина;
		Элементы.ПредупреждениеОбОтслеживаемыхРеквизитах.Заголовок = "Внимание! Отслеживается изменение только части реквизитов!";
		Элементы.ПредупреждениеОбОтслеживаемыхРеквизитах.ЦветТекста = ЦветаСтиля.ЦветАкцента;
	ИначеЕсли Дерево.Строки.Найти(1, "ОтслеживатьИзменения", Истина) = Неопределено Тогда
		Элементы.ОпределитьОтслеживаемыеРеквизиты.Заголовок = "Список отслеживаемых реквизитов";
		Элементы.ОпределитьОтслеживаемыеРеквизиты.Картинка = БиблиотекаКартинок.ВыбратьЗначение;
		Элементы.ПредупреждениеОбОтслеживаемыхРеквизитах.Видимость = Истина;
		Элементы.ПредупреждениеОбОтслеживаемыхРеквизитах.Заголовок = "Ошибка! В списке отслеживаемых реквизитов нет ни одного значения!";
		Элементы.ПредупреждениеОбОтслеживаемыхРеквизитах.ЦветТекста = ЦветаСтиля.ЦветОсобогоТекста;
	Иначе
		Элементы.ОпределитьОтслеживаемыеРеквизиты.Заголовок = "Отслеживаются любые изменения";
		Элементы.ОпределитьОтслеживаемыеРеквизиты.Картинка = БиблиотекаКартинок.СинтаксическийКонтроль;
		Элементы.ПредупреждениеОбОтслеживаемыхРеквизитах.Видимость = Ложь;
	КонецЕсли;
	Элементы.ОпределитьОтслеживаемыеРеквизиты.Доступность = ЗначениеЗаполнено(ПримерСсылка);
КонецПроцедуры

&НаКлиенте
Процедура НаправлениеПотокаПриИзменении(Элемент)
    ОбновитьВидимостьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Функция ПроверкаПрошлаУспешно()
	Успех = Истина; 

	ИнфоОшибки = Неопределено;
	Попытка
		ПроверитьПоПримеруНаСервере();
	Исключение
		ИнфоОшибки = ИнформацияОбОшибке(); 
		Успех = Ложь;
	КонецПопытки;
	
	Возврат Успех;
КонецФункции

//Вынести в отдельную процедуру
&НаКлиенте
Процедура ПослеОтветаНаВопросОЗаписи(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать(Новый Структура("ВалидироватьПередЗаписью", Ложь));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыРедактированияОтслеживаемыхРеквизитов(ОтслеживаемыеРеквизиты, ДополнительныеПараметры) Экспорт
	Если ЗначениеЗаполнено(ОтслеживаемыеРеквизиты) Тогда
		Объект.ОтслеживаемыеРеквизиты = ОтслеживаемыеРеквизиты;
	КонецЕсли;
	
	ВизуальныеНастройкиЭлементовФормСвязанныхСОтслеживаемымиРеквизитами();
КонецПроцедуры

&НаСервере
Процедура ДобавитьПеременные()
	
	конс_ПодключаемаяКонсольСервер.ДобавитьПеременную(ЭтотОбъект, "Результат", Новый Соответствие);
	конс_ПодключаемаяКонсольСервер.ДобавитьПеременную(ЭтотОбъект, "ИсходныеДанные", ПримерСсылка);
	
КонецПроцедуры 


&НаКлиенте
Процедура УстановитьСхемуРезультата()
	Если Не ЗначениеЗаполнено(Объект.СхемаДанных) или Не ЗначениеЗаполнено(Объект.ИмяСхемыПакета) Тогда 
		Возврат;
	КонецЕсли;     
	Схема = ПолучитьТекстСхемы(); 
	конс_ПодключаемаяКонсольКлиент.ЗагрузитьФайлСхемы(ЭтаФорма, Схема); 
	конс_ПодключаемаяКонсольКлиент.ЗагрузитьСхемуПеременной(ЭтаФорма, "Результат", Схема, Объект.ИмяСхемыПакета);

КонецПроцедуры   


&НаКлиенте
Процедура ИмяСхемыПакетаПриИзменении(Элемент)
	УстановитьСхемуРезультата();
КонецПроцедуры


&НаСервере
Функция ПолучитьТекстСхемы()

	Возврат Справочники.инт_Схемы.ПолучитьOpenApiСхему(Объект.СхемаДанных);

КонецФункции 

#КонецОбласти
