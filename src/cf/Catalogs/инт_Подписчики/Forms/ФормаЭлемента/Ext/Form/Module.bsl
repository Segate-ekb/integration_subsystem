////////////////////////////////////////////////////////////////////////////////
// инт_Подписчики
//  
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    ОбновитьВидимостьДоступностьЭлементовФормы();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Справочники.инт_Эндпоинты.ДобавитьИнформациюОбЭндпоинтеНаФорму(ЭтотОбъект,
																	Объект.СсылкаНаСервис,
																		Элементы.ГруппаПараметрыОбщие,
																			"КоличествоПопытокОтправки");
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.СсылкаНаСервис = Справочники.инт_Эндпоинты.СоздатьОбновитьЭндпоинт(ЭтотОбъект,
																					ТекущийОбъект.СсылкаНаСервис);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
  
&НаКлиенте
Процедура ТипПодписчикаПриИзменении(Элемент)
    ОбновитьВидимостьДоступностьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура АдресРесурсаПриИзменении(Элемент)
	инт_ЭндпоинтыКлиент.АдресРесурсаПриИзменении(ЭтотОбъект["АдресРесурса"], ЭтотОбъект);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТипАвторизацииПриИзменении(Элемент)
	инт_ЭндпоинтыКлиент.ТипАвторизацииПриИзменении(ЭтотОбъект["ТипАвторизации"], ЭтотОбъект);
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ИмяТопикаПриИзменении(Элемент)
	ОтобразитьСтатусПроверкиТопика();
КонецПроцедуры

&НаКлиенте
Процедура Basic_ПарольПриИзменении(Элемент)
	Basic_ПарольИзменен = Истина;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура Bearer_ТокенПриИзменении(Элемент)
	Bearer_ТокенИзменен = Истина;
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьВидимостьДоступностьЭлементовФормы()
	Страница = ГруппаПоТипуПодписчика(Объект.ТипПодписчика);
	Элементы.ГруппаПараметрыПоТипуПодписчика.Видимость = Не Страница = Неопределено;
	Если Не Страница = Неопределено Тогда
		Элементы.ГруппаПараметрыПоТипуПодписчика.ТекущаяСтраница = Страница;
	КонецЕсли;
	ОтобразитьСтатусПроверкиТопика();
КонецПроцедуры

&НаСервере
Функция ГруппаПоТипуПодписчика(ТипПодписчика)
	Группа = Неопределено;
	Если ТипПодписчика = Перечисления.инт_ТипыПодписчиков.Kafka Тогда
		Группа = Элементы.ГруппаKafka;
	ИначеЕсли ТипПодписчика = Перечисления.инт_ТипыПодписчиков.ПроизвольныйHTTP Тогда
		Группа = Элементы.ГруппаПроизвольныйHTTP;
	КонецЕсли;
	
	Возврат Группа;
	
КонецФункции

&НаСервере
Процедура ПроверитьОбязательныеПараметрыВСсылкеНаСервис(Отказ)
	Если Объект.СпособПередачиИдентификатораСообщения = Перечисления.инт_СпособыПередачиПараметровПоHTTP.ВПараметреПути
				И СтрНайти(Объект.СсылкаНаСервис, "/{flow_id}") = 0 Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "В ссылке на сервис отсутствует требуемый параметр {flow_id}.";
		Сообщение.Поле = "Объект.СсылкаНаСервис";
		Сообщение.Сообщить();
	КонецЕсли;
	
	Если Объект.СпособПередачиИдентификатораСообщения = Перечисления.инт_СпособыПередачиПараметровПоHTTP.ВПараметреПути
			И СтрНайти(Объект.СсылкаНаСервис, "/{message_id}") = 0 Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "В ссылке на сервис отсутствует требуемый параметр {message_id}.";
		Сообщение.Поле = "Объект.СсылкаНаСервис";
		Сообщение.Сообщить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПостПроцессингОбработчикНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДопПараметры = Новый Структура("ТекстОбработчика", Элемент.ТекстРедактирования);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияРедактированияОбработчика", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.инт_Подписчики.Форма.ФормаРедактированияОбработчика", // BSLLS:Typo-off
					ДопПараметры,
					Элемент,
					,
					,
					,
					ОписаниеОповещения,
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияРедактированияОбработчика(ТекстОбработчика, ДополнительныеПараметры) Экспорт
    Если ТекстОбработчика = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Элементы.ПостПроцессинг.ТекущиеДанные.Обработчик = ТекстОбработчика;
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСтатусПроверкиТопика()
	Если НЕ Объект.ТипПодписчика = Перечисления.инт_ТипыПодписчиков.Kafka Тогда
		Возврат;
	КонецЕсли;
	Брокеры = Объект.СсылкаНаСервис.АдресРесурса;
	
	Если НЕ ЗначениеЗаполнено(Брокеры) Тогда
		Элементы.ДекорацияСтатус.Картинка = БиблиотекаКартинок.ОформлениеКругПустой;
		Элементы.ДекорацияСтатус.Подсказка = "Адрес подключения не заполнен";
		Возврат;
	КонецЕсли;
	Попытка
		ИнформацияОТопике = инт_РаботаСКафка.ПолучитьИнформациюОТопике(Брокеры,Объект.ИмяТопика);
		ТекстОшибки = ИнформацияОТопике.Получить("error");
		Если ТекстОшибки = Неопределено Тогда
			Элементы.ДекорацияСтатус.Картинка = БиблиотекаКартинок.ОформлениеКругЗеленый;
			Элементы.ДекорацияСтатус.Подсказка = "Все настроено корректно!";
			Возврат;
		КонецЕсли;
		
		Если СтрНайти(ТекстОшибки, "Unknown topic") > 0 Тогда
			Элементы.ДекорацияСтатус.Картинка = БиблиотекаКартинок.ОформлениеКругЖелтый;
			Элементы.ДекорацияСтатус.Подсказка = "Топик не найден!";
		КонецЕсли;
	Исключение
	    Элементы.ДекорацияСтатус.Картинка = БиблиотекаКартинок.ОформлениеКругКрасный;
		Элементы.ДекорацияСтатус.Подсказка = "Не удалось получить информацию о топике!";
	КонецПопытки;

КонецПроцедуры

&НаСервере
Процедура СоздатьТопикНаСервере()
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьТопик(Команда)
	СоздатьТопикНаСервере();
КонецПроцедуры

#КонецОбласти
