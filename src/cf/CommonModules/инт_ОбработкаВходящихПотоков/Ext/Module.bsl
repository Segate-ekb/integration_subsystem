////////////////////////////////////////////////////////////////////////////////
// Модуль: инт_ОбработкаВходящихПотоков
//
// Описание:
//  Модуль управления процессом обработки входящих сообщений в рамках подсистемы интеграции.
//  Реализует прием, регистрацию и обработку входящих сообщений от внешних систем.
//
// Основные возможности:
//  - Обработка входящих сообщений по указанному потоку данных
//  - Синхронная и асинхронная обработка сообщений
//  - Регистрация входящих сообщений в очереди для асинхронной обработки
//  - Периодическая обработка очереди входящих сообщений
//
// Режимы обработки:
//  - Синхронный - сообщение обрабатывается немедленно при получении
//  - Асинхронный - сообщение помещается в очередь и обрабатывается регламентным заданием
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура - Обработка входящего сообщения по потоку
//
// Параметры:
//  Сообщение     - Соответствие,Структура,Строка,Число,Булево - Данные входящего сообщения
//  ПотокДанных     -  СправочникСсылка.инт_ПотокиДанных    - Ссылка на входящий поток данных.
//
Процедура ОбработкаВходящегоСообщенияПоПотоку(Сообщение, ПотокДанных, ИдентификаторСообщения=Неопределено) Экспорт
    Если Не ПотокДанных.НаправлениеПотока = Перечисления.инт_НаправлениеПотокаДанных.Входящий Тогда
        СообщениеОбОшибке = СтрШаблон("При попытке обработки входящего сообщения по потоку <%1> возникла ошибка.
        |Нельзя использовать исходящие потоки для входящих сообщений!",ПотокДанных);
        
        ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.ОбработкаВходящихСообщений", УровеньЖурналаРегистрации.Ошибка,,,СообщениеОбОшибке);
    	ВызватьИсключение СообщениеОбОшибке;
    КонецЕсли;
    
    Если ПотокДанных.АсинхроннаяОбработка Тогда
       РегистрыСведений.инт_ОчередьВходящихСообщений.ЗарегистрироватьСообщение(Сообщение, ПотокДанных, ИдентификаторСообщения);
    Иначе
        // Обработаем сразу же, без отправки в очередь.
       Справочники.инт_ПотокиДанных.ОбработатьВходящееСообщениеПоПотоку(Сообщение, ПотокДанных);
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Процедура - Обработка входящей очереди
//
// Описание:
//  Обрабатывает накопленные входящие сообщения из очереди.
//  Получает пачку сообщений из очереди (размер настраивается) и последовательно
//  обрабатывает каждое сообщение.
//
//  Примечание: В текущей реализации обработка однопоточная. Многопоточная обработка
//  входящих сообщений планируется в будущих версиях.
//
//  Вызывается регламентным заданием для периодической обработки очереди.
//
Процедура инт_ОбработкаВходящейОчереди() Экспорт
    РазмерПачки = инт_ОбработкаВходящихПотоковПовтИсп.РазмерПачкиОтправкиСообщений();
    МассивИдентификаторов = РегистрыСведений.инт_ТекущийСтатусВходящихСообщений.ПолучитьИдентификаторыСообщенийКФормированию(РазмерПачки);
    // Пока все однопоточное, потому формировать фоновое задание мы не будем. Потому что я не придумал нормальной схемы параллельной обработки.
    Для Каждого Идентификатор Из МассивИдентификаторов Цикл
    	РегистрыСведений.инт_ОчередьВходящихСообщений.ОбработатьСообщениеПоИдентификатору(Идентификатор);
    КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
