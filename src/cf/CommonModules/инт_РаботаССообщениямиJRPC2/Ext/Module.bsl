///////////////////////////////////////////////////////////////////////////////
// Модуль: инт_РаботаССообщениямиJRPC2
//
// Описание:
//  Модуль для работы с подписчиками типа JSON-RPC 2.0.
//  Реализует формирование и отправку сообщений по протоколу JSON-RPC 2.0.
//
// Основные возможности:
//  - Формирование JSON-RPC 2.0 сообщений с методом и параметрами
//  - Отправка сообщений подписчикам через HTTP
//  - Выполнение пост-обработки ответов от подписчиков
//
// Структура JSON-RPC 2.0 сообщения:
//  {
//    "jsonrpc": "2.0",           // Версия протокола
//    "method": "<Код потока>",   // Имя метода (идентификатор потока)
//    "params": { ... },          // Параметры (данные сообщения)
//    "id": "<UUID>"              // Идентификатор запроса (ID сообщения)
//  }
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Функция - Сформировать сообщение по подписчику
//
// Параметры:
//  ДанныеСообщения        - Соответствие                   - Соответствие содержащее данные для сериализации
//  Подписчик              - СправочникСсылка.инт_Подписчики - Ссылка на подписчика
//  ИдентификаторСообщения - УникальныйИдентификатор        - УИД сообщения
// 
// Возвращаемое значение:
//  Строка - Сериализованное, готовое к отправке подписчику сообщение
//
Функция СформироватьСообщениеПоПодписчику(ДанныеСообщения, Подписчик, ИдентификаторСообщения) Экспорт
    // Пока так. 
    // В теории здесь могут производиться дополнительные манипуляции с сообщением, например, изменение параметров сериализации (форматы дат, локализация и т.д.)
	СоответствиеJRPC = СформироватьСоответствиеJRPC(ДанныеСообщения, ИдентификаторСообщения);
	
	Возврат инт_КоннекторHTTP.ОбъектВJson(СоответствиеJRPC);
	
КонецФункции

// Процедура - Отправить сообщение подписчику
//
// Параметры:
//  Сообщение     - Структура - Данные сообщения 
//
Процедура ОтправитьСообщение(Сообщение) Экспорт
	
	ДанныеСообщения = РегистрыСведений.инт_ОчередьИсходящихСообщений.ПолучитьДанныеОчередиПоИдентификатору(Сообщение.ИдентификаторСообщения, "ИсходныеДанные");

	Если ДанныеСообщения = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Данные сообщения с идентификатором <%1> не найдены.", Сообщение.ИдентификаторСообщения);
	КонецЕсли;

	Подписчик		= Сообщение.Подписчик;
	СтруктураПараметровСоединения = Справочники.инт_Эндпоинты.СтруктураПараметровСоединения(Подписчик.СсылкаНаСервис);
	
	Ответ = инт_КоннекторHTTP.Post(СтруктураПараметровСоединения.АдресРесурса, Сообщение.СформированноеСообщение, , СтруктураПараметровСоединения.Сессия);
	
	Справочники.инт_Подписчики.ВыполнитьПостОбработку(Подписчик, Ответ, ДанныеСообщения.ИсходныеДанные);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция - Сформировать соответствие JRPC
//
// Параметры:
//  ДанныеСообщения         - Соответствие            - Данные сообщения для параметров JRPC
//  ИдентификаторСообщения  - УникальныйИдентификатор - Идентификатор сообщения для поля "id"
//
// Возвращаемое значение:
//  Структура - Структура сообщения JSON-RPC 2.0 со следующими полями:
//    * jsonrpc - Строка - Версия протокола ("2.0")
//    * method  - Строка - Имя метода (код потока данных)
//    * params  - Соответствие - Параметры запроса (данные сообщения)
//    * id      - Строка - Уникальный идентификатор запроса
//
// Описание:
//  Формирует структуру JSON-RPC 2.0 сообщения в соответствии со спецификацией протокола.
//  В качестве имени метода используется код потока данных, к которому относится сообщение.
//
Функция СформироватьСоответствиеJRPC(ДанныеСообщения, ИдентификаторСообщения)
	
	СтруктураПотока		= РегистрыСведений.инт_ОчередьИсходящихСообщений.ПолучитьДанныеОчередиПоИдентификатору(ИдентификаторСообщения, "ПотокДанных");
	
	Если Не СтруктураПотока.Свойство("ПотокДанных") Тогда
		ВызватьИсключение СтрШаблон("Поток данных в сообщении с идентификатором <%1> не найден.", ИдентификаторСообщения);
	КонецЕсли;

	ИдентификаторПотока = ИТКВ_Общий.ЗначениеРеквизитаОбъекта(СтруктураПотока.ПотокДанных, "Код");
	
	Если ИдентификаторПотока = Неопределено Тогда
		ВызватьИсключение "У потока данных отсутствует Идентификатор Потока (Код).";
	КонецЕсли;

	СоответствиеJRPC = Новый Структура;
	СоответствиеJRPC.Вставить("jsonrpc",	"2.0");
	СоответствиеJRPC.Вставить("method",		Строка(ИдентификаторПотока));
	СоответствиеJRPC.Вставить("params",		ДанныеСообщения);
	СоответствиеJRPC.Вставить("id",			Строка(ИдентификаторСообщения));
	
	Возврат СоответствиеJRPC;
	
КонецФункции

#КонецОбласти
