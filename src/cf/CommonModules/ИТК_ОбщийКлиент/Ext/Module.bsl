#Область ПрограммныйИнтерфейс

Процедура ОбработчикПриОткрытии(Отказ) Экспорт
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		
		Отказ = Истина;
		
		Заголовок = ИТК_ТуллкитКлиентСервер.Представление();
		
		Шаблон = НСтр("ru = 'Инструменты %1, не предназначены для запуска в режиме обычного приложения (данный режим не поддерживается)';
						|en = '%1 tools are not designed to run in normal application mode (this mode is not supported)'");
		Текст = СтрШаблон(Шаблон, Заголовок);
		
		ПоказатьПредупреждение( , Текст, , Заголовок);
		
	#КонецЕсли
	
КонецПроцедуры

Функция ОповещениеЗапускПриложения(ДополнительныеПараметры) Экспорт
	
	Возврат (Новый ОписаниеОповещения("ПодключаемыйОбработчикЗапускПриложения", ЭтотОбъект, ДополнительныеПараметры));

КонецФункции

Процедура ПодключаемыйОбработчикЗапускПриложения(Результат, ДополнительныеПараметры) Экспорт
	
	НачатьЗапускПриложения(ПустойОбработчикОповещений(), ДополнительныеПараметры.Команда);

КонецПроцедуры

Функция ПустойОбработчикОповещений() Экспорт
	
	Возврат (Новый ОписаниеОповещения("ПодключаемыйПустойОбработчикОповещений", ЭтотОбъект));
	
КонецФункции

Процедура ПодключаемыйПустойОбработчикОповещений(Результат, ДополнительныеПараметры) Экспорт
	
	ПустойОбработчик = Истина;
	
КонецПроцедуры

Функция ПустойОбработчикОповещенийДополнительныеПараметры(ДополнительныеПараметры) Экспорт
	
	Возврат (Новый ОписаниеОповещения("ПодключаемыйПустойОбработчикОповещенийДополнительныеПараметры", ЭтотОбъект));
	
КонецФункции

Процедура ПодключаемыйПустойОбработчикОповещенийДополнительныеПараметры(ДополнительныеПараметры) Экспорт
	
	ПустойОбработчик = Истина;
	
КонецПроцедуры

Процедура ОткрытьНастройки(Владелец = Неопределено, Инструмент = Неопределено, Раздел = Неопределено) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Инструмент", Инструмент);
	Параметры.Вставить("Раздел", Раздел);
	
	ОткрытьФорму("ОбщаяФорма.ИТК_Настройки", Параметры, Владелец, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ОткрытьЗапросИсточника(Владелец, Колонки, Источник) Экспорт
	
	Если Не ЗначениеЗаполнено(Колонки) Тогда
		
		ПоказатьПредупреждение( , НСтр("ru = 'Нет колонок в источнике (таблице значений)'; en = 'No columns in source (value table)'"), ,
									ИТК_КонсольРазработчикаКлиентСервер.Представление());
		Возврат;
		
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Колонки", Колонки);
	Параметры.Вставить("Источник", Источник);
	
	ОткрытьФорму("Обработка.ИТК_КонсольРазработчика.Форма.ЗапросИсточника", Параметры, Владелец, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Функция ЭтоWindowsКлиент() Экспорт
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	
	Возврат (СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86
	             ИЛИ СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64);
	
КонецФункции

Функция РежимОтладки() Экспорт
	
	Возврат СтрНайти(ПараметрЗапуска, "РежимОтладки");
	
КонецФункции

Функция ИнформацияДляТехническойПоддержки() Экспорт
	
	Результат = Новый Массив;
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	
	Текст = НСтр("ru = 'Клиент:'; en = 'Client:'");
	Результат.Добавить(Текст);
	
	// Версия ОС
	Шаблон = НСтр("ru = 'ОС: %1 (%2)'; en = 'OS: %1 (%2)'");
	Текст = СтрШаблон(Шаблон, СистемнаяИнформация.ТипПлатформы, СистемнаяИнформация.ВерсияОС);
	Результат.Добавить(Текст);
	
	// Язык системы
	Шаблон = НСтр("ru = 'Язык: %1'; en = 'Language: %1'");
	Текст = СтрШаблон(Шаблон, ТекущийЯзыкСистемы());
	Результат.Добавить(Текст);
	
	// Режим запуска
	Шаблон = НСтр("ru = 'Режим запуска: %1'; en = 'Launch mode: %1'");
	Текст = СтрШаблон(Шаблон, ТекущийРежимЗапуска());
	Результат.Добавить(Текст);

	Возврат СтрСоединить(Результат, Символы.ПС);
	
КонецФункции

#КонецОбласти
