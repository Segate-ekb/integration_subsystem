///////////////////////////////////////////////////////////////////////////////
// Модуль: инт_РаботаССообщениямиПроизвольныйHTTP
//
// Описание:
//  Модуль для работы с подписчиками типа "Произвольный HTTP".
//  Реализует формирование и отправку HTTP-запросов с гибкой настройкой параметров.
//
// Основные возможности:
//  - Сериализация сообщений в формат JSON
//  - Отправка POST-запросов с настраиваемыми параметрами
//  - Передача идентификаторов потока и сообщения различными способами:
//    * В параметрах пути URL (например: /api/{flow_id}/messages/{message_id})
//    * В заголовках HTTP (например: X-Flow-Id, X-Message-Id)
//    * В параметрах запроса (query string, например: ?flow_id=xxx&message_id=yyy)
//  - Выполнение пост-обработки ответов от подписчиков
//
// Использование:
//  Подходит для интеграции с REST API внешних систем, где требуется
//  гибкая настройка параметров HTTP-запроса.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Функция - Сформировать сообщение по подписчику
//
// Параметры:
//  ДанныеСообщения     -   Соответствие   - Соттветствие содержащее данные для сериатизации
//  Подписчик         -  СпраочникСсылка.инт_Подписчики    - ссылка на подписчика.
// 
// Возвращаемое значение:
//  Строка - Сериализованное, котовое к отправке подписчику сообщение
//
Функция СформироватьСообщениеПоПодписчику(ДанныеСообщения, Подписчик) Экспорт
    // Пока так. 
    // В теории тут могут быть манипуляции с сообщением. 
	// Сериализация в xml, подсовывание других параметров сериализации(не знаю, форматы дат-там другие и тд)
    // но пока просто в json для mvp
    Возврат инт_КоннекторHTTP.ОбъектВJson(ДанныеСообщения);
		
КонецФункции

// Процедура - Отправить сообщение подписчику
//
// Параметры:
//  Сообщение     - Структура - Данные сообщения 
//
Процедура ОтправитьСообщение(Сообщение) Экспорт
	ДанныеИсходящегоСообщения = РегистрыСведений.инт_ОчередьИсходящихСообщений.ПолучитьДанныеОчередиПоИдентификатору(
																						Сообщение.ИдентификаторСообщения,
																						"ПотокДанных, ИдентификаторСообщения, ИсходныеДанные");
	Подписчик = Сообщение.Подписчик;
	СтруктураПараметровСоединения = Справочники.инт_Эндпоинты.СтруктураПараметровСоединения(Подписчик.СсылкаНаСервис);

	url = СформироватьURLПоДаннымПодписчика(СтруктураПараметровСоединения.АдресРесурса,
												Подписчик, ДанныеИсходящегоСообщения);
	Заголовки = СформироватьЗаголовкиПоДаннымПодписчика(Подписчик, ДанныеИсходящегоСообщения);
	ПараметрыЗапроса = СформироватьПараметрыЗапросаПоДаннымПодписчика(Подписчик, ДанныеИсходящегоСообщения);
	
	ДополнительныеПараметры = Новый Структура("Заголовки, ПараметрыЗапроса", Заголовки, ПараметрыЗапроса);
	
	Ответ = инт_КоннекторHTTP.Post(url, Сообщение.СформированноеСообщение,
																ДополнительныеПараметры,
																СтруктураПараметровСоединения.Сессия);
	
	Справочники.инт_Подписчики.ВыполнитьПостОбработку(Подписчик, Ответ, ДанныеИсходящегоСообщения.ИсходныеДанные);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция - Сформировать URL по данным подписчика
//
// Параметры:
//  url                       - Строка                              - Базовый URL сервиса подписчика
//  Подписчик                 - СправочникСсылка.инт_Подписчики     - Ссылка на подписчика
//  ДанныеИсходящегоСообщения - Структура                           - Данные исходящего сообщения
//
// Возвращаемое значение:
//  Строка - Сформированный URL с подставленными параметрами пути
//
// Описание:
//  Формирует итоговый URL для отправки сообщения, подставляя в шаблон URL
//  идентификаторы потока и сообщения, если настроена передача в параметрах пути.
//  Поддерживаемые макросы: {flow_id}, {message_id}
//
Функция СформироватьURLПоДаннымПодписчика(Знач url, // BSLLS:LatinAndCyrillicSymbolInWord-off
											Подписчик,
											ДанныеИсходящегоСообщения)
	Если НЕ ЗначениеЗаполнено(url) Тогда
		ВызватьИсключение СтрШаблон("Не заполнена ссылка на сервис для подписчика <%1>", Подписчик);
	КонецЕсли;
	
	Если Подписчик.СпособПередачиИдентификатораПотока =
			Перечисления.инт_СпособыПередачиПараметровПоHTTP.ВПараметреПути Тогда
		url = СтрЗаменить(URL, "{flow_id}", ДанныеИсходящегоСообщения.ПотокДанных.Код);
	КонецЕсли;
	
	Если Подписчик.СпособПередачиИдентификатораСообщения =
			Перечисления.инт_СпособыПередачиПараметровПоHTTP.ВПараметреПути Тогда
		url = СтрЗаменить(URL, "{message_id}", ДанныеИсходящегоСообщения.ИдентификаторСообщения);
	КонецЕсли;
	
	Возврат url;
КонецФункции

// Функция - Сформировать заголовки по данным подписчика
//
// Параметры:
//  Подписчик                 - СправочникСсылка.инт_Подписчики - Ссылка на подписчика
//  ДанныеИсходящегоСообщения - Структура                       - Данные исходящего сообщения
//
// Возвращаемое значение:
//  Соответствие - Заголовки HTTP-запроса (ключ - имя заголовка, значение - значение заголовка)
//
// Описание:
//  Формирует заголовки HTTP-запроса, добавляя идентификаторы потока и сообщения
//  в заголовки "flow_id" и "message_id", если настроена передача в заголовках.
//
Функция СформироватьЗаголовкиПоДаннымПодписчика(Подписчик, ДанныеИсходящегоСообщения)
	Заголовки = Новый Соответствие;
	
	Если Подписчик.СпособПередачиИдентификатораПотока =
			Перечисления.инт_СпособыПередачиПараметровПоHTTP.ВЗаголовке Тогда
		Заголовки.Вставить("flow_id", ДанныеИсходящегоСообщения.ПотокДанных.Код);
	КонецЕсли;
	
	Если Подписчик.СпособПередачиИдентификатораСообщения =
			Перечисления.инт_СпособыПередачиПараметровПоHTTP.ВЗаголовке Тогда
		Заголовки.Вставить("message_id", ДанныеИсходящегоСообщения.ИдентификаторСообщения);
	КонецЕсли;
	
	Возврат Заголовки;
	
КонецФункции

// Функция - Сформировать параметры запроса по данным подписчика
//
// Параметры:
//  Подписчик                 - СправочникСсылка.инт_Подписчики - Ссылка на подписчика
//  ДанныеИсходящегоСообщения - Структура                       - Данные исходящего сообщения
//
// Возвращаемое значение:
//  Структура - Параметры запроса (query string параметры)
//
// Описание:
//  Формирует параметры запроса (query string), добавляя идентификаторы потока
//  и сообщения в параметры "flow_id" и "message_id", если настроена передача в параметрах запроса.
//
 Функция СформироватьПараметрыЗапросаПоДаннымПодписчика(Подписчик, ДанныеИсходящегоСообщения)
	ПараметрыЗапроса = Новый Структура;
	
	Если Подписчик.СпособПередачиИдентификатораПотока =
			Перечисления.инт_СпособыПередачиПараметровПоHTTP.ВПараметреЗапроса Тогда
		ПараметрыЗапроса.Вставить("flow_id", ДанныеИсходящегоСообщения.ПотокДанных.Код);
	КонецЕсли;
	
	Если Подписчик.СпособПередачиИдентификатораСообщения =
			Перечисления.инт_СпособыПередачиПараметровПоHTTP.ВПараметреЗапроса Тогда
		ПараметрыЗапроса.Вставить("message_id", ДанныеИсходящегоСообщения.ИдентификаторСообщения);
	КонецЕсли;
	
	Возврат ПараметрыЗапроса;
	
КонецФункции
#КонецОбласти
