////////////////////////////////////////////////////////////////////////////////
// инт_РаботаСКафка
//
// В данном модуле собраны методы работы с ВК simple kafka
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

Процедура ОтправитьСообщение(ОтправляемоеСообщение, Брокеры, Топик, НомерПартиции = -1, ИдентификаторСообщения = "", Заголовки = "") Экспорт

	Компонента = ПолучитьЭкземплярКомпоненты();
	
	// инициализируем подключение к брокеру
	// достаточно указать одного брокера из кластера, либо есть возможность перечислить брокеров через символ запятая
	РезультатИнициализации = Компонента.ИнициализироватьПродюсера(Брокеры);
	
	Если РезультатИнициализации Тогда
		РезультатОтправки = Компонента.ОтправитьСообщение(ОтправляемоеСообщение, Топик, НомерПартиции, ИдентификаторСообщения, Заголовки);
		Если Не РезультатОтправки = 0 Тогда
			СообщениеОбОшибке = СтрШаблон("Отправка сообщения не удалась.
			| Информация об ошибке: %1", Компонента.ПолучитьСообщениеОбОшибке());
			ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.РаботаСКафка",
			УровеньЖурналаРегистрации.Ошибка, , , СообщениеОбОшибке);
			ВызватьИсключение СообщениеОбОшибке;
		КонецЕсли;
	Иначе
		СообщениеОбОшибке = СтрШаблон("Не удалось инициализировать компоненту.
			| Информация об ошибке: %1", Компонента.ПолучитьСообщениеОбОшибке());
		ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.РаботаСКафка",
                                	УровеньЖурналаРегистрации.Ошибка, , , СообщениеОбОшибке);
		ВызватьИсключение СообщениеОбОшибке;
 
	КонецЕсли;
	
	Компонента.ОстановитьПродюсера();
	Компонента = Неопределено;

КонецПроцедуры

Функция ПолучитьСтруктуруСообщенияДляПакетнойОтправки() Экспорт
	Возврат инт_РаботаСКафкаПовтИсп.ПолучитьСтруктуруСообщенияДляПакетнойОтправки();
КонецФункции

Функция ПолучитьИнформациюОТопике(Брокеры, ИмяТопика) Экспорт
	
	Компонента = ПолучитьЭкземплярКомпоненты();
	РезультатJSON = Компонента.ПолучитьМетаданныеТопика(Брокеры, ИмяТопика);
	
	Если ПустаяСтрока(РезультатJSON) Тогда
		СообщениеОбОшибке = СтрШаблон("Не удалось получить информацию о топике %1.
		| Информация об ошибке: %2", ИмяТопика, Компонента.ПолучитьСообщениеОбОшибке());
		ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.РаботаСКафка",
						УровеньЖурналаРегистрации.Ошибка, , , СообщениеОбОшибке);
		ВызватьИсключение СообщениеОбОшибке;
	КонецЕсли;
	
	ДанныеТопика = инт_КоннекторHTTP.JsonВОбъект(РезультатJSON);
	
	Возврат ДанныеТопика;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Функция ПолучитьЭкземплярКомпоненты()
	ПодключитьВнешнююКомпоненту("ОбщийМакет.инт_simple_kafka", "Integration", ТипВнешнейКомпоненты.Native, ТипПодключенияВнешнейКомпоненты.Изолированно);

	Попытка
		Компонента = Новый("AddIn.Integration.simpleKafka1C");
	Исключение
		СообщениеОбОшибке = "Ошибка при подключении компоненты <Simple Kafka 1C>!";
		ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.РаботаСКафка",
                                        УровеньЖурналаРегистрации.Ошибка, , , СообщениеОбОшибке);
		ВызватьИсключение СообщениеОбОшибке;
	КонецПопытки;
	
	Возврат Компонента;
КонецФункции

#КонецОбласти
