///////////////////////////////////////////////////////////////////////////////
// инт_ПодписчикиОбщий
// Модуль-диспетчер для вызова методов типизированных справочников подписчиков
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Формирует сообщение для отправки подписчику
//
// Параметры:
//  ДанныеСообщения - Соответствие - Данные для сериализации
//  Подписчик - ОпределяемыйТип.инт_Подписчик - Ссылка на подписчика
//  ИдентификаторСообщения - УникальныйИдентификатор - УИД сообщения
//
// Возвращаемое значение:
//  Строка - Сериализованное сообщение
//
Функция СформироватьСообщение(ДанныеСообщения, Подписчик, ИдентификаторСообщения) Экспорт
    
    Возврат МенеджерПодписчика(Подписчик).СформироватьСообщение(ДанныеСообщения, Подписчик, ИдентификаторСообщения);
    
КонецФункции

// Отправляет сообщение подписчику
//
// Параметры:
//  Сообщение - Структура - Данные сообщения (ИдентификаторСообщения, Подписчик, СформированноеСообщение)
//  ОтложенныеДействия - Соответствие - Для пакетной обработки
//
Процедура ОтправитьСообщение(Сообщение, ОтложенныеДействия) Экспорт
    
    МенеджерПодписчика(Сообщение.Подписчик).ОтправитьСообщение(Сообщение, ОтложенныеДействия);
    
КонецПроцедуры

// Возвращает параметры повтора для подписчика
//
// Параметры:
//  Подписчик - ОпределяемыйТип.инт_Подписчик - Ссылка на подписчика
//
// Возвращаемое значение:
//  Структура - КоличествоПопытокОтправки, ПаузаМеждуПопытками
//
Функция ПолучитьПараметрыПовтора(Подписчик) Экспорт
    
    Результат = Новый Структура;
    Результат.Вставить("КоличествоПопытокОтправки", Подписчик.КоличествоПопытокОтправки);
    Результат.Вставить("ПаузаМеждуПопытками", Подписчик.ПаузаМеждуПопыткамиОбработки);
    
    Возврат Результат;
    
КонецФункции

// Рассчитывает дату следующей попытки отправки
//
// Параметры:
//  ДатаНачальная - Дата - Дата от которой будет отсчитываться пауза
//  Подписчик - ОпределяемыйТип.инт_Подписчик - Подписчик для расчёта
//
// Возвращаемое значение:
//  Дата - Дата следующей попытки отправки сообщения
//
Функция РассчитатьДатуСледующейПопытки(ДатаНачальная, Подписчик) Экспорт
    
    Возврат Дата(ДатаНачальная + Подписчик.ПаузаМеждуПопыткамиОбработки);
    
КонецФункции

// Возвращает список имён справочников подписчиков из ОпределяемогоТипа
//
// Возвращаемое значение:
//  Массив из Строка - Имена справочников (инт_ПодписчикHTTP, инт_ПодписчикJRPC и т.д.)
//
Функция СписокТиповПодписчиков() Экспорт
    
    Возврат инт_ПодписчикиОбщийПовтИсп.СписокТиповПодписчиков();
    
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает менеджер справочника по ссылке подписчика
//
// Параметры:
//  Подписчик - ОпределяемыйТип.инт_Подписчик - Ссылка на подписчика
//
// Возвращаемое значение:
//  СправочникМенеджер - Менеджер соответствующего справочника подписчиков
//
Функция МенеджерПодписчика(Подписчик)
    
    ИмяСправочника = Подписчик.Метаданные().Имя;
    Возврат Справочники[ИмяСправочника];
    
КонецФункции

#КонецОбласти
