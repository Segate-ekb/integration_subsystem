// BSLLS:NestedFunctionInParameters-off
// BSLLS:Typo-off
////////////////////////////////////////////////////////////////////////////////

// инт_ОтправкаИсходящихСообщений

//  

////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура инт_ЗапуститьМенеджерПотоковРассылкиСообщений() Экспорт
    РегистрыСведений.инт_МенеджерПотоковОтправкиСообщений.СобратьМусор();
    
    РазделятьПотокиПоТипам = инт_ОтправкаИсходящихСообщенийПовтИсп.РазделятьПотокиОтправкиПоТипамПодписчиков();
    
    Если РазделятьПотокиПоТипам Тогда
        Для Каждого ТипПодписчика Из Перечисления.инт_ТипыПодписчиков Цикл
            СоздатьПотокиОтправкиСообщенийПоТипу(ТипПодписчика);
        КонецЦикла;
    Иначе
        СоздатьПотокиОтправкиСообщенийПоТипу(Перечисления.инт_ТипыПодписчиков.ПустаяСсылка());
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьПотокиОтправкиСообщенийПоТипу(ТипПодписчика)
    КоличествоПотоковКСозданию = РегистрыСведений
                                    .инт_МенеджерПотоковОтправкиСообщений
                                    .КоличествоПотоковКСозданию(ТипПодписчика);
    Для Итератор = 1 По КоличествоПотоковКСозданию Цикл // BSLLS:UnusedLocalVariable-off
        СоздатьПотокОтправкиСообщений(ТипПодписчика);
    КонецЦикла;
    
КонецПроцедуры

Процедура СоздатьПотокОтправкиСообщений(ТипПодписчика)
    КоличествоСообщенийВПачке = инт_ОтправкаИсходящихСообщенийПовтИсп.РазмерПачкиОтправкиСообщений();
    МассивСообщений = РегистрыСведений
                            .инт_ТекущийСтатусРассылкиСообщений
                            .ПолучитьСообщенияКОтправке(КоличествоСообщенийВПачке, ТипПодписчика);
    Если МассивСообщений.Количество() = 0 Тогда
        // Нет сообщений для обработки.
        Возврат;
    КонецЕсли;
    
    НачатьТранзакцию();
    Попытка
        РегистрыСведений.инт_ТекущийСтатусРассылкиСообщений
                                        .ЗаписатьСтатусСообщений(МассивСообщений,
                                            Перечисления.инт_СтатусыРассылкиИсходящихСообщений.ВПроцессеОтправки);
        МассивПараметров = Новый Массив;
        МассивПараметров.Добавить(МассивСообщений);
        Поток = ФоновыеЗадания.Выполнить("инт_ОтправкаИсходящихСообщенийСлужебный.ВыполнитьОбработкуСообщенийВПотоке",
                                                МассивПараметров,
                                                ,
                                                СтрШаблон("Поток отправки исходящих сообщений по Типу подписчика <%1>.",
                                                    ТипПодписчика));
        РегистрыСведений.инт_МенеджерПотоковОтправкиСообщений
                            .ЗарегистрироватьПоток(Поток.УникальныйИдентификатор, ТипПодписчика, МассивСообщений);
        ЗафиксироватьТранзакцию();
    Исключение
        ОтменитьТранзакцию();
        СообщениеОбОшибке = СтрШаблон("Ошибка при создании потока отправки сообщений по типу подписчика <%1>.
        |
        |Информация об ошибке: %2", ТипПодписчика, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
        ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.МенеджерПотоковОтправкиСообщений",
                                        УровеньЖурналаРегистрации.Ошибка, , , СообщениеОбОшибке);
    КонецПопытки;
    
КонецПроцедуры

#КонецОбласти
