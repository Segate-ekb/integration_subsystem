
&НаКлиенте
Перем ДанныеПеременных;
&НаКлиенте
Перем ДанныеДляПередачи;
&НаКлиенте
Перем СоответствиеПолей;

#Область Обработчики_событий_формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок           = Параметры.Тип;
	РежимРаботы         = Параметры.РежимРаботы;
	КоличествоЭлементов = Параметры.КоличествоЭлементов;
	ИмяПеременной       = Параметры.ИмяПеременной;
	
	Если РежимРаботы = "Сервер" Тогда
		Адрес = Параметры.Адрес;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если РежимРаботы = "Сервер" Тогда
		ПодключитьОбработчикОжидания("СформироватьОтображениеКоллекции", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПередачаСтруктурыПеременных" И ЭтаФорма.ВладелецФормы = Источник Тогда
		ДанныеПеременных  = Параметр;
		СоответствиеПолей = конс_ПросмотрЗначенийКлиентСервер.ПолучитьСоответствиеПолейКоллекции(ИмяПеременной, ДанныеПеременных);
		СформироватьОтображениеКоллекцииОбщая();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если РежимРаботы = "Сервер" И ЗначениеЗаполнено(ДанныеДляПередачи) Тогда
		ПоместитьВоВременноеХранилище(Неопределено, ДанныеДляПередачи);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Обработчики_событий_элементов_формы

&НаКлиенте
Процедура ПоказатьОтдельно(Команда)
	
	ПоказатьЗначениеСтрокиОтдельно();
			
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКоллекцииВыбор()

	ПоказатьЗначениеСтрокиОтдельно();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКоллекцииПриАктивизацииСтроки(Элемент)
	
	Элементы.ПоказатьОтдельно.Доступность = Элемент.ТекущиеДанные <> Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область Служебные_процедуры_и_функции

&НаКлиенте
Процедура ПоказатьЗначениеСтрокиОтдельно()
	
	ТекущиеДанные = Элементы[ПолучитьИмяТаблицыКоллекции()].ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимРаботы = "Сервер" Тогда
		ДанныеДляПередачи = ПолучитьАдресДанныхДляПередачиСервер(Адрес, ИмяПеременной, ТекущиеДанные.Индекс, УникальныйИдентификатор);
		ПараметрыОткрытия = Новый Структура("Адрес,РежимРаботы", ДанныеДляПередачи, РежимРаботы);
	Иначе
		ПараметрыОткрытия = Новый Структура("РежимРаботы", РежимРаботы);
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.конс_Просмотрщик", ПараметрыОткрытия, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	Если РежимРаботы = "Клиент" Тогда
		ЗаполнитьДанныеДляПередачи(ИмяПеременной, ТекущиеДанные.Индекс);
		Оповестить("ПередачаПеременных", ДанныеДляПередачи, ЭтаФорма);
	КонецЕсли;
				
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеДляПередачи(ИмяПеременной, Индекс)
	
	ДанныеДляПередачи = Новый Соответствие;
		
	Переменные = Новый Соответствие;
	Переменные.Вставить(ИмяПеременной + "[" + Формат(Индекс, "ЧН=0; ЧГ=0") + "]", ДанныеПеременных[ИмяПеременной][Индекс]);
	
	ДанныеДляПередачи.Вставить("СтруктурыТипов", ДанныеПеременных["СтруктурыТипов"]);
	ДанныеДляПередачи.Вставить("Переменные",     Переменные);
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьАдресДанныхДляПередачиСервер(Адрес, ИмяПеременной, Индекс, Идентификатор)
	
	ДанныеДляПередачи = Новый Соответствие;
	Переменные        = Новый Соответствие;
	ДанныеПеременных  = ПолучитьИзВременногоХранилища(Адрес);
	
	Переменные.Вставить(ИмяПеременной + "[" + Формат(Индекс, "ЧН=0; ЧГ=0") + "]", ДанныеПеременных[ИмяПеременной][Индекс]);
	
	ДанныеДляПередачи.Вставить("СтруктурыТипов", ДанныеПеременных["СтруктурыТипов"]);
	ДанныеДляПередачи.Вставить("Переменные",     Переменные);
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеДляПередачи, Идентификатор);
	
КонецФункции

&НаСервере
Процедура СформироватьТаблицуКоллекции(СоответствиеПолей)
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	ИмяТаблицыКоллекции = ПолучитьИмяТаблицыКоллекции();
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяТаблицыКоллекции, Новый ОписаниеТипов("ТаблицаЗначений"), "", ""));
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Индекс",           Новый ОписаниеТипов("Число"),  ИмяТаблицыКоллекции, НСтр("en = 'Index'; ru = 'Индекс'")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ЗначениеЭлемента", Новый ОписаниеТипов("Строка"), ИмяТаблицыКоллекции, НСтр("en = 'Item value'; ru = 'Значение элемента'")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ТипЭлемента",      Новый ОписаниеТипов("Строка"), ИмяТаблицыКоллекции, НСтр("en = 'Item type'; ru = 'Тип элемента'")));
	
	Для Каждого КЗ Из СоответствиеПолей Цикл
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("_" + КЗ.Ключ, Новый ОписаниеТипов("Строка"), ИмяТаблицыКоллекции, КЗ.Ключ));
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		
	ТаблицаКоллекции                           = Элементы.Добавить(ИмяТаблицыКоллекции, Тип("ТаблицаФормы"), Элементы.СтраницаТаблица);
	ТаблицаКоллекции.ПутьКДанным               = ИмяТаблицыКоллекции;
	ТаблицаКоллекции.ТолькоПросмотр            = Истина;
	ТаблицаКоллекции.КоманднаяПанель.Видимость = Ложь;
	
	ДобавитьКолонкуТаблицы("Индекс",           ТаблицаКоллекции, ИмяТаблицыКоллекции);
	ДобавитьКолонкуТаблицы("ЗначениеЭлемента", ТаблицаКоллекции, ИмяТаблицыКоллекции);
	ДобавитьКолонкуТаблицы("ТипЭлемента",      ТаблицаКоллекции, ИмяТаблицыКоллекции);
	
	Элементы["Индекс"].Формат = "ЧН=0";
	
	Для Каждого КЗ Из СоответствиеПолей Цикл
		ДобавитьКолонкуТаблицы("_" + КЗ.Ключ, ТаблицаКоллекции, ИмяТаблицыКоллекции);
	КонецЦикла;
	
	Элементы[ИмяТаблицыКоллекции].УстановитьДействие("ПриАктивизацииСтроки", "ТаблицаКоллекцииПриАктивизацииСтроки");
	Элементы[ИмяТаблицыКоллекции].УстановитьДействие("Выбор", "ТаблицаКоллекцииВыбор");
		
КонецПроцедуры

&НаСервере
Процедура ДобавитьКолонкуТаблицы(ИмяКолонки, ТаблицаКоллекции, ИмяТаблицыКоллекции)

    НовыйЭлемент                = Элементы.Добавить(ИмяКолонки, Тип("ПолеФормы"), ТаблицаКоллекции);
    НовыйЭлемент.Вид            = ВидПоляФормы.ПолеВвода;
    НовыйЭлемент.ПутьКДанным    = ИмяТаблицыКоллекции + "." + ИмяКолонки;
	
КонецПроцедуры

&НаКлиенте
Функция СформироватьОтображениеКоллекции() Экспорт
	
	СоответствиеПолей = ПолучитьСоответствиеПолейКоллекцииНаСервере(ИмяПеременной, Адрес);
	СформироватьОтображениеКоллекцииОбщая();
	
КонецФункции

&НаКлиенте
Процедура СформироватьОтображениеКоллекцииОбщая()
	
	СформироватьТаблицуКоллекции(СоответствиеПолей);
	ДополнитьТаблицуКоллекции(КоличествоЭлементов);
	
	ЭтаФорма.ТекущийЭлемент                               = Элементы[ПолучитьИмяТаблицыКоллекции()];
	Элементы.СтраницыОтображениеКоллекции.ТекущаяСтраница = Элементы.СтраницаТаблица;
	Элементы.ПоказатьОтдельно.Видимость                   = Истина;
		
КонецПроцедуры

&НаКлиенте
Процедура ДополнитьТаблицуКоллекции(КоличествоВыводимыхСтрок)
	
	Если РежимРаботы = "Сервер" Тогда
		МассивСтруктурПеременных = ПолучитьМассивСтруктурПеременныхНаСервере(ИмяПеременной, Адрес, 0, КоличествоВыводимыхСтрок);
	Иначе
		МассивСтруктурПеременных = конс_ПросмотрЗначенийКлиентСервер.ПолучитьМассивСтруктурПеременных(ИмяПеременной, ДанныеПеременных, 0, КоличествоВыводимыхСтрок);
	КонецЕсли;
	
	ДобавитьСтрокиВТаблицуКоллекции(МассивСтруктурПеременных);
		
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтрокиВТаблицуКоллекции(МассивСтруктурПеременных)
	
	ИмяТаблицыКоллекции = ПолучитьИмяТаблицыКоллекции();
	ТекИндекс           = ЭтаФорма[ИмяТаблицыКоллекции].Количество();
	
	Для Каждого стЭлементКоллекции Из МассивСтруктурПеременных Цикл
		Стр = ЭтаФорма[ИмяТаблицыКоллекции].Добавить();
		
		Для Каждого КЗ Из СоответствиеПолей Цикл
			Стр["_" + КЗ.Ключ] = "<>";
		КонецЦикла;
		
		Если стЭлементКоллекции.Код <> 1 Тогда
			Для Каждого стСвойство Из стЭлементКоллекции.Подчиненные Цикл
				ИмяСвойства = стСвойство.Свойство;
				Если СоответствиеПолей[ИмяСвойства] <> Неопределено Тогда
					Стр["_" + ИмяСвойства] = стСвойство.Значение;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Стр.ТипЭлемента      = стЭлементКоллекции.Тип;
		Стр.ЗначениеЭлемента = стЭлементКоллекции.Значение;
		Стр.Индекс           = ТекИндекс;
		ТекИндекс            = ТекИндекс + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьМассивСтруктурПеременныхНаСервере(ИмяПеременной, Адрес, КоличествоВыведенных, КоличествоВыводимых)
	
	Возврат конс_ПросмотрЗначенийКлиентСервер.ПолучитьМассивСтруктурПеременных(ИмяПеременной, Адрес, КоличествоВыведенных, КоличествоВыводимых);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСоответствиеПолейКоллекцииНаСервере(ИмяПеременной, Адрес)
	
	Возврат конс_ПросмотрЗначенийКлиентСервер.ПолучитьСоответствиеПолейКоллекции(ИмяПеременной, Адрес);
			
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИмяТаблицыКоллекции()
	
	Возврат "ТаблицаКоллекции";
	
КонецФункции

#КонецОбласти

