&НаКлиенте
Перем ДанныеПеременных;
&НаКлиенте
Перем ДанныеДляПередачи;
&НаКлиенте
Перем ВиртуальнаяМашина;

#Область События_формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РежимРаботы = Параметры.РежимРаботы;
	
	Если Параметры.Свойство("АдресВМ") Тогда
		ВычислениеВыражения = Истина;
		Выражение           = Параметры.Выражение;
		ТекущийПуть         = Выражение;
		УровеньСтекаВызовов = Параметры.УровеньСтекаВызовов;
		Если РежимРаботы = "Сервер" Тогда
			АдресВМ = Параметры.АдресВМ;
		КонецЕсли;
	Иначе
		Элементы.ТекущийПуть.ТолькоПросмотр = Истина;
		Если РежимРаботы = "Сервер" Тогда
			Адрес = Параметры.Адрес;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если РежимРаботы = "Сервер" Тогда
		ОбновитьОтображениеПеременных();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПередачаПеременных" И ЭтаФорма.ВладелецФормы = Источник Тогда
		Если ВычислениеВыражения Тогда
			ВиртуальнаяМашина = Параметр;
		Иначе
			ДанныеПеременных = Параметр;
		КонецЕсли;
		
		ОбновитьОтображениеПеременных();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ЗначениеЗаполнено(АдресВМ) Тогда
		ОчиститьТекущиеХранилищаДанныхПеременных();
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область Обработчики_событий_элементов_формы

&НаКлиенте
Процедура ПеременныеПередРазворачиванием(Элемент, Строка, Отказ)

	конс_ПодключаемаяКонсольКлиент.ПеременныеПередРазворачиванием(Переменные, Адрес, ДанныеПеременных, РежимРаботы, Элемент, Строка, Отказ);
		
КонецПроцедуры

&НаКлиенте
Процедура ПеременныеПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийПуть                           = ?(ЗначениеЗаполнено(ТекущиеДанные.ИмяПеременной), ТекущиеДанные.ИмяПеременной, Выражение) + ТекущиеДанные.Путь;
	Элементы.ПоказатьОтдельно.Доступность = ?(ТекущиеДанные.ЭтоКоллекция ИЛИ ТекущиеДанные.Тип = "Строка"
										ИЛИ ТекущиеДанные.Тип = "String" ИЛИ конс_ПодключаемаяКонсольКлиент.ЭтоТипДляПоказаЗначения(ТекущиеДанные.Тип), Истина, Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ТекущийПутьОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Выражение = Текст;
	ОбновитьОтображениеПеременных();

КонецПроцедуры

&НаКлиенте
Процедура ПеременныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	конс_ПодключаемаяКонсольКлиент.ПоказатьЗначениеПеременнойОтдельно(ЭтаФорма, РежимРаботы, Адрес, ДанныеПеременных, ДанныеДляПередачи, Элементы.Переменные.ТекущиеДанные);
		
КонецПроцедуры

#КонецОбласти

#Область Обработчики_команд_формы

&НаКлиенте
Процедура ПоказатьОтдельно(Команда)
	
	конс_ПодключаемаяКонсольКлиент.ПоказатьЗначениеПеременнойОтдельно(ЭтаФорма, РежимРаботы, Адрес, ДанныеПеременных, ДанныеДляПередачи, Элементы.Переменные.ТекущиеДанные);
			
КонецПроцедуры

#КонецОбласти

#Область Служебные_процедуры_и_функции

&НаКлиенте
Процедура ОчиститьТекущиеХранилищаДанныхПеременных()
	
	ПоместитьВоВременноеХранилище(Неопределено, Адрес);
	ПоместитьВоВременноеХранилище(Неопределено, ДанныеДляПередачи);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеПеременных()
	
	Если ВычислениеВыражения Тогда
		Если РежимРаботы = "Сервер" Тогда
			ОчиститьТекущиеХранилищаДанныхПеременных();
		Иначе
			ДанныеПеременных = Неопределено;
			ДанныеДляПередачи = Неопределено;
		КонецЕсли;
		
		ЭлементыДереваПеременных = Переменные.ПолучитьЭлементы();
		ЭлементыДереваПеременных.Очистить();
	КонецЕсли;
			
	МассивВыражений = Новый Массив;
	МассивВыражений.Добавить(Выражение);
	
	конс_ПодключаемаяКонсольКлиент.ОбновитьОтображениеПеременных(Переменные, РежимРаботы, ВычислениеВыражения, МассивВыражений, Адрес, ДанныеПеременных, АдресВМ, ВиртуальнаяМашина, УникальныйИдентификатор, УровеньСтекаВызовов);
		
КонецПроцедуры

#КонецОбласти

