////////////////////////////////////////////////////////////////////////////////
// Форма настроек редактора кода
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗагрузитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		СохранитьНастройкиНаСервере();
		// Оповещаем все открытые формы об изменении настроек
		Оповестить("конс_НастройкиРедактораИзменены");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПутьКИсходникамКонфигурацииНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ВыборКаталогаИсходногоКодаЗавершение", ЭтотОбъект);
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбора.Каталог = ПутьКИсходникамКонфигурации;
	ДиалогВыбора.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборКаталогаИсходногоКодаЗавершение(Файлы, ДопПараметры) Экспорт
	
	Если Файлы <> Неопределено И Файлы.Количество() > 0 Тогда
		ПутьКИсходникамКонфигурации = Файлы[0];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКШаблонамКодаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ВыборПутиКШаблонамКодаЗавершение", ЭтотОбъект);
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбора.МножественныйВыбор = Ложь;
	ДиалогВыбора.Фильтр = "Файл шаблонов (*.st)|*.st";
	ДиалогВыбора.Каталог = ПутьКШаблонамКода;
	ДиалогВыбора.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПутиКШаблонамКодаЗавершение(Файлы, ДопПараметры) Экспорт
	
	Если Файлы <> Неопределено И Файлы.Количество() > 0 Тогда
		ПутьКШаблонамКода = Файлы[0];
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьНастройки()
	
	Настройки = конс_ПодключаемаяКонсольСервер.ПолучитьНастройкиИзХранилища();
	
	Тема = Настройки.Тема;
	ПутьКИсходникамКонфигурации = Настройки.ПутьКИсходникамКонфигурации;
	ПутьКШаблонамКода = Настройки.ПутьКШаблонамКода;
	ВключитьБыстрыеПодсказки = Настройки.БыстрыеПодсказки;
	ОтображениеКартыКода = Настройки.КартаКода;
	ОтображениеПробеловИТабуляций = Настройки.ПробелыИТабуляции;
	ОтображениеСтрокиСостояния = Настройки.СтрокаСостояния;
	Если Настройки.Свойство("ОтключитьВстроенныеСниппеты") Тогда
		ОтключитьВстроенныеСниппеты = Настройки.ОтключитьВстроенныеСниппеты;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервере()
	
	Настройки = конс_ПодключаемаяКонсольСервер.ПолучитьНастройкиИзХранилища();
	
	Настройки.Тема = Тема;
	Настройки.ПутьКИсходникамКонфигурации = ПутьКИсходникамКонфигурации;
	Настройки.ПутьКШаблонамКода = ПутьКШаблонамКода;
	Настройки.БыстрыеПодсказки = ВключитьБыстрыеПодсказки;
	Настройки.КартаКода = ОтображениеКартыКода;
	Настройки.ПробелыИТабуляции = ОтображениеПробеловИТабуляций;
	Настройки.СтрокаСостояния = ОтображениеСтрокиСостояния;
	Настройки.Вставить("ОтключитьВстроенныеСниппеты", ОтключитьВстроенныеСниппеты);
	
	конс_ПодключаемаяКонсольСервер.СохранитьНастройкиВХранилище(Настройки);
	
КонецПроцедуры

#КонецОбласти
