// Метрика: Возраст старейшего сообщения в очереди (секунд)
// Лейблы: queue (incoming/outgoing/distribution)
// Индикатор застревания очереди

ТаблицаЗначений = Новый ТаблицаЗначений;
ТаблицаЗначений.Колонки.Добавить("queue", Новый ОписаниеТипов("Строка"));
ТаблицаЗначений.Колонки.Добавить("value", Новый ОписаниеТипов("Число"));

Запрос = Новый Запрос;
Запрос.Текст =
"ВЫБРАТЬ
|	""incoming"" КАК queue,
|	ЕСТЬNULL(РАЗНОСТЬДАТ(МИНИМУМ(История.Период), &ТекущаяДата, СЕКУНДА), 0) КАК value
|ИЗ
|	РегистрСведений.инт_ТекущийСтатусВходящихСообщений КАК Статус
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.инт_ИсторияСтатусовВходящихСообщений КАК История
|		ПО Статус.ИдентификаторСообщения = История.ИдентификаторСообщения
|			И История.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.инт_СтатусыВходящихСообщений.Новый)
|ГДЕ
|	Статус.СтатусСообщения В (
|		ЗНАЧЕНИЕ(Перечисление.инт_СтатусыВходящихСообщений.Новый),
|		ЗНАЧЕНИЕ(Перечисление.инт_СтатусыВходящихСообщений.ОшибкаОбработки))
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	""outgoing"",
|	ЕСТЬNULL(РАЗНОСТЬДАТ(МИНИМУМ(История.Период), &ТекущаяДата, СЕКУНДА), 0)
|ИЗ
|	РегистрСведений.инт_ТекущийСтатусИсходящихСообщений КАК Статус
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.инт_ИсторияСтатусовИсходящихСообщений КАК История
|		ПО Статус.ИдентификаторСообщения = История.ИдентификаторСообщения
|			И История.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.инт_СтатусыИсходящихСообщений.Новый)
|ГДЕ
|	Статус.СтатусСообщения В (
|		ЗНАЧЕНИЕ(Перечисление.инт_СтатусыИсходящихСообщений.Новый),
|		ЗНАЧЕНИЕ(Перечисление.инт_СтатусыИсходящихСообщений.ОшибкаФормирования),
|		ЗНАЧЕНИЕ(Перечисление.инт_СтатусыИсходящихСообщений.ОшибкаОтправки))
|
|ОБЪЕДИНИТЬ ВСЕ
|
|ВЫБРАТЬ
|	""distribution"",
|	ЕСТЬNULL(РАЗНОСТЬДАТ(МИНИМУМ(История.Период), &ТекущаяДата, СЕКУНДА), 0)
|ИЗ
|	РегистрСведений.инт_ТекущийСтатусРассылкиСообщений КАК Статус
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.инт_ИсторияСтатусовРассылкиСообщений КАК История
|		ПО Статус.ИдентификаторСообщения = История.ИдентификаторСообщения
|			И Статус.Подписчик = История.Подписчик
|			И История.СтатусСообщения = ЗНАЧЕНИЕ(Перечисление.инт_СтатусыРассылкиИсходящихСообщений.Новый)
|ГДЕ
|	Статус.СтатусСообщения В (
|		ЗНАЧЕНИЕ(Перечисление.инт_СтатусыРассылкиИсходящихСообщений.Новый),
|		ЗНАЧЕНИЕ(Перечисление.инт_СтатусыРассылкиИсходящихСообщений.Сформирован),
|		ЗНАЧЕНИЕ(Перечисление.инт_СтатусыРассылкиИсходящихСообщений.ОшибкаОбработки))";

Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());

Результат = Запрос.Выполнить();
Если НЕ Результат.Пустой() Тогда
	ТаблицаЗначений = Результат.Выгрузить();
КонецЕсли;
