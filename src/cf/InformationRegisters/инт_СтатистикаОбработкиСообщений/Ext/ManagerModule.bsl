// BSLLS:Typo-off)))
#Область ПрограммныйИнтерфейс

// Записывает статистику обработки сообщения.
// Запись выполняется независимо без блокировки (WriteMode = Independent).
//
// Параметры:
//  ИдентификаторСообщения - УникальныйИдентификатор - Идентификатор обработанного сообщения.
//  ТипОперации            - ПеречислениеСсылка.инт_ТипыОперацийСтатистики - Тип выполненной операции.
//  ПотокДанных            - СправочникСсылка.инт_ПотокиДанных - Поток данных сообщения.
//  ДлительностьМс         - Число - Длительность операции в миллисекундах.
//  Успешно                - Булево - Признак успешного завершения операции. По умолчанию Истина.
//  Подписчик              - ОпределяемыйТип.инт_ТипСсылкиНаПодписчика - Ссылка на подписчика (необязательно).
//
Процедура ЗаписатьСтатистику(ИдентификаторСообщения, ТипОперации, ПотокДанных, ДлительностьМс, Успешно = Истина, Подписчик = Неопределено) Экспорт
	
	Запись = РегистрыСведений.инт_СтатистикаОбработкиСообщений.СоздатьМенеджерЗаписи();
	Запись.Период = ТекущаяДатаСеанса();
	Запись.ИдентификаторСообщения = ИдентификаторСообщения;
	Запись.ТипОперации = ТипОперации;
	Запись.ПотокДанных = ПотокДанных;
	Запись.ДлительностьМс = ДлительностьМс;
	Запись.Успешно = Успешно;
	
	Если Подписчик <> Неопределено Тогда
		Запись.Подписчик = Подписчик;
	КонецЕсли;
	
	Запись.Записать();
	
КонецПроцедуры

// Записывает статистику операции формирования сообщения.
//
// Параметры:
//  ИдентификаторСообщения - УникальныйИдентификатор - Идентификатор сообщения.
//  ПотокДанных            - СправочникСсылка.инт_ПотокиДанных - Поток данных.
//  ДлительностьМс         - Число - Длительность формирования в миллисекундах.
//  Успешно                - Булево - Признак успешного завершения. По умолчанию Истина.
//
Процедура ЗаписатьСтатистикуФормирования(ИдентификаторСообщения, ПотокДанных, ДлительностьМс, Успешно = Истина) Экспорт
	
	ЗаписатьСтатистику(
		ИдентификаторСообщения, 
		Перечисления.инт_ТипыОперацийСтатистики.Формирование,
		ПотокДанных,
		ДлительностьМс,
		Успешно);
	
КонецПроцедуры

// Записывает статистику операции отправки сообщения.
//
// Параметры:
//  ИдентификаторСообщения - УникальныйИдентификатор - Идентификатор сообщения.
//  ПотокДанных            - СправочникСсылка.инт_ПотокиДанных - Поток данных.
//  ДлительностьМс         - Число - Длительность отправки в миллисекундах.
//  Успешно                - Булево - Признак успешного завершения. По умолчанию Истина.
//  Подписчик              - ОпределяемыйТип.инт_ТипСсылкиНаПодписчика - Ссылка на подписчика.
//
Процедура ЗаписатьСтатистикуОтправки(ИдентификаторСообщения, ПотокДанных, ДлительностьМс, Успешно = Истина, Подписчик = Неопределено) Экспорт
	
	ЗаписатьСтатистику(
		ИдентификаторСообщения, 
		Перечисления.инт_ТипыОперацийСтатистики.Отправка,
		ПотокДанных,
		ДлительностьМс,
		Успешно,
		Подписчик);
	
КонецПроцедуры

// Записывает статистику операции обработки входящего сообщения.
//
// Параметры:
//  ИдентификаторСообщения - УникальныйИдентификатор - Идентификатор сообщения.
//  ПотокДанных            - СправочникСсылка.инт_ПотокиДанных - Поток данных.
//  ДлительностьМс         - Число - Длительность обработки в миллисекундах.
//  Успешно                - Булево - Признак успешного завершения. По умолчанию Истина.
//
Процедура ЗаписатьСтатистикуОбработкиВходящего(ИдентификаторСообщения, ПотокДанных, ДлительностьМс, Успешно = Истина) Экспорт
	
	ЗаписатьСтатистику(
		ИдентификаторСообщения, 
		Перечисления.инт_ТипыОперацийСтатистики.ОбработкаВходящего,
		ПотокДанных,
		ДлительностьМс,
		Успешно);
	
КонецПроцедуры

// Удаляет устаревшие записи статистики.
//
// Параметры:
//  СрокХраненияДней - Число - Количество дней хранения статистики.
//
Процедура УдалитьУстаревшуюСтатистику(СрокХраненияДней) Экспорт
	
	ГраничнаяДата = НачалоДня(ТекущаяДатаСеанса() - СрокХраненияДней * 86400);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Статистика.Период КАК Период,
		|	Статистика.ИдентификаторСообщения КАК ИдентификаторСообщения,
		|	Статистика.ТипОперации КАК ТипОперации,
		|	Статистика.ПотокДанных КАК ПотокДанных
		|ИЗ
		|	РегистрСведений.инт_СтатистикаОбработкиСообщений КАК Статистика
		|ГДЕ
		|	Статистика.Период < &ГраничнаяДата";
	Запрос.УстановитьПараметр("ГраничнаяДата", ГраничнаяДата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.инт_СтатистикаОбработкиСообщений.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
		МенеджерЗаписи.Удалить();
	КонецЦикла;
	
КонецПроцедуры

// Очищает устаревшую статистику обработки сообщений.
// Вызывается регламентным заданием инт_ОчисткаСтатистикиОбработкиСообщений.
//
Процедура ОчиститьУстаревшуюСтатистику() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СрокХранения = Константы.инт_СрокХраненияСтатистикиДней.Получить();
	Если НЕ ЗначениеЗаполнено(СрокХранения) Тогда
		СрокХранения = 7; // По умолчанию 7 дней
	КонецЕсли;
	
	УдалитьУстаревшуюСтатистику(СрокХранения);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти
