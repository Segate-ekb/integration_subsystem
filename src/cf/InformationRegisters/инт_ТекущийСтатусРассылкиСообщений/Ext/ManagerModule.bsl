#Область ПрограммныйИнтерфейс
// Процедура - Записать статус сообщения
// Записывает в регистр текущий статус сообщения. 
// Если Статус "ОшибкаОбработки" тогда обязательно указание текста ошибки.
// В случае фиксации статуса ошибки - выполняется расчет попыток отправки.
//
// Параметры:
//  ИдентификаторСообщения     -  УникальныйИдентификатор  - Уникальный идентификатор сообщения
//  Статус                     -  ПеречислениеСсылка.инт_СтатусыИсходящихСообщений  -  Статус сообщения.
//  ТекстОшибки                -  Строка    - Текст ошибки при формировании сообщения
//
Процедура ЗаписатьСтатусСообщения(ИдентификаторСообщения, Подписчик, СтатусСообщения, ТекстОшибки="") Экспорт
        ЗафиксироватьСтатус(ИдентификаторСообщения, Подписчик, СтатусСообщения, ТекстОшибки);
КонецПроцедуры

// Процедура - Записать статус сообщений
//
// Параметры:
//  МассивСообщений                     -  Массив   - Массив структур <УникальныйИдентификатор, Подписчик> сообщений, для которых будет осуществлена запись статуса
//  СтатусСообщений                     -  ПеречислениеСсылка.инт_СтатусыРассылкиИсходящихСообщений  -  Статус сообщения. 
//  ТекстОшибки                         -  Строка    - Текст ошибки при формировании сообщения
Процедура ЗаписатьСтатусСообщений(МассивСообщений, СтатусСообщений, ТекстОшибки="") Экспорт
    Для Каждого Сообщение Из МассивСообщений Цикл
    	ЗаписатьСтатусСообщения(Сообщение.ИдентификаторСообщения, Сообщение.Подписчик, СтатусСообщений, ТекстОшибки);
    КонецЦикла;
КонецПроцедуры

// Функция - Текущий статус сообщения по идентификатору
//
// Параметры:
//  ИдентификаторСообщения     -   УникальныйИдентификатор, Строка   - ИдентификаторСообщения
//  Подписчик - СправочникСсылка.инт_Подписчики - Ссылка на подписчика  
//
// Возвращаемое значение:
//   ПеречислениеСсылка.инт_СтатусыИсходящихСообщений - Текущий статус сообщения. Если сообщение не найдено, вернется "Неопределено"
//
Функция ТекущийСтатусСообщения(Знач ИдентификаторСообщения, Подписчик) Экспорт
    Статус = Неопределено;
    Если ТипЗнч(ИдентификаторСообщения) = Тип("Строка") Тогда
    	ИдентификаторСообщения = Новый УникальныйИдентификатор(ИдентификаторСообщения);
    КонецЕсли;
    
    Запрос = Новый запрос;
    Запрос.Текст = "ВЫБРАТЬ
                   |    инт_ТекущийСтатусРассылкиСообщений.СтатусСообщения КАК СтатусСообщения
                   |ИЗ
                   |    РегистрСведений.инт_ТекущийСтатусРассылкиСообщений КАК инт_ТекущийСтатусРассылкиСообщений
                   |ГДЕ
                   |    инт_ТекущийСтатусРассылкиСообщений.ИдентификаторСообщения = &ИдентификаторСообщения
                   |       И инт_ТекущийСтатусРассылкиСообщений.Подписчик = &Подписчик";
    Запрос.УстановитьПараметр("ИдентификаторСообщения", ИдентификаторСообщения);
    Запрос.УстановитьПараметр("Подписчик", Подписчик);
    Выборка = Запрос.Выполнить().Выбрать();
    
    Если Выборка.Следующий() Тогда
    	Статус = Выборка.СтатусСообщения;
    КонецЕсли;
    
    Возврат Статус;
КонецФункции

// Функция - Получить сообщения к Отправке
// Получает сообщения к отправке. Или те что в статусе <новое> или в статусе <ОшибкаОбработки и с количеством попыток менее максимального
// Параметры:
// Функция - Получить сообщения к отправке
//
// Параметры:
//  КоличествоСообщенийВПачке     - Число - Если 0, то получает все возможные сообщения
//  ТипПодписчика                 - Строка - Имя справочника подписчика (например "инт_ПодписчикHTTP"). Если не указан - все типы.
// 
// Возвращаемое значение:
//  Массив из Структура - Массив структур <ИдентификаторСообщения, Подписчик>
//
Функция ПолучитьСообщенияКОтправке(КоличествоСообщенийВПачке, ТипПодписчика = Неопределено) Экспорт
    МассивСообщений = Новый Массив;
    
    ПодстрокаОграничения = ?(КоличествоСообщенийВПачке > 0, "ПЕРВЫЕ " + Формат(КоличествоСообщенийВПачке, "ЧГ="), "");
    
    Запрос = Новый Запрос;
    Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
    Запрос.УстановитьПараметр("СтатусНовый", Перечисления.инт_СтатусыРассылкиИсходящихСообщений.Новый);
    Запрос.УстановитьПараметр("СтатусОшибка", Перечисления.инт_СтатусыРассылкиИсходящихСообщений.ОшибкаОбработки);
    
    // Если указан конкретный тип - один запрос, иначе UNION по всем
    Если ЗначениеЗаполнено(ТипПодписчика) Тогда
        Запрос.Текст = ТекстЗапросаПоТипуПодписчика(ПодстрокаОграничения, ТипПодписчика);
    Иначе
        ЧастиЗапроса = Новый Массив;
        Для Каждого ИмяСправочника Из инт_ПодписчикиОбщийПовтИсп.СписокТиповПодписчиков() Цикл
            ЧастиЗапроса.Добавить(ТекстЗапросаПоТипуПодписчика(ПодстрокаОграничения, ИмяСправочника));
        КонецЦикла;
        
        Если ЧастиЗапроса.Количество() = 0 Тогда
            Возврат МассивСообщений;
        КонецЕсли;
        
        Запрос.Текст = СтрСоединить(ЧастиЗапроса, "
        |
        |ОБЪЕДИНИТЬ ВСЕ
        |
        |");
    КонецЕсли;
    
    Выборка = Запрос.Выполнить().Выбрать();
    Пока Выборка.Следующий() Цикл
        СтруктураСообщения = Новый Структура("ИдентификаторСообщения,Подписчик", 
            Выборка.ИдентификаторСообщения, Выборка.Подписчик);
        МассивСообщений.Добавить(СтруктураСообщения);
    КонецЦикла;
    
    Возврат МассивСообщений;
КонецФункции

Функция ТекстЗапросаПоТипуПодписчика(ПодстрокаОграничения, ИмяСправочника)
    Возврат СтрШаблон(
        "ВЫБРАТЬ %1
        |    Статус.ИдентификаторСообщения КАК ИдентификаторСообщения,
        |    Статус.Подписчик КАК Подписчик
        |ИЗ
        |    РегистрСведений.инт_ТекущийСтатусРассылкиСообщений КАК Статус
        |        ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.%2 КАК Подписчики
        |        ПО Статус.Подписчик = Подписчики.Ссылка
        |ГДЕ
        |    (Статус.СтатусСообщения = &СтатусНовый)
        |    ИЛИ (Статус.СтатусСообщения = &СтатусОшибка
        |        И Статус.ДатаСледующейПопытки <= &ТекущаяДата
        |        И Статус.КоличествоПопыток < Подписчики.КоличествоПопытокОтправки)", 
        ПодстрокаОграничения, ИмяСправочника);
КонецФункции

// Процедура - Удалить запись по идентификатору
//
// Параметры:
//  ИдентификаторСообщения	 - УникальныйИдентификатор	 - ИдентификаторСообщения
//  Подписчик				 - 	СправочникСсылка.инт_Подписчики - Ссылка на подписчика.						 - 
//
Процедура УдалитьЗаписи(ИдентификаторСообщения, Подписчик = Неопределено) Экспорт
	Набор = РегистрыСведений.инт_ТекущийСтатусРассылкиСообщений.СоздатьНаборЗаписей();
	Набор.Отбор.ИдентификаторСообщения.Установить(ИдентификаторСообщения);
	Если НЕ Подписчик = Неопределено Тогда
		Набор.Отбор.Подписчик.Установить(Подписчик);
	КонецЕсли;
	Набор.Записать();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Функция ПолучитьНомерПопыткиОтправкиСообщения(ИдентификаторСообщения, Подписчик)
    КоличествоПопыток = 1;
    // в текущей парадигме, считается, что статус ошибки не может "перескакивать" с одной ошибки на другую.
    Запрос = Новый запрос;
    Запрос.Текст = "ВЫБРАТЬ
                   |    инт_ТекущийСтатусРассылкиСообщений.КоличествоПопыток КАК КоличествоПопыток
                   |ИЗ
                   |    РегистрСведений.инт_ТекущийСтатусРассылкиСообщений КАК инт_ТекущийСтатусРассылкиСообщений
                   |ГДЕ
                   |    инт_ТекущийСтатусРассылкиСообщений.ИдентификаторСообщения = &ИдентификаторСообщения
                   |    И инт_ТекущийСтатусРассылкиСообщений.Подписчик = &Подписчик";
    Запрос.УстановитьПараметр("ИдентификаторСообщения", ИдентификаторСообщения);
        Запрос.УстановитьПараметр("Подписчик", Подписчик);
    Выборка = Запрос.Выполнить().Выбрать();
    Если Выборка.Следующий() Тогда
       КоличествоПопыток = Выборка.КоличествоПопыток+1;
    КонецЕсли;
    
	Возврат КоличествоПопыток;
КонецФункции

Процедура ЗафиксироватьСтатус(ИдентификаторСообщения, Подписчик, СтатусСообщения, ТекстОшибки="")
	Запись = РегистрыСведений.инт_ТекущийСтатусРассылкиСообщений.СоздатьМенеджерЗаписи();
    Запись.ИдентификаторСообщения = ИдентификаторСообщения;
    Запись.Подписчик = Подписчик;
    Если СтатусСообщения = Перечисления.инт_СтатусыРассылкиИсходящихСообщений.Новый Тогда
        // тут ничего не надо делать. Записи нет, просто запишем ее со статусом	
    ИначеЕсли инт_СтатусыСообщенийПовтИсп.ЭтоОшибочныйСтатус(СтатусСообщения) Тогда
        Запись.КоличествоПопыток = ПолучитьНомерПопыткиОтправкиСообщения(ИдентификаторСообщения, Подписчик);
        Запись.ДатаСледующейПопытки = инт_ПодписчикиОбщий.РассчитатьДатуСледующейПопытки(ТекущаяДата(), Подписчик);
        Запись.ТекстОшибки = ТекстОшибки;
    Иначе
        Запись.Прочитать();
    КонецЕсли;
    Запись.СтатусСообщения = СтатусСообщения;
    Запись.Записать(Истина);
КонецПроцедуры

#КонецОбласти
