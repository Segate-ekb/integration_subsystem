// Copyright Copyright 2023-2024 Andrei Chernyak
// Licensed under the Apache License, Version 2.0

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Возвращает типовые временные интервалы
//
// Возвращаемое значение:
//     ТаблицаЗначений - интервалы сбора. Колонки:
//       * Интервал         - Строка - наименование интервала
//       * КоличествоСекунд - Строка
//
Функция СтандартныеИнтервалы() Экспорт
	
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(50, ДопустимаяДлина.Переменная));
	ОписаниеТиповЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(8, 0, ДопустимыйЗнак.Неотрицательный));
	
	Интервалы = Новый ТаблицаЗначений;
	Интервалы.Колонки.Добавить("Интервал", ОписаниеТиповСтрока);
	Интервалы.Колонки.Добавить("КоличествоСекунд", ОписаниеТиповЧисло);
	
	НоваяСтрока = Интервалы.Добавить();
	НоваяСтрока.Интервал = "Минута";
	НоваяСтрока.КоличествоСекунд = 60;
	
	НоваяСтрока = Интервалы.Добавить();
	НоваяСтрока.Интервал = "Пять минут";
	НоваяСтрока.КоличествоСекунд = 60 * 5;
	
	НоваяСтрока = Интервалы.Добавить();
	НоваяСтрока.Интервал = "Пятнадцать минут";
	НоваяСтрока.КоличествоСекунд = 60 * 15;
	
	НоваяСтрока = Интервалы.Добавить();
	НоваяСтрока.Интервал = "Тридцать минут";
	НоваяСтрока.КоличествоСекунд = 60 * 30;
	
	НоваяСтрока = Интервалы.Добавить();
	НоваяСтрока.Интервал = "Час";
	НоваяСтрока.КоличествоСекунд = 60 * 60;
	
	НоваяСтрока = Интервалы.Добавить();
	НоваяСтрока.Интервал = "Сутки";
	НоваяСтрока.КоличествоСекунд = 60 * 60 * 24;
	
	НоваяСтрока = Интервалы.Добавить();
	НоваяСтрока.Интервал = "Неделя";
	НоваяСтрока.КоличествоСекунд = 60 * 60 * 24 * 7;
	
	НоваяСтрока = Интервалы.Добавить();
	НоваяСтрока.Интервал = "Месяц";
	НоваяСтрока.КоличествоСекунд = 60 * 60 * 24 * 30;
	
	Возврат Интервалы;
	
КонецФункции

// Возвращает интервалы сбора показателей, определенные в регистре
//
// Возвращаемое значение:
//     ТаблицаЗначений - интервалы сбора. Колонки:
//       * Интервал         - Строка - наименование интервала
//       * КоличествоСекунд - Строка
//
Функция ВсеИнтервалы() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	пэ_ИнтервалыСбораПоказателей.Интервал КАК Интервал,
	|	пэ_ИнтервалыСбораПоказателей.КоличествоСекунд КАК КоличествоСекунд
	|ИЗ
	|	РегистрСведений.пэ_ИнтервалыСбораПоказателей КАК пэ_ИнтервалыСбораПоказателей
	|
	|УПОРЯДОЧИТЬ ПО
	|	КоличествоСекунд";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает текущую установленную настройку сбора показателей
//
// Возвращаемое значение:
//  КоличествоСекунд - Число
//
Функция ТекущийИнтервал() Экспорт
	
	Результат = 0;
	
	КлючОбъекта = "PrometheusExporter";
	КлючНастроек = "ИнтервалСбораПоказателей";
	Настройка = пэ_Служебный.ХранилищеОбщихНастроекЗагрузить(КлючОбъекта, КлючНастроек, , , );
	Если Настройка <> Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Интервал", Настройка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	пэ_ИнтервалыСбораПоказателей.КоличествоСекунд КАК КоличествоСекунд
		|ИЗ
		|	РегистрСведений.пэ_ИнтервалыСбораПоказателей КАК пэ_ИнтервалыСбораПоказателей
		|ГДЕ
		|	пэ_ИнтервалыСбораПоказателей.Интервал = &Интервал";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			Результат = Выборка.КоличествоСекунд;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли