////////////////////////////////////////////////////////////////////////////////
// инт_ОчередьОтправкиИсходящихСообщений
//  
////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура - Зарегистрировать сообщение к рассылке
// Процедура регистрирует сообщения для подписчиков
// Параметры:
//  ИдентификаторСообщения    -  УникальныйИдентификатор                - Уникальный идентификатор рассылаемого сообщения 
//  МассивПодписчиков         -  Массив        - Массив элементов СправочникСсылка.инт_Подписчики которым будет отправлено данное сообщение.
//
Процедура ЗарегистрироватьСообщениеКРассылкеПодписчикам(ИдентификаторСообщения, МассивПодписчиков) Экспорт

    Для Каждого Подписчик Из МассивПодписчиков Цикл
        ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.ОчередьОтправкиСообщений",УровеньЖурналаРегистрации.Информация,,,"Зарегистрируем сообщение по подписчику "+ Строка(Подписчик));
        ЗарегистрироватьСообщениеКРассылкеПодписчику(ИдентификаторСообщения, Подписчик);
    КонецЦикла;
    
КонецПроцедуры
  
// Процедура - Зарегистрировать сообщение к рассылке
// Процедура регистрирует сообщения для подписчиков
// Параметры:
//  ИдентификаторСообщения    -  УникальныйИдентификатор  - Уникальный идентификатор рассылаемого сообщения 
//  Подписчик         -  СправочникСсылка.инт_Подписчики  - Подписчик которому будет отправлено данное сообщение.
//
Процедура ЗарегистрироватьСообщениеКРассылкеПодписчику(ИдентификаторСообщения, Подписчик) Экспорт
    Если НЕ ЗначениеЗаполнено(Подписчик) Тогда
    	ВызватьИсключение "Подписчик не заполнен! Обязательно заполнение подписчика для регистрации сообщения в очередь отправки!";
    КонецЕсли;
    Если НЕ Подписчик.Активен Тогда
        // по не активным подписунам не будет сообщений
    	Возврат;
    КонецЕсли;
    НачатьТранзакцию();
    Попытка
        Запись = РегистрыСведений.инт_ОчередьОтправкиИсходящихСообщений.СоздатьМенеджерЗаписи();
        Запись.ИдентификаторСообщения = ИдентификаторСообщения;
        Запись.Подписчик = Подписчик;
        Запись.Записать();
        
        РегистрыСведений.инт_ТекущийСтатусРассылкиСообщений.ЗаписатьСтатусСообщения(Запись.ИдентификаторСообщения, Подписчик, Перечисления.инт_СтатусыРассылкиИсходящихСообщений.Новый);
        ЗафиксироватьТранзакцию();
    Исключение
        ОтменитьТранзакцию();
        ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.ОчередьОтправкиСообщений",УровеньЖурналаРегистрации.Ошибка,,,ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
        ВызватьИсключение;
    КонецПопытки;

КонецПроцедуры

// Процедура - Сформировать сообщение
//
// Параметры:
//  Сообщение     - Структура - Структура содержащая <ИдентификаторСообщения, Подписчик> 
//
Процедура СформироватьСообщение(Сообщение) Экспорт
	ДанныеИсходящегоСообщения = РегистрыСведений.инт_ОчередьИсходящихСообщений.ПолучитьДанныеОчередиПоИдентификатору(Сообщение.ИдентификаторСообщения, "СформированноеСообщение");
    
    СериализованноеСообщение = Справочники.инт_Подписчики.СформироватьСообщениеПоПодписчику(ДанныеИсходящегоСообщения.СформированноеСообщение, Сообщение.Подписчик, Сообщение.ИдентификаторСообщения);
 
    НачатьТранзакцию();
    Попытка
        ЗаписатьСформированноеСообщение(Сообщение.ИдентификаторСообщения, Сообщение.Подписчик, СериализованноеСообщение);
        РегистрыСведений.инт_ТекущийСтатусРассылкиСообщений.ЗаписатьСтатусСообщения(Сообщение.ИдентификаторСообщения, Сообщение.Подписчик, Перечисления.инт_СтатусыРассылкиИсходящихСообщений.Сформирован);
        ЗафиксироватьТранзакцию();
    Исключение
        ОтменитьТранзакцию();
        ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
        РегистрыСведений.инт_ТекущийСтатусРассылкиСообщений.ЗаписатьСтатусСообщения(Сообщение.ИдентификаторСообщения, Сообщение.Подписчик, Перечисления.инт_СтатусыРассылкиИсходящихСообщений.ОшибкаОбработки, ПодробноеПредставлениеОшибки);
        СообщениеОбОшибке = СтрШаблон("При попытке сформировать сообщение с идентификатором <%1> для подписчика <%2> возникла ошибка.
        |
        |Информация об ошибке: %3", Сообщение.ИдентификаторСообщения, Сообщение.Подписчик, ПодробноеПредставлениеОшибки);
        ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.МенеджерПотоковОтправкиСообщений", УровеньЖурналаРегистрации.Ошибка,,,СообщениеОбОшибке);
        ВызватьИсключение;
    КонецПопытки;
    
КонецПроцедуры

// Процедура - Отправить сообщение
// Пытается отправить сообщение способом указанным в подписчике.
// Параметры:
//   Сообщение     - Структура - Структура содержащая <ИдентификаторСообщения, Подписчик>  
//
Процедура ОтправитьСообщение(Сообщение, ОтложенныеДействия) Экспорт
    ДанныеСообщения = РегистрыСведений.инт_ОчередьОтправкиИсходящихСообщений.СоздатьМенеджерЗаписи();
    ДанныеСообщения.ИдентификаторСообщения = Сообщение.ИдентификаторСообщения;
    ДанныеСообщения.Подписчик = Сообщение.Подписчик;
    ДанныеСообщения.Прочитать();

    НачатьТранзакцию();
    Попытка
        Справочники.инт_Подписчики.ОтправитьСообщение(ДанныеСообщения, ОтложенныеДействия);
        РегистрыСведений.инт_ТекущийСтатусРассылкиСообщений.ЗаписатьСтатусСообщения(Сообщение.ИдентификаторСообщения, Сообщение.Подписчик, Перечисления.инт_СтатусыРассылкиИсходящихСообщений.Отправлен);
        ЗафиксироватьТранзакцию();
    Исключение
        ОтменитьТранзакцию();
        ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
        РегистрыСведений.инт_ТекущийСтатусРассылкиСообщений.ЗаписатьСтатусСообщения(Сообщение.ИдентификаторСообщения, Сообщение.Подписчик, Перечисления.инт_СтатусыРассылкиИсходящихСообщений.ОшибкаОбработки, ПодробноеПредставлениеОшибки);
        СообщениеОбОшибке = СтрШаблон("При попытке сформировать сообщение с идентификатором <%1> для подписчика <%2> возникла ошибка.
        |
        |Информация об ошибке: %3", Сообщение.ИдентификаторСообщения, Сообщение.Подписчик, ПодробноеПредставлениеОшибки);
        ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.МенеджерПотоковОтправкиСообщений", УровеньЖурналаРегистрации.Ошибка,,,СообщениеОбОшибке);
        ВызватьИсключение;
    КонецПопытки;
КонецПроцедуры

// Процедура - Удалить из сообщение из очереди
//
// Параметры:
//  ИдентификаторСообщения    -  УникальныйИдентификатор        - Уникальный идентификатор рассылаемого сообщения 
//  Подписчик         -  СправочникСсылка.инт_Подписчики        - Ссылка на подписчика.
//
Процедура УдалитьИзСообщениеИзОчереди(ИдентификаторСообщения, Подписчик = Неопределено) Экспорт
	НачатьТранзакцию();
	Попытка
		РегистрыСведений.инт_ТекущийСтатусРассылкиСообщений.УдалитьЗаписи(ИдентификаторСообщения, Подписчик);
		РегистрыСведений.инт_ИсторияСтатусовРассылкиСообщений.ОчиститьИсториюСообщений(ИдентификаторСообщения, Подписчик);
		
		Набор = РегистрыСведений.инт_ОчередьОтправкиИсходящихСообщений.СоздатьНаборЗаписей();
		Набор.Отбор.ИдентификаторСообщения.Установить(ИдентификаторСообщения);
		Если НЕ Подписчик = Неопределено Тогда
			Набор.Отбор.Подписчик.Установить(Подписчик);
		КонецЕсли;
		Набор.Записать();
		ЗафиксироватьТранзакцию();
	Исключение
	    ОтменитьТранзакцию();
		
		СообщениеОбОшибке = СтрШаблон("При попытке удаления информации о сообщении с идентификатором <%1> возникла ошибка!
		|
		|ИнформацияОбОшибке: %2",
		ИдентификаторСообщения,
		ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации("ПодсистемаИнтеграции.ОчередьОтправкиСообщений", УровеньЖурналаРегистрации.Ошибка,,,СообщениеОбОшибке);
		ВызватьИсключение;
	КонецПопытки;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаписатьСформированноеСообщение(ИдентификаторСообщения, Подписчик, СформированноеСообщение)
    
    Если НЕ ЗначениеЗаполнено(СформированноеСообщение) Тогда
        ВызватьИсключение "Сообщение должно быть заполнено!";
    КонецЕсли;
    
  	Запись = РегистрыСведений.инт_ОчередьОтправкиИсходящихСообщений.СоздатьМенеджерЗаписи();
    Запись.ИдентификаторСообщения = ИдентификаторСообщения;
    Запись.Подписчик = Подписчик;
    Запись.СформированноеСообщение = СформированноеСообщение;
    Запись.Записать(Истина);
КонецПроцедуры

#КонецОбласти

#КонецЕсли
