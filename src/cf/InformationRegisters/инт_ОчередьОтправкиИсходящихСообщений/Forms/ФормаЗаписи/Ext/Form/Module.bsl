#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.КлючЗаписи = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписьРСТекущийСтатус = ПолучитьТекущийСтатусПоКлючу(Параметры.КлючЗаписи);
	ЗначениеВРеквизитФормы(ЗаписьРСТекущийСтатус, "ЗаписьРСТекущийСтатусРассылки");
																		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьСтатус(Команда)
			
	ОткрытьФорму("Перечисление.инт_СтатусыРассылкиИсходящихСообщений.ФормаВыбора",,,,,,
				Новый ОписаниеОповещения("ИзменитьСтатусЗавершение", ЭтотОбъект));
									
КонецПроцедуры
			
&НаКлиенте
Процедура ИсторияСтатусов(Команда)
		
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ИдентификаторСообщения", Запись.ИдентификаторСообщения);
	ПараметрыОтбора.Вставить("Подписчик", Запись.Подписчик);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
	
	ОткрытьФорму("РегистрСведений.инт_ИсторияСтатусовРассылкиСообщений.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
		
КонецПроцедуры
			
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьТекущийСтатусПоКлючу(КлючЗаписи)
		
	МенеджерЗаписи = РегистрыСведений.инт_ТекущийСтатусРассылкиСообщений.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, КлючЗаписи);
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() Тогда
		Возврат МенеджерЗаписи;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ИзменитьСтатусНаСервере(Статус, ИдентификаторСообщения, Подписчик, ТекстОшибки)
		
    РегистрыСведений.инт_ТекущийСтатусРассылкиСообщений.ЗаписатьСтатусСообщения(
		ИдентификаторСообщения, Подписчик, Статус, ТекстОшибки);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСтатусЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьСтатусНаСервере(ВыбранныйЭлемент,
							Запись.ИдентификаторСообщения,
							Запись.Подписчик,
							ЗаписьРСТекущийСтатусРассылки.ТекстОшибки);
	
	ОбновитьСтатусНаФорме();
										
КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатусНаФорме()
	
	КлючЗаписи = Новый Структура("ИдентификаторСообщения, Подписчик",
								 Запись.ИдентификаторСообщения,
								 Запись.Подписчик);
	ЗаписьРСТекущийСтатус = ПолучитьТекущийСтатусПоКлючу(КлючЗаписи);
	ЗначениеВРеквизитФормы(ЗаписьРСТекущийСтатус, "ЗаписьРСТекущийСтатусРассылки");
	
КонецПроцедуры

#КонецОбласти
