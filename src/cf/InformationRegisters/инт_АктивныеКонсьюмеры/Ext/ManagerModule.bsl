////////////////////////////////////////////////////////////////////////////////
// инт_АктивныеКонсьюмеры
//
// Регистр сведений для хранения информации об активных консьюмерах.
// Модуль менеджера содержит методы регистрации и управления записями.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Регистрирует новый консьюмер в регистре.
//
// Параметры:
//  Подписка                    - ОпределяемыйТип.инт_Подписка - Подписчик Kafka/RabbitMQ
//  ИдентификаторФоновогоЗадания - УникальныйИдентификатор - ID фонового задания
//  СтатусАктивности            - Строка - "Асинхронный" или "Синхронный"
//
Процедура ЗарегистрироватьКонсьюмер(Подписка, ИдентификаторФоновогоЗадания, СтатусАктивности = "Активен") Экспорт
	
	НаборЗаписей = РегистрыСведений.инт_АктивныеКонсьюмеры.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Подписка.Установить(Подписка);
	НаборЗаписей.Отбор.ИдентификаторФоновогоЗадания.Установить(ИдентификаторФоновогоЗадания);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Подписка = Подписка;
	Запись.ИдентификаторФоновогоЗадания = ИдентификаторФоновогоЗадания;
	Запись.ВремяЗапуска = ТекущаяДатаСеанса();
	Запись.ПоследняяАктивность = ТекущаяДатаСеанса();
	Запись.СтатусАктивности = СтатусАктивности;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Обновляет время последней активности консьюмера.
//
// Параметры:
//  Подписка                    - ОпределяемыйТип.инт_Подписка - Подписчик
//  ИдентификаторФоновогоЗадания - УникальныйИдентификатор - ID фонового задания
//
Процедура ОбновитьАктивность(Подписка, ИдентификаторФоновогоЗадания) Экспорт
	
	НаборЗаписей = РегистрыСведений.инт_АктивныеКонсьюмеры.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Подписка.Установить(Подписка);
	НаборЗаписей.Отбор.ИдентификаторФоновогоЗадания.Установить(ИдентификаторФоновогоЗадания);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() > 0 Тогда
		НаборЗаписей[0].ПоследняяАктивность = ТекущаяДатаСеанса();
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Обновляет время активности по подписке (без указания ID задания).
//
// Параметры:
//  Подписка - ОпределяемыйТип.инт_Подписка - Подписчик
//
Процедура ОбновитьАктивностьПоПодписке(Подписка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктивныеКонсьюмеры.Подписка КАК Подписка,
		|	АктивныеКонсьюмеры.ИдентификаторФоновогоЗадания КАК ИдентификаторФоновогоЗадания
		|ИЗ
		|	РегистрСведений.инт_АктивныеКонсьюмеры КАК АктивныеКонсьюмеры
		|ГДЕ
		|	АктивныеКонсьюмеры.Подписка = &Подписка";
	
	Запрос.УстановитьПараметр("Подписка", Подписка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОбновитьАктивность(Выборка.Подписка, Выборка.ИдентификаторФоновогоЗадания);
	КонецЦикла;
	
КонецПроцедуры

// Удаляет запись о консьюмере из регистра.
//
// Параметры:
//  Подписка                    - ОпределяемыйТип.инт_Подписка - Подписчик
//  ИдентификаторФоновогоЗадания - УникальныйИдентификатор - ID фонового задания
//
Процедура УдалитьКонсьюмер(Подписка, ИдентификаторФоновогоЗадания) Экспорт
	
	НаборЗаписей = РегистрыСведений.инт_АктивныеКонсьюмеры.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Подписка.Установить(Подписка);
	НаборЗаписей.Отбор.ИдентификаторФоновогоЗадания.Установить(ИдентификаторФоновогоЗадания);
	НаборЗаписей.Записать(); // Пустой набор = удаление
	
КонецПроцедуры

// Возвращает список активных консьюмеров.
//
// Параметры:
//  Подписка - ОпределяемыйТип.инт_Подписка, Неопределено - Фильтр по подписке (опционально)
//
// Возвращаемое значение:
//  Массив из Структура - Записи регистра
//
Функция ПолучитьАктивныеКонсьюмеры(Подписка = Неопределено) Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктивныеКонсьюмеры.Подписка КАК Подписка,
		|	АктивныеКонсьюмеры.ИдентификаторФоновогоЗадания КАК ИдентификаторФоновогоЗадания,
		|	АктивныеКонсьюмеры.ВремяЗапуска КАК ВремяЗапуска,
		|	АктивныеКонсьюмеры.ПоследняяАктивность КАК ПоследняяАктивность,
		|	АктивныеКонсьюмеры.СтатусАктивности КАК СтатусАктивности
		|ИЗ
		|	РегистрСведений.инт_АктивныеКонсьюмеры КАК АктивныеКонсьюмеры
		|ГДЕ
		|	(&Подписка = НЕОПРЕДЕЛЕНО ИЛИ АктивныеКонсьюмеры.Подписка = &Подписка)";
	
	Запрос.УстановитьПараметр("Подписка", Подписка);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Запись = Новый Структура("Подписка, ИдентификаторФоновогоЗадания, ВремяЗапуска, ПоследняяАктивность, СтатусАктивности");
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Результат.Добавить(Запись);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Очищает все записи регистра.
//
Процедура ОчиститьРегистр() Экспорт
	
	НаборЗаписей = РегистрыСведений.инт_АктивныеКонсьюмеры.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Устанавливает статус Drained для подписки.
// Если запись для подписки уже существует — обновляет статус.
// Если нет — создаёт новую запись с пустым ИдентификаторФоновогоЗадания.
//
// Параметры:
//  Подписка - ОпределяемыйТип.инт_Подписка - Подписчик для drain
//
Процедура УстановитьСтатусDrained(Подписка) Экспорт
	
	// Ищем существующие записи для этой подписки
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктивныеКонсьюмеры.Подписка КАК Подписка,
		|	АктивныеКонсьюмеры.ИдентификаторФоновогоЗадания КАК ИдентификаторФоновогоЗадания
		|ИЗ
		|	РегистрСведений.инт_АктивныеКонсьюмеры КАК АктивныеКонсьюмеры
		|ГДЕ
		|	АктивныеКонсьюмеры.Подписка = &Подписка";
	
	Запрос.УстановитьПараметр("Подписка", Подписка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		// Обновляем статус у существующей записи
		НаборЗаписей = РегистрыСведений.инт_АктивныеКонсьюмеры.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Подписка.Установить(Выборка.Подписка);
		НаборЗаписей.Отбор.ИдентификаторФоновогоЗадания.Установить(Выборка.ИдентификаторФоновогоЗадания);
		
		Запись = НаборЗаписей.Добавить();
		Запись.Подписка = Выборка.Подписка;
		Запись.ИдентификаторФоновогоЗадания = Выборка.ИдентификаторФоновогоЗадания;
		Запись.ВремяЗапуска = ТекущаяДатаСеанса();
		Запись.ПоследняяАктивность = ТекущаяДатаСеанса();
		Запись.СтатусАктивности = "Drained";
		
		НаборЗаписей.Записать();
	Иначе
		// Создаём новую запись с пустым идентификатором ФЗ
		ЗарегистрироватьКонсьюмер(Подписка, Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"), "Drained");
	КонецЕсли;
	
КонецПроцедуры

// Снимает статус Drained с подписки.
// Удаляет все записи со статусом Drained для данной подписки.
//
// Параметры:
//  Подписка - ОпределяемыйТип.инт_Подписка - Подписчик для восстановления
//
Процедура СнятьСтатусDrained(Подписка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктивныеКонсьюмеры.Подписка КАК Подписка,
		|	АктивныеКонсьюмеры.ИдентификаторФоновогоЗадания КАК ИдентификаторФоновогоЗадания
		|ИЗ
		|	РегистрСведений.инт_АктивныеКонсьюмеры КАК АктивныеКонсьюмеры
		|ГДЕ
		|	АктивныеКонсьюмеры.Подписка = &Подписка
		|	И АктивныеКонсьюмеры.СтатусАктивности = ""Drained""";
	
	Запрос.УстановитьПараметр("Подписка", Подписка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		УдалитьКонсьюмер(Выборка.Подписка, Выборка.ИдентификаторФоновогоЗадания);
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает статус GracefulStop для ВСЕХ записей в регистре.
//
Процедура УстановитьГрейсфулСтоп() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктивныеКонсьюмеры.Подписка КАК Подписка,
		|	АктивныеКонсьюмеры.ИдентификаторФоновогоЗадания КАК ИдентификаторФоновогоЗадания,
		|	АктивныеКонсьюмеры.ВремяЗапуска КАК ВремяЗапуска
		|ИЗ
		|	РегистрСведений.инт_АктивныеКонсьюмеры КАК АктивныеКонсьюмеры
		|ГДЕ
		|	АктивныеКонсьюмеры.СтатусАктивности <> ""Drained""";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.инт_АктивныеКонсьюмеры.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Подписка.Установить(Выборка.Подписка);
		НаборЗаписей.Отбор.ИдентификаторФоновогоЗадания.Установить(Выборка.ИдентификаторФоновогоЗадания);
		
		Запись = НаборЗаписей.Добавить();
		Запись.Подписка = Выборка.Подписка;
		Запись.ИдентификаторФоновогоЗадания = Выборка.ИдентификаторФоновогоЗадания;
		Запись.ВремяЗапуска = Выборка.ВремяЗапуска;
		Запись.ПоследняяАктивность = ТекущаяДатаСеанса();
		Запись.СтатусАктивности = "GracefulStop";
		
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Очищает все записи со статусом GracefulStop.
//
Процедура ОчиститьGracefulStop() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктивныеКонсьюмеры.Подписка КАК Подписка,
		|	АктивныеКонсьюмеры.ИдентификаторФоновогоЗадания КАК ИдентификаторФоновогоЗадания
		|ИЗ
		|	РегистрСведений.инт_АктивныеКонсьюмеры КАК АктивныеКонсьюмеры
		|ГДЕ
		|	АктивныеКонсьюмеры.СтатусАктивности = ""GracefulStop""";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		УдалитьКонсьюмер(Выборка.Подписка, Выборка.ИдентификаторФоновогоЗадания);
	КонецЦикла;
	
КонецПроцедуры

// Проверяет наличие запроса на остановку (Drained или GracefulStop) для любой из подписок.
//
// Параметры:
//  МассивПодписокСсылки - Массив из ОпределяемыйТип.инт_Подписка - Подписки для проверки
//
// Возвращаемое значение:
//  Структура:
//   * НужнаОстановка - Булево - Истина, если найден запрос на остановку
//   * Причина        - Строка - "Drained" или "GracefulStop"
//
Функция ПроверитьЗапросНаОстановку(МассивПодписокСсылки) Экспорт
	
	Результат = Новый Структура("НужнаОстановка, Причина", Ложь, "");
	
	Если МассивПодписокСсылки.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	АктивныеКонсьюмеры.СтатусАктивности КАК СтатусАктивности
		|ИЗ
		|	РегистрСведений.инт_АктивныеКонсьюмеры КАК АктивныеКонсьюмеры
		|ГДЕ
		|	АктивныеКонсьюмеры.Подписка В (&МассивПодписок)
		|	И (АктивныеКонсьюмеры.СтатусАктивности = ""Drained""
		|		ИЛИ АктивныеКонсьюмеры.СтатусАктивности = ""GracefulStop"")";
	
	Запрос.УстановитьПараметр("МассивПодписок", МассивПодписокСсылки);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат.НужнаОстановка = Истина;
		Результат.Причина = Выборка.СтатусАктивности;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив подписок со статусом Drained.
//
// Возвращаемое значение:
//  Массив из Структура:
//   * Подписка  - ОпределяемыйТип.инт_Подписка - Ссылка на подписку
//   * ДатаDrain - Дата - Время установки статуса Drained
//
Функция ПолучитьDrainedПодписки() Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктивныеКонсьюмеры.Подписка КАК Подписка,
		|	АктивныеКонсьюмеры.ПоследняяАктивность КАК ДатаDrain
		|ИЗ
		|	РегистрСведений.инт_АктивныеКонсьюмеры КАК АктивныеКонсьюмеры
		|ГДЕ
		|	АктивныеКонсьюмеры.СтатусАктивности = ""Drained""";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Запись = Новый Структура("Подписка, ДатаDrain");
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Результат.Добавить(Запись);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
