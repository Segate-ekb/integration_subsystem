//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастроитьСтраницуНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьОписаниеРежимаУстановкиРеквизитов();
	ОбновитьОписаниеФорматаИменованияПеременных();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастройкиРежимУстановкиРеквизитовПриИзменении(Элемент)
	
	ОбновитьОписаниеРежимаУстановкиРеквизитов();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиФорматИменованияПеременныхПриИзменении(Элемент)
	
	ОбновитьОписаниеФорматаИменованияПеременных();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьТекст(Команда)
	
	СформироватьТекстНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьСтраницуНастроек()
	
	ЕстьНастройкиКодогенерации = ЮТГенераторКода.ЕстьНастройкиКодогенерации();
	
	РежимыУстановкиРеквизитов = ЮТГенераторКода.РежимыУстановкиРеквизитов();
	ФорматИменованияПеременных = ЮТГенераторКода.ФорматИменованияПеременных();
	
	НастройкиРежимУстановкиРеквизитов = РежимыУстановкиРеквизитов.Авто;
	НастройкиФорматИменованияПеременных = ФорматИменованияПеременных.ПоТипу;
	
	Если НЕ ЕстьНастройкиКодогенерации Тогда
		НастройкиРежимУстановкиРеквизитов = РежимыУстановкиРеквизитов.Создание;
		РежимыУстановкиРеквизитов.Удалить("Авто");
	КонецЕсли;
	
	Для Каждого Элемент Из РежимыУстановкиРеквизитов Цикл
		Элементы.НастройкиРежимУстановкиРеквизитов.СписокВыбора.Добавить(Элемент.Значение, Элемент.Значение);
	КонецЦикла;
	
	Для Каждого Элемент Из ФорматИменованияПеременных Цикл
		Элементы.НастройкиФорматИменованияПеременных.СписокВыбора.Добавить(Элемент.Значение, Элемент.Значение);
	КонецЦикла;
	
	Если НЕ ЕстьНастройкиКодогенерации Тогда
		Элементы.НастройкиИгнорироватьКодИнициализацииПоТипам.Видимость = Ложь;
		НастройкиИгнорироватьКодИнициализацииПоТипам = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОтмеченныеОбъекты()
	
	ОтмеченныеСсылки = Новый Массив;
	
	Для Каждого Элемент Из Данные Цикл
		Если Элемент.Пометка Тогда
			ОтмеченныеСсылки.Добавить(Элемент.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОтмеченныеСсылки;
	
КонецФункции

&НаСервере
Процедура СформироватьТекстНаСервере()
	
	ЕстьНастройкиКодогенерации = ЮТГенераторКода.ЕстьНастройкиКодогенерации();
	
	ОтмеченныеСсылки = ОтмеченныеОбъекты();
	Режимы = ЮТГенераторКода.РежимыУстановкиРеквизитов();
	
	ПараметрыГенерацииКода = ЮТГенераторКода.ПараметрыГенерацииКода();
	ПараметрыГенерацииКода.ПропускатьПустыеЗначения = НастройкиПропускатьПустыеЗначения;
	ПараметрыГенерацииКода.ИспользоватьПоиск = НастройкиРежимУстановкиРеквизитов = Режимы.Поиск;
	ПараметрыГенерацииКода.ИспользоватьНастройкуПостоянныеДанные = ЕстьНастройкиКодогенерации И НастройкиРежимУстановкиРеквизитов = Режимы.Авто;
	ПараметрыГенерацииКода.ФорматИменованияПеременных = НастройкиФорматИменованияПеременных;
	ПараметрыГенерацииКода.ИгнорироватьКодИнициализацииПоТипам = НЕ ЕстьНастройкиКодогенерации ИЛИ НастройкиИгнорироватьКодИнициализацииПоТипам;
	
	Результат = ЮТГенераторКодаСлужебныйСервер.СформироватьКодПоСсылкамНаОбъекты(ОтмеченныеСсылки, ПараметрыГенерацииКода);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОписаниеРежимаУстановкиРеквизитов()
	
	Режимы = ЮТГенераторКода.РежимыУстановкиРеквизитов();
	
	Если НастройкиРежимУстановкиРеквизитов = Режимы.Авто Тогда
		Описание = "Автоматический режим. Для типов указанных в НастройкиКодогенерации.ПостоянныеДанные выполняется поиск, для прочих создание";
	ИначеЕсли НастройкиРежимУстановкиРеквизитов = Режимы.Поиск Тогда
		Описание = "Используются существующие в базе данные. Выполняется поиск по номеру, коду или наименованию";
	ИначеЕсли НастройкиРежимУстановкиРеквизитов = Режимы.Создание Тогда
		Описание = "Используются программно созданные данные";
	Иначе
		Описание = "Неизвестный режим";
	КонецЕсли;
	
	Элементы.НастройкиРежимУстановкиРеквизитов.Подсказка = Описание;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОписаниеФорматаИменованияПеременных()
	
	Формат = ЮТГенераторКода.ФорматИменованияПеременных();
	ОписаниеСуффикс = "Дополнительно, если уже имеется переменная с таким же именем, то будет добавлен числовой суффикс: 'Товары', 'Товары2', 'Товары3'";
	Если НастройкиФорматИменованияПеременных = Формат.ПоТипу Тогда
		Описание = "Имя переменной формируется на основании типа значения.
		|Например, для значения 'Справочник.Товары.Универсальный телепорт' имя переменной будет - 'Товары'.
		|
		|" + ОписаниеСуффикс;
	ИначеЕсли НастройкиФорматИменованияПеременных = Формат.ПоПредставлению Тогда
		Описание = "Имя переменной формируется на основании представления значения.
		|Если представление начинается с числа, то будет добавлен символ '_'.
		|Например, для значения 'Справочник.Товары.Универсальный телепорт' имя переменной будет - 'УниверсальныйТелепорт'.
		|
		|" + ОписаниеСуффикс;
	ИначеЕсли НастройкиФорматИменованияПеременных = Формат.ПоТипуИПредставлению Тогда
		Описание = "Имя переменной формируется на основании типа и представления значения.
		|Например, для значения 'Справочник.Товары.Универсальный телепорт' имя переменной будет - 'ТоварыУниверсальныйТелепорт'.
		|
		|" + ОписаниеСуффикс;
	Иначе
		Описание = "Неизвестный формат";
	КонецЕсли;
	
	Элементы.НастройкиФорматИменованияПеременных.Подсказка = Описание;
	
КонецПроцедуры

#КонецОбласти
