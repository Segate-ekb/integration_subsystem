//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Функция - Подготовленные данные для создания тестового объекта
//
// Параметры:
//  СсылкаНаОбъект		 - ЛюбаяСсылка	 - Ссылка на объект БД
//  ОтобранныеДвижения	 - 	Массив - Массив с именами регистров движений
// 
// Возвращаемое значение:
//  Структура
//		* КодТестовогоМодуля - код создания объектов
//		* КодМодуляМенеджераОбработки - Код загрузки эталон в таблицы значений
//		* МакетыЭталонов - Массив - Табличные документы с движениями документов
//
Функция ПодготовленныеДанныеДляСозданияТестовогоОбъекта(СсылкаНаОбъект, ОтобранныеДвижения, НастройкиФормирования) Экспорт
	
	КодСоздания = Новый Структура;
	
	КодСоздания.Вставить("КодТестовогоМодуля", "");
	КодСоздания.Вставить("КодМодуляМенеджераОбработки", "");
	КодСоздания.Вставить("МакетыЭталонов", Новый Массив);
	
	ДанныеОбъекта = ПодготовленныеДанныеОбъекта(СсылкаНаОбъект, ОтобранныеДвижения);
	
	Переменные = Переменные();
	
	КонструкторРеквизиты = ЮТРаботаСТекстом.КонструкторТекста();
	СформироватьКодРеквизитов(ДанныеОбъекта, КонструкторРеквизиты, Переменные);
		
	КонструкторТЧ = ЮТРаботаСТекстом.КонструкторТекста();

	Если НастройкиФормирования.ИспользоватьМакетыТабличныхЧастей Тогда
		СформироватьКодТабличныхЧастейСМакетами(ДанныеОбъекта.ТабличныеЧасти, КонструкторТЧ, Переменные);
		НужноФормироватьЗаменяемыеЗначения = Истина;
	Иначе
		СформироватьКодТабличныхЧастей(ДанныеОбъекта.ТабличныеЧасти, КонструкторТЧ, Переменные);
		НужноФормироватьЗаменяемыеЗначения = Ложь;
	КонецЕсли;

	Если ДанныеОбъекта.ЭтоДокумент И ДанныеОбъекта.ДвиженияОбъекта.Количество() Тогда
		ПодготовитьДанныеДвижений(
				ДанныеОбъекта,
				КодСоздания,
				Переменные,
				НастройкиФормирования.ИнлайнМакетыДвижений
			);
		НужноФормироватьЗаменяемыеЗначения = Истина;
	КонецЕсли;
	
	КонструкторЗаменяемыхЗначений = ЮТРаботаСТекстом.КонструкторТекста();
	Если НужноФормироватьЗаменяемыеЗначения Тогда
		СформироватьКодУстановкиЗаменяемыхЗначений(КонструкторЗаменяемыхЗначений, Переменные);
	КонецЕсли;
	
	КонструкторПеременные = ЮТРаботаСТекстом.КонструкторТекста();
	СформироватьКодПеременных(КонструкторПеременные, Переменные);
	
	Конструктор = ЮТРаботаСТекстом.КонструкторТекста()
		.ДобавитьТекст(КонструкторПеременные.Текст()).ПереносСтроки()
		.НоваяСтрокаПоШаблону(ШаблонВызоваКонструктораОбъекта(), ДанныеОбъекта.ТипОбъекта)
		.ДобавитьТекст(КонструкторРеквизиты.Текст()).ПереносСтроки()
		.ДобавитьТекст(КонструкторЗаменяемыхЗначений.Текст()).ПереносСтроки()
		.ДобавитьТекст(КонструкторТЧ.Текст())
	;
	
	КодСоздания.КодТестовогоМодуля = Конструктор.Текст();
	
	Возврат КодСоздания;
	
КонецФункции

// Функция - Подготовленные данные для создания тестового объекта
//
// Параметры:
//  СсылкаНаОбъект		 - ЛюбаяСсылка	 - Ссылка на объект БД
//  ОтобранныеДвижения	 - 	Массив - Массив с именами регистров движений
// 
// Возвращаемое значение:
//  Структура
//		* КодЗаполненияДвижений - код создания объектов
//
Функция ПодготовленныеДанныеДляЗагрузкиДвижений(СсылкаНаОбъект, ОтобранныеДвижения, НастройкиФормирования) Экспорт
	
	КодЗаполнения = Новый Структура;
	КодЗаполнения.Вставить("КодЗаполненияДвижений", "");
	
	ДанныеОбъекта = ПодготовленныеДанныеОбъекта(СсылкаНаОбъект, ОтобранныеДвижения);
	
	Переменные = Переменные();
	
	Регистратор = ПодготовленныеДанныеРеквизита("Регистратор", СсылкаНаОбъект);
	ДобавитьНовуюПеременную(Переменные, Регистратор);
	
	ИспользоватьМакетыРегистровДвижений = НастройкиФормирования.ИспользоватьМакетыРегистровДвижений;
	
	КонструкторОсновнойКод =
		ЮТРаботаСТекстом.КонструкторТекста();
		
	Для Каждого НаборДвижений Из ДанныеОбъекта.ДанныеДвиженийОбъекта Цикл
		Если ИспользоватьМакетыРегистровДвижений Тогда
			СформироватьКодКонструктораДвиженийСИнлайнМакетами(КонструкторОсновнойКод, НаборДвижений, Переменные, СсылкаНаОбъект);
		Иначе
			СформироватьКодКонструктораДвижений(КонструкторОсновнойКод, НаборДвижений, Переменные, СсылкаНаОбъект);
		КонецЕсли;
	КонецЦикла;
		
	КонструкторПеременные = ЮТРаботаСТекстом.КонструкторТекста();
	СформироватьКодПеременных(КонструкторПеременные, Переменные);
	
	Конструктор = ЮТРаботаСТекстом.КонструкторТекста()
		.ДобавитьТекст(КонструкторПеременные.Текст()).ПереносСтроки()
		.ДобавитьТекст(КонструкторОсновнойКод.Текст()).ПереносСтроки()
	;
	
	КодЗаполнения.КодЗаполненияДвижений = Конструктор.Текст();
	
	Возврат КодЗаполнения;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодготовленныеДанныеОбъекта(СсылкаНаОбъект, ОтобранныеДвижения)
	
	ПолученныйОбъект  = СсылкаНаОбъект.ПолучитьОбъект();
	ДанныеОбъекта = ЗначенияЗаполненныхРеквизитовОбъекта(ПолученныйОбъект);
	
	Если ДанныеОбъекта.ЭтоДокумент Тогда
		ДвиженияОбъекта = ПодговитьДвиженияДокумента(ПолученныйОбъект, ОтобранныеДвижения);
		ДанныеОбъекта.Вставить("ДвиженияОбъекта", ДвиженияОбъекта);
		
		ДанныеДвиженийОбъекта = ПодготовитьДанныеДвиженийДокумента(ДвиженияОбъекта);
		ДанныеОбъекта.Вставить("ДанныеДвиженийОбъекта", ДанныеДвиженийОбъекта);
	КонецЕсли;
	
	Возврат ДанныеОбъекта;
	
КонецФункции

Процедура ПодготовитьДанныеДвижений(ДанныеОбъекта, КодСоздания, Переменные, ИнлайнМакет)
	
	Конструктор =
		ЮТРаботаСТекстом.КонструкторТекста();
	
	ЭталоныДвижений = ЭталоныДвиженийДокумента(ДанныеОбъекта.ДвиженияОбъекта, Переменные);

	СформироватьКодПодготовкиЭталонов(ЭталоныДвижений, Переменные, Конструктор, ИнлайнМакет);
	
	КодСоздания.КодМодуляМенеджераОбработки = Конструктор.Текст();

	Если Не ИнлайнМакет Тогда
		МакетыЭталонов = МакетыЭталоновДвижений(ЭталоныДвижений);
		КодСоздания.МакетыЭталонов = МакетыЭталонов;
	КонецЕсли;

КонецПроцедуры

Функция ЗначенияЗаполненныхРеквизитовОбъекта(ДанныеОбъекта)
	
	МетаданныеОбъекта = ДанныеОбъекта.Метаданные();
	ПолноеИмяМД = МетаданныеОбъекта.ПолноеИмя();
	
	ЗначенияРеквизитов = Новый Структура;
	ЗначенияРеквизитов.Вставить("ТипОбъекта", ПолноеИмяМД); // заменить на получение имя объекта из дерева МД
	
	ЭтоДокумент = СтрНайти(ПолноеИмяМД, "Документ") <> 0;
	ЗначенияРеквизитов.Вставить("ЭтоДокумент", ЭтоДокумент);
	
	Для Каждого Реквизит Из МетаданныеОбъекта.СтандартныеРеквизиты Цикл
		Если ЭтоРеквизитИсключен(Реквизит) Тогда
			Продолжить;
		КонецЕсли;
		
		ДобавитьЗначениеРеквизитаВСтруктуру(ДанныеОбъекта, ЗначенияРеквизитов, Реквизит.Имя);
	КонецЦикла;
	
	Для Каждого Реквизит Из МетаданныеОбъекта.Реквизиты Цикл
		ДобавитьЗначениеРеквизитаВСтруктуру(ДанныеОбъекта, ЗначенияРеквизитов, Реквизит.Имя);
	КонецЦикла;
	
	ТабличныеЧасти = Новый Массив;
	
	Для Каждого ТЧ Из МетаданныеОбъекта.ТабличныеЧасти Цикл
		СтрокиТабличнойЧасти = Неопределено;
		
		Выполнить("СтрокиТабличнойЧасти = ДанныеОбъекта." + ТЧ.Имя);
		
		РеквизитыТЧ = ТЧ.Реквизиты;
		ДанныеСтрокТЧ = Новый Массив;
		
		Для Каждого СтрокаТЧ Из СтрокиТабличнойЧасти Цикл
			ДанныеСтрокиТЧ = Новый Структура;
			
			Для Каждого РеквизитТЧ Из РеквизитыТЧ Цикл
				ДобавитьЗначениеРеквизитаВСтруктуру(СтрокаТЧ, ДанныеСтрокиТЧ, РеквизитТЧ.Имя, СтрокаТЧ.НомерСтроки);
			КонецЦикла;
			
			ДанныеСтрокТЧ.Добавить(ДанныеСтрокиТЧ);
		КонецЦикла;
		
		Если ДанныеСтрокТЧ.Количество() Тогда
			ТабличныеЧасти.Добавить(ПодготовленныеДанныеТЧ(ТЧ.Имя, ДанныеСтрокТЧ, СтрокиТабличнойЧасти.Выгрузить()));
		КонецЕсли;
	КонецЦикла;
	 
	ЗначенияРеквизитов.Вставить("ТабличныеЧасти", ТабличныеЧасти);
	
	Возврат ЗначенияРеквизитов;
	
КонецФункции

Процедура ДобавитьЗначениеРеквизитаВСтруктуру(ДанныеОбъекта, ЗначенияРеквизитов, ИмяРеквизита, НомерСтроки = "")
	
	ЗначениеРеквизита = ДанныеОбъекта[ИмяРеквизита];
	
	Если ЗначениеРеквизитаЗаполнено(ЗначениеРеквизита) Тогда
	
		ПодготовленныеДанныеРеквизита = ПодготовленныеДанныеРеквизита(ИмяРеквизита, ЗначениеРеквизита, НомерСтроки);
		
		КодИнициализацииРеквизитаПоУмолчанию = КодИнициализацииРеквизитаПоУмолчанию(ИмяРеквизита);
		
		Если ЗначениеЗаполнено(КодИнициализацииРеквизитаПоУмолчанию) Тогда
			ПодготовленныеДанныеРеквизита.КодИнициализации = КодИнициализацииРеквизитаПоУмолчанию;
		ИначеЕсли ПодготовленныеДанныеРеквизита.ЭтоПримитивныйТип Тогда
			ПодготовленныеДанныеРеквизита.КодИнициализации = КодДляЗначенияПримитивногоТипа(ЗначениеРеквизита);
		Иначе
			ПодготовленныеДанныеРеквизита.КодИнициализации = КодИнициализацииПоЗначению(ЗначениеРеквизита);
		КонецЕсли;
		
		ЗначенияРеквизитов.Вставить(ИмяРеквизита, ПодготовленныеДанныеРеквизита);
	КонецЕсли;
	
КонецПроцедуры

Функция ПодговитьДвиженияДокумента(ДанныеОбъекта, ОтобранныеДвижения)
	
	МетаданныеОбъекта = ДанныеОбъекта.Метаданные();
	
	КоллекцияДвижения = Новый Структура;
	
	Для Каждого МДДвижения Из МетаданныеОбъекта.Движения Цикл
		Если ОтобранныеДвижения.Найти(МДДвижения.ПолноеИмя()) <> Неопределено Тогда
			КоллекцияДвижения.Вставить(МДДвижения.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Движения Из КоллекцияДвижения Цикл
		ДвиженияПоРегистру = ДанныеОбъекта.Движения[Движения.Ключ];
		ДвиженияПоРегистру.Прочитать();
		
		Если ДвиженияПоРегистру.Количество() Тогда
			КоллекцияДвижения[Движения.Ключ] = ТаблицаДвиженийРегистра(ДвиженияПоРегистру);
		Иначе
			КоллекцияДвижения.Удалить(Движения.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Возврат КоллекцияДвижения;
	
КонецФункции

Функция ПодготовитьДанныеДвиженийДокумента(КоллекцияДвижений)
	
	ДанныеДвиженийОбъекта = Новый Массив;
	
	Для Каждого Движения Из КоллекцияДвижений Цикл
		ДанныеДвижения = Новый Массив;
		
		Для Каждого Стр Из Движения.Значение Цикл
			ДанныеСтрокиТЧ = Новый Структура;
			
			Для Каждого РеквизитТЧ Из Движения.Значение.Колонки Цикл
				ДобавитьЗначениеРеквизитаВСтруктуру(Стр, ДанныеСтрокиТЧ, РеквизитТЧ.Имя, Стр.НомерСтроки);
			КонецЦикла;
			
			ДанныеДвижения.Добавить(ДанныеСтрокиТЧ);
		КонецЦикла;
		
		Если ДанныеДвижения.Количество() Тогда
			ДанныеДвиженийОбъекта.Добавить(ПодготовленныеДанныеТЧ(Движения.Ключ,
											ДанныеДвижения,
											Движения.Значение));
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДанныеДвиженийОбъекта;
		
КонецФункции

Функция ТаблицаДвиженийРегистра(ДвиженияПоРегистру)
	МетаданныеРегистра = ДвиженияПоРегистру.Метаданные();
	
	Если ЮТМетаданные.ЭтоРегистрБухгалтерии(МетаданныеРегистра) Тогда
		ТаблицаДвижений = ДвиженияПоРегистру.Выгрузить();
		ТаблицаДвижений = ТаблицаДвижений.СкопироватьКолонки();
		
		Для Каждого Движение Из ДвиженияПоРегистру Цикл
			НоваяСтрока = ТаблицаДвижений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Движение);
			
			Для Каждого СубконтоДт Из Движение.СчетДт.ВидыСубконто Цикл
				НоваяСтрока["СубконтоДт" + СубконтоДт.НомерСтроки] = Движение.СубконтоДт[СубконтоДт.ВидСубконто];
			КонецЦикла;
			
			Для Каждого СубконтоКт Из Движение.СчетКт.ВидыСубконто Цикл
				НоваяСтрока["СубконтоКт" + СубконтоКт.НомерСтроки] = Движение.СубконтоКт[СубконтоКт.ВидСубконто];
			КонецЦикла;
		КонецЦикла;
		
	Иначе
		ТаблицаДвижений = ДвиженияПоРегистру.Выгрузить();
	КонецЕсли;
	
	Возврат ТаблицаДвижений;

КонецФункции

#Область ФормированиеКода

Процедура СформироватьКодРеквизитов(ЗначенияРеквизитов, Конструктор, Переменные)
	
	Для Каждого Реквизит Из ЗначенияРеквизитов Цикл
		ОбогащенныйРеквизит = Реквизит.Значение;
		
		ПропускаемыеСвойства = СтрРазделить("ТипОбъекта,ЭтоДокумент,ДвиженияОбъекта", ",");
		
		Если ПропускаемыеСвойства.Найти(Реквизит.Ключ) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ОбогащенныйРеквизит) = Тип("Массив") Тогда
			Продолжить;
		КонецЕсли;
	
		Если ОбогащенныйРеквизит.ЭтоПримитивныйТип Тогда
			Конструктор
				.НоваяСтрокаПоШаблону(".Установить(""%1"", %2)",
										ОбогащенныйРеквизит.Имя,
										ОбогащенныйРеквизит.КодИнициализации)
				.Табуляция(3);
		Иначе
			Конструктор
				.НоваяСтрокаПоШаблону(".Установить(""%1"", %2)",
										ОбогащенныйРеквизит.Имя,
										ОбогащенныйРеквизит.ИмяПеременной)
				.Табуляция(3);
				
			ДобавитьНовуюПеременную(Переменные, ОбогащенныйРеквизит);
		КонецЕсли;
		
	КонецЦикла;
	
	Конструктор.НоваяСтрока(";").Табуляция(1);
	
КонецПроцедуры

Процедура СформироватьКодТабличныхЧастей(ТабличныеЧасти, Конструктор, Переменные)
	
	Для Каждого ТЧ Из ТабличныеЧасти Цикл
		
		Конструктор.НоваяСтрокаПоШаблону(ШаблонПереходНаКонтекстТЧ(), ТЧ.Имя);
		
		Для Каждого СтрокаТЧ Из ТЧ.Строки Цикл
			
			Конструктор.НоваяСтрока(".ДобавитьСтроку()").Табуляция(2);
			
			Для Каждого РеквизитТЧ Из СтрокаТЧ Цикл
				ОбогащенныйРеквизит = РеквизитТЧ.Значение;
				
				Если ОбогащенныйРеквизит.ЭтоПримитивныйТип Тогда
					Конструктор
						.НоваяСтрокаПоШаблону(".Установить(""%1"", %2)",
												ОбогащенныйРеквизит.Имя,
												ОбогащенныйРеквизит.КодИнициализации)
						.Табуляция(3);
				
				Иначе
					НайденноеЗначение = Переменные.Найти(ОбогащенныйРеквизит.Значение, "Значение");
					
					Если НайденноеЗначение = Неопределено Тогда
						ДобавитьНовуюПеременную(Переменные, ОбогащенныйРеквизит);
						
						ИмяПеременной = ОбогащенныйРеквизит.ИмяПеременной;
					Иначе
						ИмяПеременной = НайденноеЗначение.ИмяПеременной;
					КонецЕсли;
					
					Конструктор
						.НоваяСтрокаПоШаблону(".Установить(""%1"", %2)",
											ОбогащенныйРеквизит.Имя,
											ИмяПеременной)
						.Табуляция(3);
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
		Конструктор
			.НоваяСтрока(";")
				.Табуляция(1)
			.ПереносСтроки();
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьКодТабличныхЧастейСМакетами(ТабличныеЧасти, Конструктор, Переменные)

	КэшТипов = Новый Соответствие;
	ПодготовленныеТЧ = Новый Массив;
	
	Для Каждого ТЧ Из ТабличныеЧасти Цикл
		ИмяМакета = СтрШаблон("Макет%1", ТЧ.Имя);
		ПодготовленныеДанныеТЧ = ПодготовитьЭталонТаблицыЗначений(ТЧ.КопияДанныхТЧ, Переменные);
		
		ПодготовленныеТЧ.Добавить(Новый Структура("Имя, ИмяМакета, ЭталонТЧ", ТЧ.Имя, ИмяМакета, ПодготовленныеДанныеТЧ));
	КонецЦикла;
	
	Для Каждого ПодготовленныеДанныеТЧ Из ПодготовленныеТЧ Цикл
		
		МакетТабличнойЧасти = КодМаркДаунТаблицаВПеременную(ПодготовленныеДанныеТЧ.ЭталонТЧ.ТаблицаДляМакета, ПодготовленныеДанныеТЧ.ИмяМакета);
		
		Конструктор
			.НоваяСтрокаПоШаблону("// Табличная часть ""%1""", ПодготовленныеДанныеТЧ.Имя)
		;
			
		ТипыКолонок = ПодготовленныеДанныеТЧ.ЭталонТЧ.ТипыКолонок;

		СформироватьКодЗаполненияСоответствияТипов(Конструктор, ТипыКолонок, КэшТипов, ПодготовленныеДанныеТЧ.Имя);

		Конструктор
			.ДобавитьТекст(МакетТабличнойЧасти)
				.ПереносСтроки()
			.НоваяСтрока("ПараметрыСозданияОбъектов = Новый Структура(""ФикцияОбязательныхПолей,ОбменДаннымиЗагрузка"", Истина, Истина);")
			.НоваяСтрокаПоШаблону("ДанныеТабличнойЧасти%1 = Ютест.Данные().ЗагрузитьИзМакета(%2, СоответствиеТипов%1,, ЗаменяемыеЗначения, ПараметрыСозданияОбъектов);",
								ПодготовленныеДанныеТЧ.Имя, ПодготовленныеДанныеТЧ.ИмяМакета)
				.ПереносСтроки()
			.НоваяСтрокаПоШаблону("ТабличнаяЧасть%1 = %2.ТабличнаяЧасть(""%1"");", ПодготовленныеДанныеТЧ.Имя, ОсновнаяПеременная())
			.НоваяСтрокаПоШаблону("Для Каждого СтрТЧ Из ДанныеТабличнойЧасти%1 Цикл", ПодготовленныеДанныеТЧ.Имя)
				.НоваяСтрока("ДанныеСтроки = ЮТКоллекции.СтрокаТаблицыЗначенийВСтруктуру(СтрТЧ);").Табуляция(1)
				.НоваяСтрокаПоШаблону("ТабличнаяЧасть%1.ДобавитьСтроку(ДанныеСтроки)", ПодготовленныеДанныеТЧ.Имя).Табуляция(1)
			.НоваяСтрока("КонецЦикла;")
				.ПереносСтроки();
		
	КонецЦикла;
			
КонецПроцедуры

Процедура СформироватьКодКонструктораДвижений(Конструктор, НаборДвижений, Переменные, СсылкаНаОбъект)
	
	Сдвиг = 1;
	
	Конструктор
		.НоваяСтрокаПоШаблону(ШаблонВызоваКонструктораДвижений(), СсылкаНаОбъект, "Регистратор", НаборДвижений.Имя, "");
	
	Для Каждого СтрокаНабора Из НаборДвижений.Строки Цикл
		
		Конструктор.НоваяСтрока(".ДобавитьСтроку()").Табуляция(Сдвиг);
		
		Для Каждого Колонка Из СтрокаНабора Цикл
			ОбогащенныйРеквизит = Колонка.Значение;
			
			Если ОбогащенныйРеквизит.ЭтоПримитивныйТип Тогда
				Конструктор
					.НоваяСтрокаПоШаблону(".Установить(""%1"", %2)",
						ОбогащенныйРеквизит.Имя,
						ОбогащенныйРеквизит.КодИнициализации
					)
					.Табуляция(Сдвиг + 1);
				
			Иначе
				НайденноеЗначение = Переменные.Найти(ОбогащенныйРеквизит.Значение, "Значение");
				
				Если НайденноеЗначение = Неопределено Тогда
					ДобавитьНовуюПеременную(Переменные, ОбогащенныйРеквизит);
					
					ИмяПеременной = ОбогащенныйРеквизит.ИмяПеременной;
				Иначе
					ИмяПеременной = НайденноеЗначение.ИмяПеременной;
				КонецЕсли;
				
				Конструктор
					.НоваяСтрокаПоШаблону(".Установить(""%1"", %2)",
						ОбогащенныйРеквизит.Имя,
						ИмяПеременной
					)
						.Табуляция(Сдвиг + 1);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Конструктор
		.НоваяСтрока(".Записать();")
		.ПереносСтроки();

КонецПроцедуры

Процедура СформироватьКодКонструктораДвиженийСИнлайнМакетами(Конструктор, НаборДвижений, Переменные, СсылкаНаОбъект)

	Сдвиг = 1;
		
	ИмяМакета = СтрШаблон("Макет%1", НаборДвижений.Имя);
	ПодготовленныеДанные = ПодготовитьЭталонТаблицыЗначений(НаборДвижений.КопияДанныхТЧ, Переменные);
	МакетТабличнойЧасти = КодМаркДаунТаблицаВПеременную(ПодготовленныеДанные.ТаблицаДляМакета, ИмяМакета);
		
	СформироватьКодУстановкиЗаменяемыхЗначений(Конструктор, Переменные);
	
	Конструктор
		.ПереносСтроки()
		.НоваяСтрокаПоШаблону("// Движения ""%1""", НаборДвижений.Имя)
	;
	
	ТипыКолонок = ПодготовленныеДанные.ТипыКолонок;
		
	КэшТипов = Новый Соответствие;
	СформироватьКодЗаполненияСоответствияТипов(Конструктор, ТипыКолонок, КэшТипов, НаборДвижений.Имя);
	
	Конструктор
		.ДобавитьТекст(МакетТабличнойЧасти, Сдвиг - 1)
			.ПереносСтроки()
		.НоваяСтрокаПоШаблону(ШаблонВызоваКонструктораДвижений(), СсылкаНаОбъект, "Регистратор", НаборДвижений.Имя, ";")
			.ПереносСтроки()
		.НоваяСтрока("ПараметрыСозданияОбъектов = Новый Структура(""ФикцияОбязательныхПолей,ОбменДаннымиЗагрузка"", Истина, Истина);")
		.НоваяСтрокаПоШаблону("ДанныеДвижений%1 = Ютест.Данные().ЗагрузитьИзМакета(%2, СоответствиеТипов%1,, ЗаменяемыеЗначения, ПараметрыСозданияОбъектов);",	НаборДвижений.Имя, ИмяМакета)
			.ПереносСтроки()
		.НоваяСтрокаПоШаблону("Для Каждого СтрТЧ Из ДанныеДвижений%1 Цикл", НаборДвижений.Имя)
			.НоваяСтрока("ДанныеСтроки = ЮТКоллекции.СтрокаТаблицыЗначенийВСтруктуру(СтрТЧ);").Табуляция(Сдвиг)
			.НоваяСтрокаПоШаблону("Движения%1.ДобавитьСтроку(ДанныеСтроки);", НаборДвижений.Имя).Табуляция(Сдвиг)
		.НоваяСтрока("КонецЦикла;")
			.ПереносСтроки();
	
	Конструктор
		.НоваяСтрокаПоШаблону("Движения%1.Записать();", НаборДвижений.Имя)
			.ПереносСтроки();

КонецПроцедуры

Процедура СформироватьКодПеременных(Конструктор, Переменные, ШагСдвига = 0)
	
	Переменные.Свернуть("Порядок, ИмяПеременной,Имя,Тип,Значение,КодИнициализации,НомерСтроки");
	Переменные.Сортировать("Порядок УБЫВ, НомерСтроки");
	
	Для Каждого Переменная Из Переменные Цикл
		КодУстановкиЗначения = СтрШаблон("%1 = %2", Переменная.ИмяПеременной, Переменная.КодИнициализации);
		Конструктор.ВставитьПервуюСтроку(КодУстановкиЗначения).Табуляция(ШагСдвига);
	КонецЦикла;
		
	Конструктор.ВставитьПервуюСтроку("// Переменные");
	
КонецПроцедуры

Процедура СформироватьКодУстановкиЗаменяемыхЗначений(Конструктор, Переменные)
	
	Конструктор
		.НоваяСтрока("// Заменяемые значения")
		.НоваяСтрока("ЗаменяемыеЗначения = Новый Соответствие;")
	;
	
	Для Каждого Переменная Из Переменные Цикл
		Конструктор
			.НоваяСтрокаПоШаблону("ЗаменяемыеЗначения.Вставить(""%1"", %2);", Переменная.ИмяПеременной, Переменная.ИмяПеременной);
	КонецЦикла;
	
КонецПроцедуры

Функция ИмяПеременной(Реквизит)
	
	Если Реквизит.НомерСтроки = "" Тогда
		ИмяПеременной = СтрШаблон("%1%2", Реквизит.Имя, Реквизит.НомерСтроки);
	Иначе
		ИмяПеременной = СтрШаблон("%1Стр%2", Реквизит.Имя, Реквизит.НомерСтроки);
	КонецЕсли;

	Возврат ИмяПеременной;
	
КонецФункции

Функция ЭтоРеквизитИсключен(Знач Реквизит)
	
	Возврат СтрНайти("Проведен,Ссылка,ПометкаУдаления,Предопределенный,ЭтоГруппа,ИмяПредопределенныхДанных", Реквизит.Имя) <> 0;
	
КонецФункции

#КонецОбласти

#Область ЭталоныДвижений

Функция ЭталоныДвиженийДокумента(ДвиженияОбъекта, Переменные)
	
	ЭталоныДвижений = Новый Структура;
	
	Для Каждого Движения Из ДвиженияОбъекта Цикл
		ЭталонДвижений = ПодготовитьЭталонТаблицыЗначений(Движения.Значение, Переменные);
		ЭталоныДвижений.Вставить(Движения.Ключ, ЭталонДвижений);
	КонецЦикла;
	
	Возврат ЭталоныДвижений;

КонецФункции

Функция ПодготовитьЭталонТаблицыЗначений(ТаблицаДляЗагрузки, Переменные)
	
	ДанныеЭталона = Новый Структура;
	ДанныеЭталона.Вставить("ТипыКолонок");
	ДанныеЭталона.Вставить("ТаблицаДляМакета");
	
	ТипыКолонок = Новый Структура;
	ТаблицаДляМакета = Новый ТаблицаЗначений;
	
	Для Каждого Колонка Из ТаблицаДляЗагрузки.Колонки Цикл
		Если Колонка.Имя = "Регистратор"
			ИЛИ Колонка.Имя = "МоментВремени"
			ИЛИ СтрНачинаетсяС(Колонка.Имя, "ВидСубконто") Тогда
			Продолжить;
		КонецЕсли;
		
		ТаблицаДляМакета.Колонки.Добавить(Колонка.Имя);
	КонецЦикла;
	
	Для Каждого Стр Из ТаблицаДляЗагрузки Цикл
		НоваяСтрока = ТаблицаДляМакета.Добавить();
		
		Для Каждого Колонка Из ТаблицаДляЗагрузки.Колонки Цикл
			
			Если Колонка.Имя = "Регистратор"
				ИЛИ Колонка.Имя = "МоментВремени"
				ИЛИ СтрНачинаетсяС(Колонка.Имя, "ВидСубконто") Тогда
				Продолжить;
			КонецЕсли;
			
			Значение = Стр[Колонка.Имя];

			Если Не ЗначениеРеквизитаЗаполнено(Значение) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭтоПримитивныйТип(Значение) Тогда
				НоваяСтрока[Колонка.Имя] = Значение;
			Иначе
				НайденноеЗначение = Переменные.Найти(Значение, "Значение");
				
				Если НайденноеЗначение = Неопределено Тогда
					ОбогащенноеЗначение = ПодготовленныеДанныеРеквизита(Колонка.Имя, Значение, Стр.НомерСтроки);
					ДобавитьНовуюПеременную(Переменные, ОбогащенноеЗначение);
					
					НоваяСтрока[Колонка.Имя] = ОбогащенноеЗначение.ИмяПеременной;
				Иначе
					НоваяСтрока[Колонка.Имя] = НайденноеЗначение.ИмяПеременной;
				КонецЕсли;
			КонецЕсли;
			
			Если ТипыКолонок.Свойство(Колонка.Имя) Тогда
				МассивТипов = ТипыКолонок[Колонка.Имя].Типы();
				МассивТипов.Добавить(ТипЗнч(Значение));
			Иначе
				МассивТипов = Новый Массив;
				МассивТипов.Добавить(ТипЗнч(Значение));
			КонецЕсли;
			
			ТипыКолонок.Вставить(Колонка.Имя, Новый ОписаниеТипов(МассивТипов));
		КонецЦикла;
		
	КонецЦикла;

	ДанныеЭталона.ТипыКолонок = ТипыКолонок;
	ДанныеЭталона.ТаблицаДляМакета = ТаблицаДляМакета;

	Возврат ДанныеЭталона;
	
КонецФункции

Процедура СформироватьКодПодготовкиЭталонов(ЭталоныДвижений, Переменные, Конструктор, ИнлайнМакет)
				
	КэшТипов = Новый Соответствие;
	
	Для Каждого Эталон Из ЭталоныДвижений Цикл
		
		ДанныеЭталона = Эталон.Значение;
		
		ПараметрыФункции = ?(ИнлайнМакет, "ЗаменяемыеЗначения", "ИмяМакета, ЗаменяемыеЗначения");
		
		Конструктор
			.НоваяСтрокаПоШаблону("Функция ДанныеДвижений%1(%2) Экспорт",
				Эталон.Ключ, ПараметрыФункции)
				.ПереносСтроки();
				
		ТипыКолонок = ДанныеЭталона.ТипыКолонок;
		
		СформироватьКодЗаполненияСоответствияТипов(Конструктор, ТипыКолонок, КэшТипов,, 1);
		
		Если ИнлайнМакет Тогда
			КодМакета = КодМаркДаунТаблицаВПеременную(Эталон.Значение.ТаблицаДляМакета);
			
			Конструктор
				.ДобавитьТекст(КодМакета, 1)
				.ПереносСтроки();
		Иначе
			Конструктор.НоваяСтрока("Макет = ПолучитьМакет(ИмяМакета);");
		КонецЕсли;
		
		Конструктор
			.НоваяСтрока("ПараметрыСозданияОбъектов = Новый Структура(""ФикцияОбязательныхПолей,ОбменДаннымиЗагрузка"", Истина, Истина);")
				.Табуляция(1)
			.НоваяСтрока("ДанныеДляЗаписи = Ютест.Данные().ЗагрузитьИзМакета(Макет, СоответствиеТипов,, ЗаменяемыеЗначения, ПараметрыСозданияОбъектов);")
				.Табуляция(1)
				.ПереносСтроки()
			.НоваяСтрока("Возврат ДанныеДляЗаписи;")
				.Табуляция(1)
				.ПереносСтроки()
			.НоваяСтрока("КонецФункции")
				.ПереносСтроки();
	КонецЦикла;
	
КонецПроцедуры

Функция МакетыЭталоновДвижений(ЭталоныДвижений)
		
	МакетыЭталонов = Новый Структура;
	
	Для Каждого Эталон Из ЭталоныДвижений Цикл
		МакетыЭталонов.Вставить(Эталон.Ключ, ТабличныйДокументИзТаблицы(Эталон.Значение.ТаблицаДляМакета));
	КонецЦикла;
	
	Возврат МакетыЭталонов;
	
КонецФункции

Процедура СформироватьКодЗаполненияСоответствияТипов(Конструктор, ТипыКолонок, КэшТипов, ПостфиксПеременной = "", Сдвиг = 0)
	
	Конструктор
			.НоваяСтрока("ТипыПолей = Новый Соответствие;").Табуляция(Сдвиг);
			
	Для Каждого КолонкаТипы Из ТипыКолонок Цикл
		
		ВозможныеТипыОбъекта = КолонкаТипы.Значение.Типы();
		
		ТипыОбъекта = Новый Массив;
		
		Для Каждого ТипОбъекта Из ВозможныеТипыОбъекта Цикл
			ПредставлениеТипа = КэшТипов[ТипОбъекта];
			
			Если ПредставлениеТипа = Неопределено Тогда
				ПредставлениеТипа = СтроковоеПредставлениеТипа(ТипОбъекта);
				КэшТипов[ТипОбъекта] = ПредставлениеТипа;
			КонецЕсли;
			
			ТипыОбъекта.Добавить(ПредставлениеТипа);
		КонецЦикла;
			
		Конструктор
			.НоваяСтрокаПоШаблону("ТипыПолей.Вставить(""%1"", ЮТКоллекции.ЗначениеВМассиве(%2));",
				КолонкаТипы.Ключ, СтрСоединить(ТипыОбъекта, ", "))
			.Табуляция(Сдвиг);
		
	КонецЦикла;
	
	Конструктор
	.ПереносСтроки()
		.НоваяСтрокаПоШаблону("СоответствиеТипов%1 = Новый Соответствие;", ПостфиксПеременной).Табуляция(Сдвиг)
		.НоваяСтрока("Для каждого ТипПоля Из ТипыПолей Цикл").Табуляция(Сдвиг)
			.НоваяСтрока("ИмяПоля = ТипПоля.Ключ;").Табуляция(Сдвиг + 1)
			.НоваяСтрока("ОписаниеТипов = Новый ОписаниеТипов(ТипПоля.Значение);").Табуляция(Сдвиг + 1)
			.НоваяСтрокаПоШаблону("СоответствиеТипов%1.Вставить(ИмяПоля, ОписаниеТипов);", ПостфиксПеременной).Табуляция(Сдвиг + 1)
		.НоваяСтрока("КонецЦикла;").Табуляция(Сдвиг)
	.ПереносСтроки();

КонецПроцедуры

#КонецОбласти

#Область Конструкторы

Функция Переменные()
	
	Переменные = Новый ТаблицаЗначений;
	Переменные.Колонки.Добавить("ИмяПеременной");
	Переменные.Колонки.Добавить("Имя");
	Переменные.Колонки.Добавить("Тип");
	Переменные.Колонки.Добавить("Значение");
	Переменные.Колонки.Добавить("КодИнициализации");
	Переменные.Колонки.Добавить("НомерСтроки");
	Переменные.Колонки.Добавить("Порядок");
	
	Возврат Переменные;
	
КонецФункции

Процедура ДобавитьНовуюПеременную(Переменные, ОбогащенныйРеквизит)
	
	НоваяПеременная = Переменные.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяПеременная, ОбогащенныйРеквизит);
		
	ПорядокСортировкиПеременных = ЮТТестовыеДанныеПереопределяемый.ПорядокСортировкиПеременных();
	
	Порядок = ПорядокСортировкиПеременных[НоваяПеременная.Тип];
	НоваяПеременная.Порядок = ?(Порядок = Неопределено, 99, Порядок);
	
	Если ЗначениеЗаполнено(НоваяПеременная.КодИнициализации) Тогда
		Возврат;
	КонецЕсли;
	
	КодИнициализацииПоЗначению = КодИнициализацииПоЗначению(ОбогащенныйРеквизит.Значение);
	НоваяПеременная.КодИнициализации = КодИнициализацииПоЗначению;

КонецПроцедуры

Функция ПодготовленныеДанныеРеквизита(ИмяРеквизита, Значение, НомерСтроки = "", КодИнициализации = "")
	
	ДанныеРеквизита = Новый Структура;
	ДанныеРеквизита.Вставить("Имя", ИмяРеквизита);
	ДанныеРеквизита.Вставить("Значение", Значение);
	ДанныеРеквизита.Вставить("Тип",ТипЗнч(Значение));
	ДанныеРеквизита.Вставить("ЭтоПримитивныйТип", ЭтоПримитивныйТип(Значение));
	ДанныеРеквизита.Вставить("НомерСтроки", НомерСтроки);
	ДанныеРеквизита.Вставить("КодИнициализации", КодИнициализации);
	ДанныеРеквизита.Вставить("ИмяПеременной", ИмяПеременной(ДанныеРеквизита));
	
	Возврат ДанныеРеквизита;
	
КонецФункции

Функция ПодготовленныеДанныеТЧ(Имя, Строки, КопияДанныхТЧ)
	
	ДанныеРеквизита = Новый Структура;
	ДанныеРеквизита.Вставить("Имя", Имя);
	ДанныеРеквизита.Вставить("Строки", Строки);
	ДанныеРеквизита.Вставить("КопияДанныхТЧ", КопияДанныхТЧ);
	
	Возврат ДанныеРеквизита;
	
КонецФункции

#КонецОбласти

#Область ШаблоныКода

Функция ОсновнаяПеременная()

	Возврат "ТестовыйОбъект";
	
КонецФункции

Функция ШаблонВызоваКонструктораОбъекта()

	Шаблон = СтрШаблон("// Конструктор объекта '%2'
							|%1 = Ютест.Данные()
							|		.КонструкторОбъекта(""%2"")", ОсновнаяПеременная(), "%1");

	Возврат Шаблон;

КонецФункции

Функция ШаблонВызоваКонструктораДвижений()

	Шаблон = "// Конструктор Движений документа %1 по регистру '%3'
			|Движения%3 = Ютест.Данные()
			|	.КонструкторДвижений(%2, ""%3"")%4";

	Возврат Шаблон;

КонецФункции

Функция ШаблонПереходНаКонтекстТЧ()

	Шаблон = СтрШаблон("// Табличная часть '%2'
							|ТЧ = %1
							|	.ТабличнаяЧасть(""%2"")",
						ОсновнаяПеременная(),
						"%1"
		);
	
	Возврат Шаблон;

КонецФункции

Функция КодИнициализацииПоТипам()
	
	КодИнициализации = Новый Соответствие;
	
	ЮТТестовыеДанныеПереопределяемый.КодИнициализацииПоТипам(КодИнициализации);
	
	Возврат КодИнициализации;
		
КонецФункции

Функция КодИнициализацииПоЗначению(Значение)

	Если ЭтоПримитивныйТип(Значение) Тогда
		Возврат КодДляЗначенияПримитивногоТипа(Значение);
	КонецЕсли;
	
	ТипЗначения = ТипЗнч(Значение);
	
	Если Метаданные.НайтиПоТипу(ТипЗначения) = Неопределено Тогда
		Возврат СтрШаблон("%1.%2;", Строка(ТипЗначения), Строка(Значение));
	КонецЕсли;
	
	МетаданныеЗначения = Значение.Метаданные();
	ШаблонКодаИнициализации = "ПредопределенноеЗначение(""%1.%2"");";
	
	Если ЮТМетаданные.ЭтоПеречисление(МетаданныеЗначения) Тогда
		Возврат СтрШаблон(ШаблонКодаИнициализации, МетаданныеЗначения.ПолноеИмя(), XMLСтрока(Значение));
		
	ИначеЕсли ЮТМетаданные.ЭтоСправочник(МетаданныеЗначения)
			Или ЮТМетаданные.ЭтоПланСчетов(МетаданныеЗначения)
			Или ЮТМетаданные.ЭтоПланВидовХарактеристик(МетаданныеЗначения) Тогда
			
		Если Значение.Предопределенный Тогда
			Возврат СтрШаблон(ШаблонКодаИнициализации, МетаданныеЗначения.ПолноеИмя(), Значение.ИмяПредопределенныхДанных);
		Иначе
			ПостоянныеДанные = ЮТТестовыеДанныеПереопределяемый.ПостоянныеДанные();
			
			ПолеПоиска = ПостоянныеДанные[ТипЗначения];
			
			Если Не ПолеПоиска = Неопределено Тогда
				ОписаниеОбъектаМетаданных = ЮТМетаданные.ОписаниеОбъектаМетаданных(Значение.Метаданные());
				
				Если ПолеПоиска = "Код" Тогда
					Возврат СтрШаблон("%1.%2.НайтиПоКоду(""%3""); // %4", ОписаниеОбъектаМетаданных.ОписаниеТипа.ИмяКоллекции, ОписаниеОбъектаМетаданных.Имя, Значение.Код, Значение.Наименование);
				ИначеЕсли ПолеПоиска = "Наименование" Тогда
					Возврат СтрШаблон("%1.%2.НайтиПоНаименованию(""%3"");", ОписаниеОбъектаМетаданных.ОписаниеТипа.ИмяКоллекции, ОписаниеОбъектаМетаданных.Имя, Значение.Наименование);
				КонецЕсли;
			КонецЕсли;
			
			КодИнициализацииПоТипам = КодИнициализацииПоТипам();
			КодИнициализацииПоТипу = КодИнициализацииПоТипам[ТипЗначения];
			
			Если ЗначениеЗаполнено(КодИнициализацииПоТипу) Тогда
				Возврат СтрШаблон(КодИнициализацииПоТипу, Строка(Значение));
			Иначе
				ПолноеИмяПоЗначению =  Значение.Метаданные().ПолноеИмя();
				Возврат СтрШаблон("Ютест.Данные().СоздатьЭлемент(""%1"", ""%2""); // %2", ПолноеИмяПоЗначению, Строка(Значение));
			КонецЕсли;
	
		КонецЕсли;
	ИначеЕсли ЮТМетаданные.ЭтоДокумент(МетаданныеЗначения) Тогда
		ПолноеИмяПоЗначению =  Значение.Метаданные().ПолноеИмя();
		Возврат СтрШаблон("Ютест.Данные().СоздатьДокумент(""%1""); // %2", ПолноеИмяПоЗначению, Строка(Значение));
	Иначе
		Возврат "Неопределено;";
	КонецЕсли;
		
КонецФункции

Функция ЭтоПримитивныйТип(Значение)
	
	Возврат ТипЗнч(Значение) = Тип("Булево")
		Или ТипЗнч(Значение) = Тип("Строка")
		Или ТипЗнч(Значение) = Тип("Дата")
		Или ТипЗнч(Значение) = Тип("МоментВремени")
		Или ТипЗнч(Значение) = Тип("Число")
		Или ТипЗнч(Значение) = Тип("Неопределено")
		Или ТипЗнч(Значение) = Тип("Null");

КонецФункции

Функция КодДляЗначенияПримитивногоТипа(Значение)
	
	ТипЗначения = ТипЗнч(Значение);
	
	Если ТипЗначения = Тип("Булево") Тогда
		
		Результат = Формат(Значение, "БЛ=Ложь; БИ=Истина");
		
	ИначеЕсли ТипЗначения = Тип("Строка") Тогда
		
		ЭкранированноеНаименование = ЭкранированиеСтроки(Значение);
		
		Результат = "НСтр(""ru = '" + ЭкранированноеНаименование + "'"")" ;
		
	ИначеЕсли ТипЗначения = Тип("Число") Тогда
		
		Результат = Формат(Значение, "ЧГ=;ЧРД='.'");
		
	ИначеЕсли ТипЗначения = Тип("Дата") Тогда
		
		Результат = "'" + Формат(Значение, "ДФ='гггг.ММ.дд ЧЧ:мм:сс';") + "'";
		
	Иначе
		
		Результат = СтрШаблон("XMLЗначение(Тип(""%1""), ""%2"")", ТипЗначения, XMLСтрока(Значение));
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции
 
Функция КодИнициализацииРеквизитаПоУмолчанию(ИмяРеквизита)
	
	КодИнициализацииРеквизитов = ЮТТестовыеДанныеПереопределяемый.ЗаменаЗначенийРеквизитовПоУмолчанию();
	КодИнициализацииРеквизита = КодИнициализацииРеквизитов[ИмяРеквизита];
	
	Если КодИнициализацииРеквизита = Неопределено Тогда
		КодИнициализацииРеквизита = "";
	КонецЕсли;
	
	Возврат КодИнициализацииРеквизита;

КонецФункции
 
Функция КодМаркДаунТаблицаВПеременную(ТаблицаДляПреобразования, ИмяПеременной = "Макет")

	ТаблицаМаркДаун =
		Обработки.ЮТРедакторМакетаСТестовымиДанными.КонвертироватьВТекстовыйДокумент(
														ТабличныйДокументИзТаблицы(ТаблицаДляПреобразования));
	
	ПодготовленнаяТаблицаМД = СтрЗаменить(ТаблицаМаркДаун, Символы.ПС, Символы.ПС + "|");
	Макет = СтрШаблон("%1 = ""%2"";",
						ИмяПеременной,
						СокрЛП(ПодготовленнаяТаблицаМД));
						
	Возврат Макет;

КонецФункции
#КонецОбласти

#Область ОбщегоНазначения

Функция ЗначениеРеквизитаЗаполнено(Знач ЗначениеРеквизита)
	
	Возврат ЗначениеЗаполнено(ЗначениеРеквизита) И ЗначениеРеквизита <> Ложь;
	
КонецФункции

Функция ТабличныйДокументИзТаблицы(Таблица)
	
	ТабДокумент	= Новый ТабличныйДокумент;
	Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1);
	
	// Выводим заголовок таблицы
	КолонкаИндекс = 0;
	Пока КолонкаИндекс < Таблица.Колонки.Количество() Цикл
		Колонка = Таблица.Колонки[КолонкаИндекс];
		Ячейка = ТабДокумент.Область(1, КолонкаИндекс + 1, 1, КолонкаИндекс + 1);
		Ячейка.Текст = Колонка.Имя;
		
		Ячейка.ГраницаСверху = Линия;
		Ячейка.ГраницаСлева = Линия;
		Ячейка.ГраницаСправа= Линия;
		Ячейка.ГраницаСнизу = Линия;

		КолонкаИндекс = КолонкаИндекс + 1;
	КонецЦикла;
	
	// Выводим строки таблицы
	СтрокаИндекс = 0;
	Пока СтрокаИндекс < Таблица.Количество() Цикл
		Таблица_Строка = Таблица[СтрокаИндекс];
		КолонкаИндекс = 0;
		Пока КолонкаИндекс < Таблица.Колонки.Количество() Цикл
			Колонка = Таблица.Колонки[КолонкаИндекс];
			Ячейка = ТабДокумент.Область(СтрокаИндекс + 2, КолонкаИндекс + 1, СтрокаИндекс + 2, КолонкаИндекс + 1);
			Ячейка.Текст = Формат(Таблица_Строка[Колонка.Имя], "ЧГ=0");

			Ячейка.ГраницаСверху = Линия;
			Ячейка.ГраницаСлева = Линия;
			Ячейка.ГраницаСправа= Линия;
			Ячейка.ГраницаСнизу = Линия;

			КолонкаИндекс = КолонкаИндекс + 1;
		КонецЦикла;
		СтрокаИндекс = СтрокаИндекс + 1;
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции

Функция ЭтоСсылка(ПроверяемыйТип)
	
	Возврат ПроверяемыйТип <> Тип("Неопределено") И ОписаниеТипаВсеСсылки().СодержитТип(ПроверяемыйТип);
	
КонецФункции

Функция ОписаниеТипаВсеСсылки()
	
	Возврат Новый ОписаниеТипов(Новый ОписаниеТипов(Новый ОписаниеТипов(Новый ОписаниеТипов(Новый ОписаниеТипов(
		Новый ОписаниеТипов(Новый ОписаниеТипов(Новый ОписаниеТипов(Новый ОписаниеТипов(
			Справочники.ТипВсеСсылки(),
			Документы.ТипВсеСсылки().Типы()),
			ПланыОбмена.ТипВсеСсылки().Типы()),
			Перечисления.ТипВсеСсылки().Типы()),
			ПланыВидовХарактеристик.ТипВсеСсылки().Типы()),
			ПланыСчетов.ТипВсеСсылки().Типы()),
			ПланыВидовРасчета.ТипВсеСсылки().Типы()),
			БизнесПроцессы.ТипВсеСсылки().Типы()),
			БизнесПроцессы.ТипВсеСсылкиТочекМаршрутаБизнесПроцессов().Типы()),
			Задачи.ТипВсеСсылки().Типы());
	
КонецФункции

Функция ЭкранированиеСтроки(Значение)
	
	ЭкранированноеНаименование = Строка(Значение);
	ЭкранированноеНаименование = СтрЗаменить(ЭкранированноеНаименование, Символы.ПС, Символы.ПС + "|" );
	ЭкранированноеНаименование = СтрЗаменить(ЭкранированноеНаименование, "'", "''" );
	ЭкранированноеНаименование = СтрЗаменить(ЭкранированноеНаименование, """", """"""); // Замена одной кавычки на две
	
	Возврат ЭкранированноеНаименование;
	
КонецФункции
 
Функция СтроковоеПредставлениеТипа(Тип)
	
	Представление = "";
	
	Если ЭтоСсылка(Тип) Тогда
	
		ПолноеИмя = Метаданные.НайтиПоТипу(Тип).ПолноеИмя();
		ИмяОбъекта = СтрРазделить(ПолноеИмя, ".")[1];
		
		Если Справочники.ТипВсеСсылки().СодержитТип(Тип) Тогда
			Представление = "СправочникСсылка";
		
		ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(Тип) Тогда
			Представление = "ДокументСсылка";
		
		ИначеЕсли БизнесПроцессы.ТипВсеСсылки().СодержитТип(Тип) Тогда
			Представление = "БизнесПроцессСсылка";
		
		ИначеЕсли ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(Тип) Тогда
			Представление = "ПланВидовХарактеристикСсылка";
		
		ИначеЕсли ПланыСчетов.ТипВсеСсылки().СодержитТип(Тип) Тогда
			Представление = "ПланСчетовСсылка";
		
		ИначеЕсли ПланыВидовРасчета.ТипВсеСсылки().СодержитТип(Тип) Тогда
			Представление = "ПланВидовРасчетаСсылка";
		
		ИначеЕсли Задачи.ТипВсеСсылки().СодержитТип(Тип) Тогда
			Представление = "ЗадачаСсылка";
		
		ИначеЕсли ПланыОбмена.ТипВсеСсылки().СодержитТип(Тип) Тогда
			Представление = "ПланОбменаСсылка";
		
		ИначеЕсли Перечисления.ТипВсеСсылки().СодержитТип(Тип) Тогда
			Представление = "ПеречислениеСсылка";
		
		КонецЕсли;
		
		Представление = ?(Представление = "", Представление, Представление + "." + ИмяОбъекта);
		
	Иначе
		
		Представление = Строка(Тип);
		
	КонецЕсли;
	
	Результат = СтрШаблон("Тип(""%1"")", Представление);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти
