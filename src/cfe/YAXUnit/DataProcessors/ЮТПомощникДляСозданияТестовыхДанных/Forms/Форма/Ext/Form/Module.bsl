//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2025 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура СсылкаНаОбъектПриИзменении(Элемент)
	СсылкаНаОбъектПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СсылкаНаОбъектПриИзмененииНаСервере()

	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеОбъекта  = СсылкаНаОбъект.ПолучитьОбъект();
	
	МетаданныеОбъекта = ДанныеОбъекта.Метаданные();
	ПолноеИмяМД =  МетаданныеОбъекта.ПолноеИмя();
	
	ФормируемКодДокумента = СтрНайти(ПолноеИмяМД, "Документ") <> 0;
	
	Если ФормируемКодДокумента Тогда
		
		ДвиженияДокумента = ДанныеОбъекта.Движения;
		ИменаРегистров.Очистить();
		
		Для Каждого МДДвижения Из МетаданныеОбъекта.Движения Цикл
			ДвиженияПоРегистру = ДвиженияДокумента[МДДвижения.Имя];
			ДвиженияПоРегистру.Прочитать();
			
			Если Не ДвиженияПоРегистру.Количество() Тогда
				Продолжить;
			КонецЕсли;
			
			Описание = ЮТМетаданные.ОписаниеОбъектаМетаданных(МДДвижения.ПолноеИмя());
			ИменаРегистров.Добавить(МДДвижения.ПолноеИмя(), Описание.Представление, , БиблиотекаКартинок[Описание.ОписаниеТипа.Имя]);
			
			ИменаРегистров.СортироватьПоПредставлению();
		КонецЦикла;
		
	Иначе
		ИменаРегистров.Очистить();
	КонецЕсли;
	
	УстановитьВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РежимВыгрузкиМакетаДвижений = "ТабличныйДокумент";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьКодДляКонструктораОбъекта(Команда)
	Если ПроверитьЗаполнение() Тогда
		СформироватьКодДляКонструктораОбъектаНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СформироватьКодДляКонструктораОбъектаНаСервере()
	
	УдалитьДобавленныеЭлементы();
	
	Отмеченные = Новый Массив;
	
	Для Каждого Элемент Из ИменаРегистров Цикл
		Если Элемент.Пометка Тогда
			Отмеченные.Добавить(Элемент.Значение);
		КонецЕсли;
	КонецЦикла;
	
	ИнлайнМакетыДвижений = (РежимВыгрузкиМакетаДвижений = "Inline");
	
	НастройкиФормирования = Новый Структура;
	НастройкиФормирования.Вставить("ИнлайнМакетыДвижений", ИнлайнМакетыДвижений);
	НастройкиФормирования.Вставить("ИспользоватьМакетыТабличныхЧастей", ИспользоватьМакетыТабличныхЧастей);
	
	ПодготовленныеДанные = Обработки.ЮТПомощникДляСозданияТестовыхДанных.ПодготовленныеДанныеДляСозданияТестовогоОбъекта(СсылкаНаОбъект,
																														 Отмеченные,
																														 НастройкиФормирования);
	
	КодТестовогоМодуля.УстановитьТекст(ПодготовленныеДанные.КодТестовогоМодуля);
	КодМодуляМенеджераОбработки.УстановитьТекст(ПодготовленныеДанные.КодМодуляМенеджераОбработки);
	
	Если Не ИнлайнМакетыДвижений Тогда
		ДобавитьСтраницыДвижений(ПодготовленныеДанные.МакетыЭталонов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьКодДляКонструктораДвижений(Команда)
	Если ПроверитьЗаполнение() Тогда
		СформироватьКодДляКонструктораДвиженийНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СформироватьКодДляКонструктораДвиженийНаСервере()

	УдалитьДобавленныеЭлементы();
	
	Отмеченные = Новый Массив;
	
	Для Каждого Элемент Из ИменаРегистров Цикл
		Если Элемент.Пометка Тогда
			Отмеченные.Добавить(Элемент.Значение);
		КонецЕсли;
	КонецЦикла;
	
	НастройкиФормирования = Новый Структура;
	НастройкиФормирования.Вставить("ИспользоватьМакетыРегистровДвижений", ИспользоватьМакетыРегистровДвижений);
	
	ПодготовленныеДанные = Обработки.ЮТПомощникДляСозданияТестовыхДанных.ПодготовленныеДанныеДляЗагрузкиДвижений(СсылкаНаОбъект,
																												 Отмеченные,
																												 НастройкиФормирования);
	
	КодТестовогоМодуля.УстановитьТекст(ПодготовленныеДанные.КодЗаполненияДвижений);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	СсылкаНаОбъектПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьКодТестовогоМодуля(Команда)
	
	ТекстКода	= КодТестовогоМодуля.ПолучитьТекст();
	
	Если Не ПустаяСтрока(ТекстКода) Тогда
		
		COM = Новый COMОбъект("htmlfile");
		COM.ParentWindow.ClipboardData.Setdata("Text", ТекстКода);
		COM	= Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьКодМодуляМенеджераОбработки(Команда)
	
	ТекстКода	= КодМодуляМенеджераОбработки.ПолучитьТекст();
	
	Если Не ПустаяСтрока(ТекстКода) Тогда
		
		COM = Новый COMОбъект("htmlfile");
		COM.ParentWindow.ClipboardData.Setdata("Text", ТекстКода);
		COM	= Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКодМодуляМенеджераОбработки(Команда)
	
	КодМодуляМенеджераОбработки.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКодТестовогоМодуля(Команда)
	
	КодТестовогоМодуля.Очистить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьСтраницыДвижений(КоллекцияДвижений)

	Для Каждого Элемент Из КоллекцияДвижений Цикл
		Если РежимВыгрузкиМакетаДвижений = "Markdown" Тогда
			ИмяЭлемента = Элемент.Ключ;
			РеквизитФормы = НовыйРеквизитФормы(ИмяЭлемента, "ТекстовыйДокумент");

			ИмяСтраницы = "Страница" + ИмяЭлемента;
			НоваяСтраница = НоваяСтраница(ИмяСтраницы, ИмяЭлемента, "ДвиженияЭталоны");

			ДобавитьПолеТекстовогоЭлемента("ТекстовоеПоле" + ИмяЭлемента, ИмяЭлемента, НоваяСтраница);

			Текст = Обработки.ЮТРедакторМакетаСТестовымиДанными.КонвертироватьВТекстовыйДокумент(Элемент.Значение);
			ЭтаФорма[ИмяЭлемента].УстановитьТекст(Текст);
			
		ИначеЕсли РежимВыгрузкиМакетаДвижений = "ТабличныйДокумент" Тогда
			ИмяЭлемента = Элемент.Ключ;
			РеквизитФормы = НовыйРеквизитФормы(ИмяЭлемента, "ТабличныйДокумент");

			ИмяСтраницы = "Страница" + ИмяЭлемента;
			НоваяСтраница = НоваяСтраница(ИмяСтраницы, ИмяЭлемента, "ДвиженияЭталоны");

			ДобавитьТабличноеПоле("ТабличноеПоле" + ИмяЭлемента, ИмяЭлемента, НоваяСтраница);

			ЭтаФорма[ИмяЭлемента].Вывести(Элемент.Значение);
		Иначе
			ВызватьИсключение "Режим выгрузки макета не поддерживается!";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НовыйРеквизитФормы(ИмяРеквизита, Тип)
	
	Если РеквизитАналогичен(ИмяРеквизита, Тип) Тогда
		ЭтаФорма[ИмяРеквизита] = Новый (Тип);
		Возврат ЭтаФорма[ИмяРеквизита];
	КонецЕсли;
		
	ТипРеквизита = Новый ОписаниеТипов(Тип);
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	РеквизитФормы = Новый РеквизитФормы(ИмяРеквизита, ТипРеквизита, "",	ИмяРеквизита);
	ДобавляемыеРеквизиты.Добавить(РеквизитФормы);

 	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ДобавленныеРеквизиты.Добавить(ИмяРеквизита);
	
	Возврат РеквизитФормы;

КонецФункции

&НаСервере
Функция НоваяСтраница(ИмяСтраницы, Заголовок, Родитель)
	
	СтраницыФормы = Элементы[Родитель];
	
	Если Элементы.Найти(ИмяСтраницы) <> Неопределено Тогда
		Возврат Элементы.Найти(ИмяСтраницы);
	КонецЕсли;
	
	СтраницаФормы = Элементы.Добавить(ИмяСтраницы, Тип("ГруппаФормы"), СтраницыФормы);
	СтраницаФормы.Вид = ВидГруппыФормы.Страница;
	СтраницаФормы.Заголовок = Заголовок;
	
	ДобавленныеЭлементы.Добавить(ИмяСтраницы);
	
	Возврат СтраницаФормы;

КонецФункции

&НаСервере
Процедура ДобавитьТабличноеПоле(ИмяЭлемента, ПутьКДанным, РодительЭлемента)
	
	Если Элементы.Найти(ИмяЭлемента) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементФормы = Элементы.Добавить(ИмяЭлемента, Тип("ПолеФормы"), РодительЭлемента);
	
	ЭлементФормы.Заголовок = ИмяЭлемента;
	ЭлементФормы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементФормы.Вид = ВидПоляФормы.ПолеТабличногоДокумента;
	ЭлементФормы.ПутьКДанным = ПутьКДанным;
	
	ДобавленныеЭлементы.Добавить(ИмяЭлемента);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПолеТекстовогоЭлемента(ИмяЭлемента, ПутьКДанным, РодительЭлемента)
	
	Если Элементы.Найти(ИмяЭлемента) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементФормы = Элементы.Добавить(ИмяЭлемента, Тип("ПолеФормы"), РодительЭлемента);
	
	ЭлементФормы.Заголовок = ИмяЭлемента;
	ЭлементФормы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементФормы.Вид = ВидПоляФормы.ПолеТекстовогоДокумента;
	ЭлементФормы.ПутьКДанным = ПутьКДанным;
	
	ДобавленныеЭлементы.Добавить(ИмяЭлемента);
	
КонецПроцедуры

&НаСервере
Функция РеквизитАналогичен(ИмяРеквизита, Тип)
	Попытка
		Возврат Тип(ЭтаФорма[ИмяРеквизита]) = Тип(Тип);
	Исключение
		Возврат Ложь;
	КонецПопытки;
КонецФункции

&НаСервере
Процедура УдалитьДобавленныеЭлементы()
	
	Для Каждого Элемент Из ДобавленныеЭлементы Цикл
		Если Элементы.Найти(Элемент.Значение) <> Неопределено Тогда
			Элементы.Удалить(Элементы[Элемент.Значение]);
		КонецЕсли;
	КонецЦикла;
	
	ИзменитьРеквизиты(, ДобавленныеРеквизиты.ВыгрузитьЗначения());
	
	ДобавленныеЭлементы.Очистить();
	ДобавленныеРеквизиты.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	Если ФормируемКодДокумента Тогда
		Элементы.МакетыДвижений.Видимость = Истина;
	Иначе
		Элементы.МакетыДвижений.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
