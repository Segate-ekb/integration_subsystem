////////////////////////////////////////////////////////////////////////////////
// ГенераторТестовыхДанных
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Функция - Подписчик
// 
// Возвращаемое значение:
//  СправочникСсылка.инт_Подписчики - Ссылка на фиктивного подписчика
//
Функция Подписчик(ТипПодписчика = Неопределено, КоличествоПопытокОтправки =0, ПаузаМеждуПопыткамиОбработки=0, Активен=Истина) Экспорт
    Данные = ЮТест.Данные();
    Конструктор = Данные.КонструкторОбъекта(Справочники.инт_Подписчики)
    .ФикцияОбязательныхПолей()
    .Установить("КоличествоПопытокОтправки", ?(КоличествоПопытокОтправки = 0, Данные.СлучайноеЧисло(1, 4), КоличествоПопытокОтправки))
    .Фикция("СпособПередачиИдентификатораСообщения")
    .Фикция("СпособПередачиИдентификатораПотока")
    .Установить("ПаузаМеждуПопыткамиОбработки", ПаузаМеждуПопыткамиОбработки)
    .Установить("Активен", Активен);
    Если НЕ ТипПодписчика = Неопределено Тогда
        Конструктор.Установить("ТипПодписчика", ТипПодписчика);
    Иначе
        Конструктор.Фикция("ТипПодписчика");
    КонецЕсли;
    
    Возврат Конструктор.Записать();
КонецФункции

Функция НеОшибочныйСтатусРассылкиСообщений() Экспорт
    Данные = ЮТест.Данные();
    ЭтотСтатусПодходит = Ложь;
    Пока Не ЭтотСтатусПодходит Цикл
        
        Статус = Данные.СлучайноеЗначениеПеречисления(Перечисления.инт_СтатусыРассылкиИсходящихСообщений);
        Если Не Перечисления.инт_СтатусыРассылкиИсходящихСообщений.ЭтоОшибочныйСтатус(Статус) Тогда
            ЭтотСтатусПодходит = Истина;
        КонецЕсли;
    КонецЦикла;
    
    Возврат Статус;
    
КонецФункции

Функция ОшибочныйСтатусРассылкиСообщений() Экспорт
    Данные = ЮТест.Данные();
    ЭтотСтатусПодходит = Ложь;
    Пока Не ЭтотСтатусПодходит Цикл
        
        Статус = Данные.СлучайноеЗначениеПеречисления(Перечисления.инт_СтатусыРассылкиИсходящихСообщений);
        Если Перечисления.инт_СтатусыРассылкиИсходящихСообщений.ЭтоОшибочныйСтатус(Статус) Тогда
            ЭтотСтатусПодходит = Истина;
        КонецЕсли;
    КонецЦикла;
    
    Возврат Статус;
    
КонецФункции

Функция ПотокДанныхИсходящий(Активен = Истина,КоличествоПопытокОбработки =0, ПаузаМеждуПопыткамиОбработки=0, Валидация = Ложь, ИдентификаторПотока = "") Экспорт
    
        НаправлениеПотока = Перечисления.инт_НаправлениеПотокаДанных.Исходящий;
        // Все тесты будут проводиться на одном тестовом документе, потому и тестовые данные будут обрабатываться одним обработчиком 
        ТекстОбработчика = "Результат = новый Соответствие;
        |Результат.Вставить(""uuid"", ИсходныеДанные.УникальныйИдентификатор());
        |Результат.Вставить(""date"", ИсходныеДанные.Дата);
        |Результат.Вставить(""text"", ИсходныеДанные.ДемоРеквизитСтрока);
        |МассивСтрок = Новый Массив;
        |Для каждого СтрокаТовары Из ИсходныеДанные.Тест Цикл
        |	МассивСтрок.Добавить(Новый Структура(""item, price, quantity"", СтрокаТовары.Название, СтрокаТовары.Цена, СтрокаТовары.Количество));
        |КонецЦикла;
        |Результат.Вставить(""items"", МассивСтрок);";
        
    Если НЕ ЗначениеЗаполнено(ИдентификаторПотока) Тогда
         ИдентификаторПотока = ЮТест.Данные().СлучайнаяСтрока(9);
    КонецЕсли;
   
    Данные = ЮТест.Данные();
    Конструктор = Данные.КонструкторОбъекта(Справочники.инт_ПотокиДанных)
   
    .Установить("ТекстОбработчика", ТекстОбработчика)
    .Установить("НаправлениеПотока", НаправлениеПотока)
    .Установить("Активен", Активен)
    .Установить("Валидация", Валидация)
    .Установить("КоличествоПопытокОбработки",?(КоличествоПопытокОбработки = 0, Данные.СлучайноеЧисло(1, 4), КоличествоПопытокОбработки))
    .Установить("ПаузаМеждуПопыткамиОбработки",ПаузаМеждуПопыткамиОбработки)
    .Установить("Код", ИдентификаторПотока);
    
    Конструктор.ТабличнаяЧасть("ПодписчикиПотока");
    
    Для сч = 1 По 3 Цикл
    	Конструктор.ДобавитьСтроку()
            .Установить("Подписчик", Подписчик());
    КонецЦикла;
    
    Возврат Конструктор.Записать();
    
КонецФункции

Функция ПотокДанныхВходящий(Активен = Истина,КоличествоПопытокОбработки =0, ПаузаМеждуПопыткамиОбработки=0, ИдентификаторПотока = "", АсинхроннаяОбработка = Истина) Экспорт
    
        НаправлениеПотока = Перечисления.инт_НаправлениеПотокаДанных.Входящий;
        // Все тесты будут проводиться на одном тестовом документе, потому и тестовые данные будут обрабатываться одним обработчиком 
        ТекстОбработчика = "Сообщить(""Привет из входящего потока данных"");";
    Если НЕ ЗначениеЗаполнено(ИдентификаторПотока) Тогда
         ИдентификаторПотока = ЮТест.Данные().СлучайнаяСтрока(9);
    КонецЕсли;
  
    Данные = ЮТест.Данные();
    Конструктор = Данные.КонструкторОбъекта(Справочники.инт_ПотокиДанных)
   
    .Установить("ТекстОбработчика", ТекстОбработчика)
    .Установить("НаправлениеПотока",НаправлениеПотока)
    .Установить("Активен",Активен)
    .Установить("АсинхроннаяОбработка",АсинхроннаяОбработка)
    .Установить("КоличествоПопытокОбработки",?(КоличествоПопытокОбработки = 0, Данные.СлучайноеЧисло(1, 4), КоличествоПопытокОбработки))
    .Установить("ПаузаМеждуПопыткамиОбработки",ПаузаМеждуПопыткамиОбработки)
    .Установить("Код", ИдентификаторПотока);

    Возврат Конструктор.Записать();
    
КонецФункции

Функция демо_Документ() Экспорт
    Конструктор = ЮТест.Данные().КонструкторОбъекта(Документы.демо_Документ);
    
    Конструктор
        .Фикция("ДемоРеквизитСтрока")
        .ТабличнаяЧасть("Тест");
    
    Для Инд = 1 По 10 Цикл
    Конструктор.ДобавитьСтроку()
        .Фикция("Название")
        .Фикция("Количество")
        .Фикция("Цена");
    КонецЦикла;
    
    Возврат Конструктор.Провести();
КонецФункции

Функция HTTPСервисЗапрос(ИдентификаторПотока="", ИдентификаторСообщения="", Тело=Неопределено) Экспорт
    
    Если Тело = Неопределено Тогда
        Тело = Новый Структура("hello", "world");
    КонецЕсли;

    Запрос = ЮТест.Данные().HTTPСервисЗапрос()
    
    .Метод("POST");
    Если ЗначениеЗаполнено(ИдентификаторПотока) Тогда
    	  Запрос.ДобавитьПараметрURL("flow_id", ИдентификаторПотока);
    КонецЕсли;
    Если ЗначениеЗаполнено(ИдентификаторСообщения) Тогда
    	  Запрос.ДобавитьПараметрURL("message_id", ИдентификаторСообщения);
    КонецЕсли;
    
    Если ТипЗнч(Тело) = Тип("Строка") Тогда
        Запрос.УстановитьТелоКакСтроку(Тело);
    Иначе
        Запрос.УстановитьТелоКакСтрокуJSON(Тело)
    КонецЕсли;
    
    Возврат запрос;
КонецФункции

Функция HTTPСервисОтвет(КодСостояния, Тело) Экспорт

    Ответ = Новый HTTPСервисОтвет(КодСостояния);
    Ответ.УстановитьТелоИзСтроки(Тело);
    Возврат Ответ;
КонецФункции

Функция СхемаДанных(Внешняя = Истина) Экспорт
	Конструктор = ЮТест.Данные().КонструкторОбъекта(Справочники.инт_Схемы);
	Конструктор
		.ФикцияОбязательныхПолей()
		.Установить("Внешняя", Внешняя)
		.Установить("СсылкаНаСхему", "https://cloud.1cdevelopers.ru/s/6LNdep8exoRq6yK/download/openapi.json");
		
	Возврат Конструктор;
КонецФункции
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
