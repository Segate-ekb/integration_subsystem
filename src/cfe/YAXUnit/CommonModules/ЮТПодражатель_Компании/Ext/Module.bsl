//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2023 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Возвращает случайное имя компании
//
// Возвращаемое значение:
//	Строка
Функция Наименование() Экспорт

	СловарьИмен = СловарьИменаКомпаний();
	СловарьПрефиксов = СловарьПрефиксыИменКомпаний();

	Возврат СтрШаблон(
		"%1 %2",
		ЮТПодражательСлужебный.СлучайноеЗначениеИзСловаря(СловарьПрефиксов),
		ЮТПодражательСлужебный.СлучайноеЗначениеИзСловаря(СловарьИмен)
	);

КонецФункции

// Формирует случайный валидный ИНН РФ.
//
// Параметры:
//  КодРегиона - Строка - Код региона это первые две цифры кода ИНН. Список действующих кодов регионов
//    можно подсмотреть: https://www.nalog.gov.ru/html/docs/kods_regions.doc . Если код региона задан
//    как "00" то будет сформирован случайный код региона. По умолчанию "00" (случайный код региона)
//  ЭтоИННФизическогоЛица - Булево - Если Истина, то это ИНН физического лица, иначе ИНН юридического
//    лица. По умолчанию Ложь (ИНН юридического лица)
//
// Возвращаемое значение:
//  Строка - Случайный ИНН, например 3444140904
Функция ИНН(КодРегиона = "00", ЭтоИННФизическогоЛица = Ложь) Экспорт
	
	ЦифрыИНН = Новый Массив;
	
	ДобавитьВИННКодПодразделенияФНС(ЦифрыИНН, КодРегиона);
	
	ДобавитьВИНННомерНалогоплательщикаФНС(ЦифрыИНН, ЭтоИННФизическогоЛица);
	
	ДобавитьВИННПроверочныйКод(ЦифрыИНН, ЭтоИННФизическогоЛица);
	
	Результат = СтрСоединить(ЦифрыИНН);
	
	Возврат Результат;
	
КонецФункции

// Формирует случайный валидный КПП РФ
//
// Параметры:
//  КодНалоговогоОргана - Строка - Код налогового органа, первые четыре цифры КПП.
//  Причина - Число - Причина постановки. Поддерживаются следующие варианты
//    # 2 - код причины "43" постановка на учет филиала российской организации
//    # любая другая цифра - код причины "01" постановка на учет российской организации
//    по месту ее нахождения
//
// Возвращаемое значение:
//  Строка - Случайный КПП, например 344401001
Функция КПП(КодНалоговогоОргана = "0000", Причина = 1) Экспорт

	СлучайныйКПП = "";

	Если ТипЗнч(Причина) = Тип("Число") И Причина = 2 Тогда
		Код2 = "43";
	Иначе
		Код2 = "01";
	КонецЕсли;

	Если ТипЗнч(КодНалоговогоОргана) = Тип("Строка") И СтрДлина(КодНалоговогоОргана) = 4 Тогда
		Код1 = КодНалоговогоОргана;
	Иначе
		МассивКод1 = Новый Массив(4);
		Для Индекс = 0 По МассивКод1.ВГраница() Цикл
			МассивКод1.Установить(Индекс, ЮТТестовыеДанные.СлучайноеЧисло(0, 9, 0));
		КонецЦикла;
		Код1 = СтрСоединить(МассивКод1);
	КонецЕсли;

	Код3 = "00" + Строка(ЮТТестовыеДанные.СлучайноеЧисло(0, 9, 0));

	СлучайныйКПП = СтрШаблон("%1%2%3", Код1, Код2, Код3);

	Возврат СлучайныйКПП;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяРеализации()
	Возврат "Компании";
КонецФункции

Функция СловарьИменаКомпаний()
	Возврат ЮТПодражательСлужебный.Словарь(ИмяРеализации(), "Наименования");
КонецФункции

Функция СловарьПрефиксыИменКомпаний()
	Возврат ЮТПодражательСлужебный.Словарь(ИмяРеализации(), "ПрефиксыНаименований");
КонецФункции

#Область ИНН
Процедура ДобавитьВИННКодПодразделенияФНС(ЦифрыИНН, КодРегиона)
	
	Если КодРегиона = "00"
		Или СтрДлина(КодРегиона) <> 2 Тогда
		ПерваяЦифра = СлучайнаяЦифраИНН();
		ВтораяЦифра = СлучайнаяЦифраИНН();
	Иначе
		Типизатор = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1, 0, ДопустимыйЗнак.Неотрицательный));
		
		ПерваяЦифра = Типизатор.ПривестиЗначение(Сред(КодРегиона, 1, 1));
		ВтораяЦифра = Типизатор.ПривестиЗначение(Сред(КодРегиона, 2, 1));
	КонецЕсли;
	
	// Код региона
	ЦифрыИНН.Добавить(ПерваяЦифра);
	ЦифрыИНН.Добавить(ВтораяЦифра);
	
	// Код подразделения в регионе
	ЦифрыИНН.Добавить(СлучайнаяЦифраИНН());
	ЦифрыИНН.Добавить(СлучайнаяЦифраИНН());
	
КонецПроцедуры

Процедура ДобавитьВИНННомерНалогоплательщикаФНС(ЦифрыИНН, ЭтоИННФизическогоЛица)
	
	Если ЭтоИННФизическогоЛица Тогда
		КоличествоЦифрВНомере = 6;
	Иначе
		КоличествоЦифрВНомере = 5;
	КонецЕсли;
	
	Для Счетчик = 1 По КоличествоЦифрВНомере Цикл
		ЦифрыИНН.Добавить(СлучайнаяЦифраИНН());
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьВИННПроверочныйКод(ЦифрыИНН, ЭтоИННФизическогоЛица)
	
	Если ЭтоИННФизическогоЛица Тогда
		
		// Весовой коэффициент для 12-значного ИНН (первое контрольное число)
		ВесовыеКоэффициенты = ВесовыеКоэффициентыИНН(11);
		ПроверочнаяЦифра = ПроверочнаяЦифраИНН(ЦифрыИНН, ВесовыеКоэффициенты);
		ЦифрыИНН.Добавить(ПроверочнаяЦифра);
		
		// Весовой коэффициент для 12-значного ИНН (второе контрольное число)
		ВесовыеКоэффициенты = ВесовыеКоэффициентыИНН(12);
		ПроверочнаяЦифра = ПроверочнаяЦифраИНН(ЦифрыИНН, ВесовыеКоэффициенты);
		ЦифрыИНН.Добавить(ПроверочнаяЦифра);
		
	Иначе
		
		// Весовой коэффициент для 10-значного ИНН
		ВесовыеКоэффициенты = ВесовыеКоэффициентыИНН(10);
		ПроверочнаяЦифра = ПроверочнаяЦифраИНН(ЦифрыИНН, ВесовыеКоэффициенты);
		ЦифрыИНН.Добавить(ПроверочнаяЦифра);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ВесовыеКоэффициентыИНН(НомерПроверочныйЦифрыИНН)
	
	Результат = Новый Массив;
	
	Если НомерПроверочныйЦифрыИНН = 12 Тогда
		Результат.Добавить(3);
	КонецЕсли;
	
	Если НомерПроверочныйЦифрыИНН = 12
		Или НомерПроверочныйЦифрыИНН = 11 Тогда
		Результат.Добавить(7);
	КонецЕсли;

	Результат.Добавить(2);
	Результат.Добавить(4);
	Результат.Добавить(10);
	Результат.Добавить(3);
	Результат.Добавить(5);
	Результат.Добавить(9);
	Результат.Добавить(4);
	Результат.Добавить(6);
	Результат.Добавить(8);
	
	Возврат Результат;
	
КонецФункции

Функция ПроверочнаяЦифраИНН(ЦифрыИНН, ВесовыеКоэффициенты)
	
	Сумма = 0;
	
	ГраницаКоэффициентов = ВесовыеКоэффициенты.ВГраница();
	Для Индекс = 0 По ГраницаКоэффициентов Цикл
		Сумма = Сумма + ЦифрыИНН[Индекс] * ВесовыеКоэффициенты[Индекс];
	КонецЦикла;
	
	Результат = Сумма % 11;
	
	Если Результат = 10 Тогда
		Результат = 0;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СлучайнаяЦифраИНН()
	
	Результат = ЮТТестовыеДанные.СлучайноеЧисло(0, 9, 0);
	
	Возврат Результат;
	
КонецФункции
#КонецОбласти

#КонецОбласти
